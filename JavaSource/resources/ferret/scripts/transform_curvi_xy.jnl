! transform_curvi_xy.jnl
! 
! 18-Apr-2007 extracted from LAS_transforms.jnl
! Set up for curvilinear XY data: curvilinear-to-rectilinear regridding
! or native curvilinear xy plots or sample from curvilinear coordinates.


IF `($constraint_0_lhs"0|Curv_to_Rect_Regridding>1|*>0")` THEN 
   DEFINE SYMBOL data_var_transform = , Regridded Curvilinear to Rectilinear ($constraint_0_rhs)-degree Grid,
      DEFINE SYMBOL data_dup_varname ($data_var)_regrid

ELIF  `STRINDEX("($ferret_view)", "xy") EQ 0` THEN
   IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN 
      DEFINE SYMBOL data_var_transform = , Interpolated from Curvilinear coordinates
      DEFINE SYMBOL data_dup_varname ($data_var)_sample
   ENDIF

ELSE
   IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN \
      DEFINE SYMBOL data_var_transform = , Interpolated from Curvilinear coordinates
ENDIF

! Check the regions in x and y. The Sampling schemes work best if they
! dont have to do moduloing; reset x regions to try to match the longitude
! coordinate variable, and call the initialize_region script again.

! For large datasets, we might pre-set these in the initialization script. 
! Compute them only if undefined

IF `($curv_lon_min"1|*>0")` THEN \
   DEFINE SYMBOL curv_lon_min = `($ferret_curvi_coord_lon)[d=($data_num),x=@min,y=@min]`
IF `($curv_lon_max"1|*>0")` THEN \
   DEFINE SYMBOL curv_lon_max = `($ferret_curvi_coord_lon)[d=($data_num),x=@max,y=@max]`

! e.g. tripolar grid where the range is 0 to 426 messes up the calculations...

IF `($curv_lon_max)- ($curv_lon_min) GT 360` THEN DEFINE SYMBOL curv_lon_max `($curv_lon_min) + 360`

IF `($region_x_lo) LT ($curv_lon_min)` THEN 
   DEFINE SYMBOL region_0_x_lo `($region_0_x_lo) + 360`
   DEFINE SYMBOL region_0_x_hi `($region_0_x_hi) + 360`
   GO LAS_initialize_region 0
ENDIF
IF `($region_x_hi) GT ($curv_lon_max)` THEN 
   DEFINE SYMBOL region_0_x_lo `($region_0_x_lo) - 360`
   DEFINE SYMBOL region_0_x_hi `($region_0_x_hi) - 360`

   GO LAS_initialize_region 0

   IF `($region_x_lo) LT ($curv_lon_min)` THEN  ! still crosses branch cut
      DEFINE SYMBOL two_slices 1  ! will need to use all xcoords (get_curv_coord_box)
   ENDIF
ENDIF

! curvilinear to rectilinear regridding

IF `($constraint_0_lhs"0|Curv_to_Rect_Regridding>1|*>0")` THEN
   IF ($region_t"0|*>1") THEN DEFINE SYMBOL reg_zt ($region_t)
   IF ($region_z"0|*>1") THEN DEFINE SYMBOL reg_zt ($region_z)
   IF ($region_zt"0|*>1") THEN DEFINE SYMBOL reg_zt ($region_zt)
   GO transform_curv_to_rect.jnl

! Define upper-right labels with the url

DEFINE SYMBOL n_right_labels = 0
IF `STRINDEX("($data_0_url)","http") NE 0` THEN

   LET last_slash_0 = `STRRINDEX("($data_0_url)", "/")`
   DEFINE SYMBOL data_0_urlpath = `SUBSTRING("($data_0_url)", 0, last_slash_0)`

   DEFINE SYMBOL n_right_labels = `($n_right_labels)+1`
   DEFINE SYMBOL upper_right_($n_right_labels) = "DODS URL: ($data_0_urlpath)/"

ELSE
   DEFINE SYMBOL n_right_labels = `($n_right_labels)+1`
   DEFINE SYMBOL upper_right_($n_right_labels) = DATA SET: @AC`($data_var)[d=($data_num)],RETURN=dset`
ENDIF

ELSE

! Set up native curvilinear plots

   LET/TITLE="Longitude coordinates"/units=degrees xcoord = ($ferret_curvi_coord_lon)[d=($data_num)]
   LET/TITLE="Latitude coordinates"/units=degrees  ycoord = ($ferret_curvi_coord_lat)[d=($data_num)]

!   Native plot in XY

   IF ($ferret_view"|xy>1|*>0") THEN

     IF `($constraint_0_lhs"1|Curv_to_Rect_Regridding>0|*>1")` THEN  ! Do this setup if NOT curv-to-rect regridding
        DEFINE SYMBOL native_curvilinear_xy = 1

        ! Get the range of I,J needed to grab the region. Use it to define the plot or output variable.
!        GO get_curv_coord_bbox

        ! Remove this  and uncomment the above to set index range by region
        DEFINE SYMBOl clon_imin = 1
        DEFINE SYMBOl clon_imax = `($ferret_curvi_coord_lon)[d=($data_num),j=1,x=@ngd]`
        DEFINE SYMBOl clat_jmin = 1
        DEFINE SYMBOl clat_jmax = `($ferret_curvi_coord_lon)[d=($data_num),i=1,y=@ngd]`
        
        LET/TITLE="Longitude coordinates"/units=degrees xcoord = \
          ($ferret_curvi_coord_lon)[d=($data_num),i=($clon_imin):($clon_imax),j=($clat_jmin):($clat_jmax)]
        LET/TITLE="Latitude coordinates"/units=degrees  ycoord = \ 
          ($ferret_curvi_coord_lat)[d=($data_num),i=($clon_imin):($clon_imax),j=($clat_jmin):($clat_jmax)]

        LET/d=($data_num) plot_var = ($data_var)[d=($data_num),i=($clon_imin):($clon_imax),j=($clat_jmin):($clat_jmax)]
        IF ($region_zt"0|*>1") THEN LET/d=($data_num) plot_var = \
        ($data_var)[d=($data_num),i=($clon_imin):($clon_imax),j=($clat_jmin):($clat_jmax),($region_zt)]

        DEFINE SYMBOL ferret_plot_var ($expr1)plot_var($expr2), xcoord, ycoord
        IF ($region_zt"0|*>1") THEN DEFINE SYMBOL ferret_plot_var ($expr1)plot_var($expr2), xcoord, ycoord
        DEFINE SYMBOL ferret_plot_title "($expr1)($data_title)($expr2) (($data_units)) Native Curvilinear Plot"

        IF ($expr3"0|*>1") THEN
           DEFINE SYMBOL ferret_plot_var ($expr1)plot_var($expr3)plot_var($expr4), xcoord, ycoord
           IF ($region_zt"0|*>1") THEN DEFINE SYMBOL ferret_plot_var \
             ($expr1)plot_var($expr3)plot_var($expr4), xcoord, ycoord
           DEFINE SYMBOL ferret_plot_title "($expr1)($data_title)($expr3)($data_title)($expr4) (($data_units)) Native Curvilinear Plot"
        ENDIF

        DEFINE SYMBOL ferret_curvi_quals MODULO
        IF ($region_x_lo"0|*>1") THEN DEFINE SYMBOL ferret_curvi_quals \   
          ($ferret_curvi_quals)/HLIM=($region_x_lo):($region_x_hi)
        IF ($region_y_lo"0|*>1") THEN DEFINE SYMBOL ferret_curvi_quals \   
          ($ferret_curvi_quals)/VLIM=($region_y_lo):($region_y_hi)

        IF ($region_z"0|*>1") THEN DEFINE SYMBOL ferret_curvi_quals  \   
          ($ferret_curvi_quals)/($region_z)
        IF ($region_t"0|*>1") THEN DEFINE SYMBOL ferret_curvi_quals  \   
          ($ferret_curvi_quals)/($region_t)
        CANCEL SYMBOL ferret_use_graticule

     ENDIF  ! setup for NOT curv-to-rect regridding

! Or set up for native slices: Sample in the X and Y directions of the slice

   ELIF `STRLEN ("($ferret_view)") LE 2` THEN

     IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN 
        GO curvi_sample_slice.jnl
        DEFINE SYMBOL data_var_transform = , Interpolated from Curvilinear coordinates
     ELSE
        GO curvi_nrst_slice.jnl
     ENDIF

   ENDIF

ENDIF  ! native curvilinear plots or slices.

! End of $RCSfile ------------transform_curvi_xy.jnl-------------------------------

