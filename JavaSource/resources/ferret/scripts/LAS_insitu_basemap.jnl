\cancel mode verify
! LAS_insitu_basemap.jnl

! 
! $Author: ansley 
! $Date: 2006/07/18

! based on std_refmap.jnl
! Description: draw an underlay basemap for insitu plot.

! arguments:	               1    
! Usage: GO LAS_insitu_basemap [magnify]
!
! For example:
!	yes? go LAS_insitu_basemap  
! and
!	yes? go LAS_insitu_basemap 2

! this script assumes that the viewport has already been set/window opened
! The region symbols are assumed to include space surrounding the scattered points

SET DATA/SAVE

DEFINE SYMBOL region_xy =  x=($region_x_lo):($region_x_hi),y=($region_y_lo):($region_y_hi)

! Always just use etopo20. 
USE etopo20 

LET basemap_xmin = ($region_x_lo)
LET basemap_xmax = ($region_x_hi)
LET basemap_ymin = ($region_y_lo)
LET basemap_ymax = ($region_y_hi)
DEFINE SYMBOL region_xy =  x=`basemap_xmin`:`basemap_xmax`,y=`basemap_ymin`:`basemap_ymax`

! If there is a symbol set to expand the reference map, then check
! how much land is in the region chosen by the above logic. If its 
! less than a threshold, then expand the region in steps of 10 deg
! in x and 5 in y until the percent land is over that threshold

DEFINE SYMBOL ferret_expand_refmap = 1

IF ($ferret_expand_refmap"0|*>1") THEN

   LET threshold = 0.03
   LET mask = IF rose[($region_xy)] GT 0 THEN rose[($region_xy)]

   LET gooddat = mask[x=@NGD,y=@NGD]
   LET alldat = rose[x=`basemap_xmin`:`basemap_xmax`@NGD,y=`basemap_ymin`:`basemap_ymax`@NGD]
   LET pct = gooddat/alldat

   IF `pct LT threshold` THEN 
      REPEAT/RANGE=1:8/NAME=m (\
         LET basemap_xmin = `basemap_xmin -  5`; \
         LET basemap_xmax = `basemap_xmax +  5`; \
         LET basemap_ymin = `basemap_ymin - 10`; \
         LET basemap_ymax = `basemap_ymax + 10`; \
         LET alldat = rose[x=`basemap_xmin`:`basemap_xmax`@NGD,y=`basemap_ymin`:`basemap_ymax`@NGD]; \
         LET mask = IF rose[x=`basemap_xmin`:`basemap_xmax`,y=`basemap_ymin`:`basemap_ymax`] GT 0 \
            THEN rose[x=`basemap_xmin`:`basemap_xmax`,y=`basemap_ymin`:`basemap_ymax`]; \
         IF `pct GT threshold` THEN EXIT/LOOP) )
   ENDIF
ENDIF

DEFINE SYMBOL region_xy =  x=`basemap_xmin`:`basemap_xmax`,y=`basemap_ymin`:`basemap_ymax`

! set plot qualifiers for graticules, margins

IF ($ferret_use_graticules"1|0|*>1) THEN
  SET MODE GRATICULE:(DASH,COLOR=black)
ENDIF

! Set up for degrees-minutes-seconds labels on axes with units of degrees

IF ($ferret_deg_min_sec"0|false>0|*>1") THEN GO LAS_set_deg_min_sec.jnl

! For Filled land, set the palette to grey; for outline make it white. Draw
! the shade plot. This way we get graticules and axes and margins set up.
! For an outline plot, just call land below and we are set.

IF ($ferret_land_type"1|none>0|contour>0|filled>1|default>1|*>0") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/PALETTE=grey
ELIF ($ferret_land_type"0|none>0|contour>1|filled>0|*>0") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/PALETTE=white
ENDIF

show sym qualifiers

! draw gray shaded continents
! known potential bug: if X region begins at, say, 0E then magnification 3
!	or greater results in negative modulo subscripts

shade($qualifiers)/lev=(0,10000,10000)/nokey/nolab/set rose[($region_xy)]
   IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
   IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
PPL SHADE

cancel data etopo20
set data/restore

IF ($ferret_land_type"1|none>0|contour>0|filled>1|default>1|*>0") THEN
  GO LAS_fland " " 20
ELIF ($ferret_land_type"0|none>0|contour>1|filled>0|*>0") THEN
  GO land
ENDIF
set mode/last verify
