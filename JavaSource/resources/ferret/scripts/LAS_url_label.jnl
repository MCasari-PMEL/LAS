! LAS_url_label.jnl


! Define upper-right labels with the url; the URL is not plotted automatically when the var is
! (the same thing may be done in transform scripts or in differencing setup; that is ok

IF `($n_right_labels"0") EQ 0` THEN DEFINE SYMBOL n_right_labels = 0
IF `($data_$1_url"0|*>1") EQ 0` THEN EXIT/SCRIPT

IF `STRINDEX("($data_$1_url)","http") NE 0` THEN

   IF ($data_$1_dataset_url"0|*>1") THEN 
      DEFINE SYMBOL data_$1_urlpath = ($data_$1_dataset_url)
   ELSE
      DEFINE SYMBOL data_$1_urlpath = ($data_$1_url)
   ENDIF

   DEFINE SYMBOL n_right_labels = `($n_right_labels)+1`
   DEFINE SYMBOL upper_right_($n_right_labels) = DODS URL: ($data_$1_urlpath)

ELSE
   DEFINE SYMBOL n_right_labels = `($n_right_labels)+1`
   DEFINE SYMBOL upper_right_($n_right_labels) = DATA SET: `($data_var)[d=($data_num)],RETURN=dset`
ENDIF

! If its really long, split into multiple lines at the location of a slash.

LET slen = STRLEN ("($upper_right_($n_right_labels))")
IF `slen GT 80` THEN
   LET slen2 = INT(slen/2)
   LET last_slash = `STRRINDEX("($upper_right_($n_right_labels))", "/")`

   IF `last_slash GT slen2` THEN
      DEFINE SYMBOL upper_right_first = ($upper_right_($n_right_labels))
      REPEAT/RANGE=1:5 (\
        DEFINE SYMBOL upper_right_first = `SUBSTRING("($upper_right_first)", 0, last_slash-1)` ;\
        LET last_slash = `STRRINDEX("($upper_right_first)", "/")`;\
        IF `last_slash LE slen2` THEN EXIT/LOOP)
   ENDIF

   DEFINE SYMBOL upper_right_first = `SUBSTRING("($upper_right_($n_right_labels))", 0, last_slash)`
   DEFINE SYMBOL upper_right_last = `SUBSTRING("($upper_right_($n_right_labels))", last_slash+1, slen-last_slash)`
   DEFINE SYMBOL upper_right_($n_right_labels) = ($upper_right_first)/<NL>@AC($upper_right_last)
ENDIF

DEFINE SYMBOL upper_right_($n_right_labels) = @AC($upper_right_($n_right_labels))


! For comparison plots get rid of the long ugly URL labels at the upper right.
! This could be an option...
IF ($operation_service_action"0|Compare_Plot>1|*>0") THEN DEFINE SYMBOL n_right_labels = 0

! End of $RCSfile ------------LAS_url_label.jnl--------------------------
