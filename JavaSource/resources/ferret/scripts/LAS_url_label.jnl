! LAS_url_label.jnl


! Define upper-right labels with the url; the URL is not plotted automatically when the var is
! (the same thing may be done in transform scripts or in differencing setup; that is ok
! 1/2010 fix ticket 745 - immediate-mode expressions where the string contains an = sign
!        trip up the command parsing. Break these definitions into two steps.

IF `($n_right_labels"0") EQ 0` THEN DEFINE SYMBOL n_right_labels = 0
!IF `($data_$1_url"0|*>1") EQ 0` THEN EXIT/SCRIPT


! Instead of the URL use the dataset name or ID passed in to the 
! (addXML gets this from the title attribute from the original dataset
! dataset if that attrib exists)

LET got_dataset_label = 0

IF `got_dataset_label EQ 0` THEN
   IF `($data_$1_dataset_id"0|*>1")+($data_$1_dataset_name"0|*>1") GT 0` THEN \
      DEFINE SYMBOL n_right_labels = `($n_right_labels)+1`
   
   IF ($data_$1_dataset_id"0|*>1") THEN
     DEFINE SYMBOL upper_right_($n_right_labels) = DATA SET: ($data_$1_dataset_id)
     DEFINE SYMBOL upper_right_text_($n_right_labels) = DATA SET: ($data_$1_dataset_id)
     LET got_dataset_label = 1
   ENDIF
   IF ($data_$1_dataset_name"0|*>1") THEN
     DEFINE SYMBOL upper_right_($n_right_labels) = DATA SET: ($data_$1_dataset_name)
     DEFINE SYMBOL upper_right_text_($n_right_labels) = DATA SET: ($data_$1_dataset_name)
     LET got_dataset_label = 1
   ENDIF
ENDIF

! If we didnt get a label from the above, use the URL

IF `got_dataset_label EQ 0` THEN
   IF `STRINDEX("($data_$1_url)","http") NE 0` THEN
   
      IF ($data_$1_dataset_url"0|*>1") THEN 
         DEFINE SYMBOL data_$1_urlpath = ($data_$1_dataset_url)
      ELSE
         DEFINE SYMBOL data_$1_urlpath = ($data_$1_url)
      ENDIF
   
      DEFINE SYMBOL n_right_labels = `($n_right_labels)+1`
      DEFINE SYMBOL upper_right_($n_right_labels) = DODS URL: ($data_$1_urlpath)
      DEFINE SYMBOL upper_right_text_($n_right_labels) = DODS URL ($data_$1_urlpath)
   ELSE
      DEFINE SYMBOL n_right_labels = `($n_right_labels)+1`
      DEFINE SYMBOL upper_right_($n_right_labels) = DATA SET: `($data_var)[d=($data_num)],RETURN=dset`
      DEFINE SYMBOL upper_right_($n_right_labels) = DATA SET: ($data_$1_url)
      DEFINE SYMBOL upper_right_text_($n_right_labels) = DATA SET: ($data_$1_url)
   ENDIF
   
ENDIF

! If its really long, split into multiple lines at the location of a slash.
   
LET slen = STRLEN ("($upper_right_($n_right_labels))")

IF `slen GT 80` THEN
   LET slen2 = INT(slen/2)
   LET last_slash = STRRINDEX("($upper_right_($n_right_labels))", "/")

   IF `last_slash GT slen2` THEN
!      LET upper_right_first = "($upper_right_($n_right_labels))"
!      DEFINE SYMBOL upper_right_first = `upper_right_first`
      DEFINE SYMBOL upper_right_first = ($upper_right_($n_right_labels))
      REPEAT/RANGE=1:5 (\
        LET upper_right_first = SUBSTRING("($upper_right_first)", 0, last_slash-1) ;\
        DEFINE SYMBOL upper_right_first = `upper_right_first` ;\
        LET last_slash = STRRINDEX("($upper_right_first)", "/");\
        IF `last_slash LE slen2` THEN EXIT/LOOP)
   ENDIF

   IF `last_slash GT 0` THEN 
      LET upper_right_first = SUBSTRING("($upper_right_($n_right_labels))", 0, last_slash)
      DEFINE SYMBOL upper_right_first = `upper_right_first`
      LET upper_right_last = SUBSTRING("($upper_right_($n_right_labels))", last_slash+1, slen-last_slash)
      DEFINE SYMBOL upper_right_last = `upper_right_last`
      DEFINE SYMBOL upper_right_($n_right_labels) = ($upper_right_first)/<NL>($upper_right_last)
!      DEFINE SYMBOL upper_right_($n_right_labels) = ($upper_right_first)/<NL>@AS($upper_right_last)
   ENDIF
ENDIF


DEFINE SYMBOL upper_right_($n_right_labels) = @AS($upper_right_($n_right_labels))
IF `($n_right_labels"0") GT 1` THEN \
   DEFINE SYMBOL upper_right_($n_right_labels) = @P2($upper_right_($n_right_labels))

! For comparison plots get rid of the long ugly URL labels at the upper right.
! (DO this in the difference scripts!??

IF `got_dataset_label EQ 0` THEN
   IF ($operation_service_action"0|Compare_Plot>1|*>0") THEN DEFINE SYMBOL n_right_labels = 0
ENDIF

! End of $RCSfile ------------LAS_url_label.jnl--------------------------
