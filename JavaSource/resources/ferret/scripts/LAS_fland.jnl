!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: LAS_fland.jnl,v $
! $Revision: 1.1 $
! 
! $Author: ansley $
! $Date: 2006/07/06 17:54:11 $
!
! LAS_fland.jnl automatically adjusts the resolution
!
! $1 = ignore the user specified resolution
! $2 = palette

DEFINE SYMBOL fland_palette ($2"gray")

LET lon_range = `($region_X_hi) - ($region_X_lo)`
LET lat_range = `($region_Y_hi) - ($region_Y_lo)`
LET fland_area = `ABS(lon_range * lat_range) / (360 * 180)`

! If a tiny region then use the etopo2 landmask from PMEL server
! (see the Jons_fland.jnl under the SOCAT customization for use of
! etopo1. Need to decide how to make it available - on the Ferret thredds
! server as below?)

IF `fland_area LT 0.005` THEN
  
   SET DATA/SAVE

   DEF SYM xlo_hack ($xaxis_min%0%)
   DEF SYM xhi_hack ($xaxis_max%360%)
   DEF SYM ylo_hack ($yaxis_min%-90%)
   DEF SYM yhi_hack ($yaxis_max%90%)

   DEF SYM map_reg = [x=($xlo_hack):($xhi_hack),y=($ylo_hack):($yhi_hack)]

   DEFINE SYMBOL land_levels = (0,10000,10000)

   IF `TEST_OPENDAP("http://ferret.pmel.noaa.gov/thredds/dodsC/data/PMEL/landmask02.nc") EQ 0` THEN 
     USE "http://ferret.pmel.noaa.gov/thredds/dodsC/data/PMEL/landmask02.nc"
     DEFINE SYMBOL toponame = `landmask,RETURN=dset`
     SET VAR/name=rose landmask
     DEFINE SYMBOL land_levels = (1,2,1)
   ELSE
      USE etopo05
      DEFINE SYMBOL toponame = etopo05
   ENDIF

   SHADE/NOLAB/NOKEY/LEV=($land_levels)/PAL=gray/OVERLAY rose($map_reg)
   CAN DATA ($toponame)
   SET DATA/RESTORE
   DEFINE SYMBOL fland_done = 1

ELIF `fland_area LT 0.04` THEN
  DEFINE SYMBOL fland_resolution 05
ELSE
  DEFINE SYMBOL fland_resolution 20
ENDIF

IF `($fland_done"0|*>1") EQ 0` THEN \
GO fland ($fland_resolution) ($fland_palette)

! End of $RCSfile: LAS_fland.jnl,v $ -----------------------------------------------------
