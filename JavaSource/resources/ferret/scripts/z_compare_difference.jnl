!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: z_compare_difference.jnl,v $
! $Revision: 1.6 $
! 
! $Author: ansley $
! $Date: 2007/02/02 $
! 3/7/2007: ACM. Put code to check that the region was not too small, resulting 
!           in degenerate plot into LAS_initialize_data.jnl rather than in the 
!           individual plot scripts.
! 3/29/2007 ACM Fix logic interpreting contents of ferret_interpolate_data 

! z_compare_difference.jnl creates a Z line plot for use with the Back 
! End Server code that sits behind a Live Access Server (LAS). 
! It produces a difference plot of 2 variables which are on the same
! z grid
!
! Set any Ferret modes

IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL fview = `UPCASE("($ferret_view)")`

! Define symbols associated with the region and data and perform any
! initialization needed for this dataset.

GO LAS_initialize_region 0
GO ($ferret_init_script"LAS_initialize_data|*>*") 0

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small; reduces to a single grid point.
! (Region is a point)
GO LAS_check_1d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small; reduces to a single grid point.
! (Region is a point)
GO LAS_check_1d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Here is variable 0 and its title

DEFINE SYMBOL ferret_var_0 = ($ferret_plot_var)
DEFINE SYMBOL ferret_title_0 = ($ferret_plot_title"($data_var)")

! Initialize next dataset. 
! Assume the interface has returned correct, matching regions in Z

! Apply any expression only to the first variable not to this one.
CANCEL SYMBOL ferret_expression 

GO LAS_initialize_region 1
GO ($ferret_init_script"LAS_initialize_data|*>*") 1

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Here is variable 1 and its title

DEFINE SYMBOL ferret_var_1 = ($ferret_plot_var)
DEFINE SYMBOL ferret_title_1 = ($ferret_plot_title"($data_var)")

! Compare the Z axes of the two varibles. They must be the same.

GO LAS_compare_axes "($data_0_var)[d=($data_0_num)]" "($data_1_var)[d=($data_1_num)]" ($fview)

! Check for errors 

IF ($error_status"0|*>1") THEN   
   DEFINE SYMBOL error_string = ($fview) grid of ($data_1_var) must match ($fview) grid of ($data_0_var)
   MESSAGE/ERROR **ERROR ($error_string)
   EXIT/PROGRAM
ENDIF

! Define the difference variable 

DEFINE SYMBOL ferret_diff_var = ($ferret_var_0)  -  ($ferret_var_1)

! Set the title, and the labels for the upper left listing the locations
! and time in directions normal to the plot.

GO LAS_set_diff_labels ($fview)

! Open the window, apply size, scale options
GO LAS_open_window

! Define the plot variable

! TODO:  Should the creation of the ($qualifiers) symbol for standard
! TODO:  1D plots be separated out in a separate script called:
! TODO:    LAS_1D_plot_qualifiers.jnl
!
! Build up the plot qualifiers

IF ($ferret_use_graticules"1|0|*>1) THEN
  SET MODE GRATICULE:(DASH,COLOR=black)
ENDIF

! Line color

IF `($ferret_line_color%0|default>0|*>1%)` THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/COLOR=($ferret_line_color%0|default>black|red|blue|green|lightblue|purple|*>black%)
ENDIF

! symbols

IF `($ferret_line_or_sym%0|default>1|*>1%)` THEN
   DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_line_or_sym%/LINE|default>/LINE|line>/LINE|both>/LINE/SYM|sym>/SYM%)
ENDIF

! line thickness

IF `($ferret_line_thickness%0|default>1|*>1%)` THEN
   DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_line_thickness%/THICK=1|default>/THICK=1|1>/THICK=1|2>/THICK=2|3>/THICK=3|*>/THICK=1%)
ENDIF

! Do the plot. 

PLOT($qualifiers)/set/title = "($ferret_diff_title)" ($ferret_diff_var)
IF `($labnum_x%0|*>1%)` THEN GO unlabel ($labnum_x)
IF `($labnum_y%0|*>1%)` THEN GO unlabel ($labnum_y)
IF `($labnum_t%0|*>1%)` THEN GO unlabel ($labnum_t)
  IF `($title_size%0|*>1%)` THEN PPL LABSET ($title_size)
PPL plot

LABEL/NOUSER `($ppl$xlen)/2`, `($ppl$ylen)+1.2`, 0, 0, 0.12, "LAS 7.+/Ferret ($FERRET_VERSION)     NOAA/PMEL"
IF ($needs_url"0|*>1") THEN \
LABEL/NOUSER `($ppl$xlen)*.33`, `($ppl$ylen)+1.`, -1, 0, 0.10, "DATA SET: @AS($data_var_url)"

! Put the labels in the upper left

LET labdel = 0.2
LET labyloc = `($ppl$ylen) + labdel*($n_left_labels) `
LET labsiz = 0.1

LABEL/NOUSER 0, `labyloc`, -1, 0.0, `labsiz`, ($upper_left_1)
LET labyloc = `labyloc - labdel`
IF `($n_left_labels) GT 1` THEN LABEL/NOUSER 0, `labyloc`, -1, 0.0, `labsiz`, ($upper_left_2)

LET labyloc = `labyloc - labdel`
IF `($n_left_labels) GT 2` THEN LABEL/NOUSER 0, `labyloc`, -1, 0.0, `labsiz`, ($upper_left_3)

! Put any URL labels in the upper right

IF `($n_right_labels) GT 0` THEN
   LET labxloc = `($ppl$xlen)`
   LET labdel = 0.2
   LET labsiz = 0.1
   LET labyloc = `($ppl$ylen) + labdel*($n_left_labels) `  ! start with loc of left-side labels
   LET labyloc = `labyloc + labdel*($n_right_labels)`

   LABEL/NOUSER `labxloc`, `labyloc`, 1, 0.0, `labsiz`, ($upper_right_1)
   LET labyloc = `labyloc - labdel`
   IF `($n_right_labels) GT 1` THEN LABEL/NOUSER `labxloc`, `labyloc`, 1, 0.0, `labsiz`, ($upper_right_2)
ENDIF 

! Save the results
GO LAS_results xline

! End of $RCSfile -----------------------------------------------------

