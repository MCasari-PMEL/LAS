! LAS_check_2d_region.jnl
! 
! Author: ansley
! $Date: 2007/06/22
! ACM 8/7/2007 - Apply expressions here.
!              - Save symbols with information about too-small regions
!                for use by labels_above_plot.jnl
!              - If this is to be an overlay plot, and region is too small
!                then overlay cant be done. Reset number of datasets to 1.
!
! Check whether the region is too small to make a 2D plot on this grid.
! Call after region and dataset have been initialized for 2D plot.

! Change ($data_var)[d=($data_num)] to the_plot_var_noregion

CANCEL SYMBOL region_small*

LET/D=($data_num) the_plot_var_noregion = ($ferret_plot_var_noregion)

IF ($data_analysis_expr"0|*>1") THEN
   DEFINE SYMBOL apply_analysis_expr = ($data_analysis_expr)
ENDIF

! Do not make the check for native curvlinear or hybrid z plots.

IF `($do_curvi_xy"0") AND ($native_curvilinear_xy"0")` THEN EXIT/SCRIPT
IF `($do_hybrid_z"0") AND ($native_Z_plot"0")` THEN EXIT/SCRIPT
IF `($do_curvi_xy_and_hybrid_z)` THEN EXIT/SCRIPT

IF `($do_curvi_xy"0") OR ($do_hybrid_z"0") OR ($do_curvi_xy_and_hybrid_z"0")` THEN EXIT/SCRIPT

! If doing a slice of a curvilinear variable then note that RETURN=shape is not
! always correct for these variables 
! IF `STRINDEX ("($ferret_plot_var)", "sampled_var") GT 0` THEN EXIT/SCRIPT !(dont exit, test works for some cases)

LET/D=($data_num) the_plot_var = ($ferret_plot_var)
LOAD the_plot_var

DEFINE SYMBOL varshape = `the_plot_var,RETURN=shape`
DEFINE SYMBOL viewshape =  `UPCASE("($ferret_view)")`

IF `STRCMP("($varshape)", "POINT") EQ 0` THEN
!  DEFINE SYMBOL error_status = ERROR
!  DEFINE SYMBOL error_type = REGION
!  DEFINE SYMBOL error_action = Checking grid and region
!  DEFINE SYMBOL error_string = The ($viewshape) region is too small to draw a 2D plot
!  GO LAS_error_result
!  EXIT/PROGRAM

   DEFINE SYMBOL ferret_set_aspect = no
   DEFINE SYMBOL ferret_use_graticules = 0
   IF ($ferret_contour_style"0|contour_lines>1|*>0) THEN 
      DEFINE SYMBOL ferret_palette = white
      DEFINE SYMBOL qualifiers = ($qualifiers)/NOKEY
   ENDIF
   DEFINE SYMBOL ferret_contour_style  = raster

   DEFINE SYMBOL axlab_command AXLINT 50,50 
   DEFINE SYMBOL tnmlab_command TXNMTC,0
   DEFINE SYMBOL axtic_command TICS 0 0 0 0
   DEFINE SYMBOL ferret_size = 0.4
!   DEFINE SYMBOL axlen_command AXLEN,4,4
!   go margins " " 4 4
 SET WINDOW/ASPECT = 1

   DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
   DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested region is smaller than 1 grid cell]
   DEFINE SYMBOL region_small_($n_left_labels"0") = yes

   IF `STRINDEX("($viewshape)", "X") GT 0` THEN
      LET ilo = `i[gx=the_plot_var,x=($region_x_lo)] - 1`
      LET ihi = `ilo + 2`

      DEFINE SYMBOL var_xmod = "`($data_var),return=xmod`"  
      IF  `STRCMP (" ", ($var_xmod)) EQ 0` THEN
         LET xmod = 0
         CANCEL SYMBOL FER_LAST_ERROR
      ELSE
         LET xmod = 1
      ENDIF

      IF `ilo LT 0 AND xmod EQ 0` THEN 
         LET ilo = 1
         LET ihi = 3
      ENDIF
   ENDIF

   IF `STRINDEX("($viewshape)", "Y") GT 0` THEN
      LET jlo = MAX(1, `j[gy=the_plot_var,y=($region_y_lo)] - 1`)
      LET jhi = jlo + 2
      LET jmax = `($data_var),return=jsize`
      IF `jhi GT jmax` THEN 
        LET jhi = jmax
        LET jlo = jhi - 2
      ENDIF
   ENDIF
   IF `STRINDEX("($viewshape)", "Z") GT 0` THEN
      LET klo = MAX(1, `k[gz=the_plot_var,z=($region_z_lo)] - 1`)
      LET khi = klo + 2
      LET kmax = `($data_var),return=ksize`
      IF `khi GT kmax` THEN 
        LET khi = kmax
        LET klo = khi - 2
      ENDIF
   ENDIF
   IF `STRINDEX("($viewshape)", "T") GT 0` THEN
      LET llo = MAX(1, `l[gt=the_plot_var,t=($region_t_lo)] - 1`)
      LET lhi = llo + 2
      LET lmax = `($data_var),return=lsize`
      IF `lhi GT lmax` THEN 
        LET lhi = lmax
        LET llo = lhi - 2
      ENDIF
   ENDIF

   IF `STRINDEX("($viewshape)", "XY") GT 0` THEN
      IF ($region_zt"0|*>1") THEN 
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[I=`ilo`:`ihi`,J=`jlo`:`jhi`($data_analysis_expr),($region_zt)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[I=`ilo`:`ihi`,J=`jlo`:`jhi`($data_analysis_expr)]
      ENDIF
   ENDIF

   IF `STRINDEX("($viewshape)", "XZ") GT 0` THEN
      IF ($region_yt"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[I=`ilo`:`ihi`,K=`klo`:`khi`($data_analysis_expr),($region_yt)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[I=`ilo`:`ihi`,K=`klo`:`khi`($data_analysis_expr)]
      ENDIF
   ENDIF

   IF `STRINDEX("($viewshape)", "XT") GT 0` THEN
      IF ($region_yz"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[I=`ilo`:`ihi`,L=`llo`:`lhi`($data_analysis_expr),($region_yz)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[I=`ilo`:`ihi`,L=`llo`:`lhi`($data_analysis_expr)]
      ENDIF
      DEFINE SYMBOL axtype_command TXTYPE,mon,day 
      DEFINE SYMBOL axtype_command TXTYPE,yr,mon 
   ENDIF

   IF `STRINDEX("($viewshape)", "YZ") GT 0` THEN
      IF ($region_xt"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[J=`jlo`:`jhi`,K=`klo`:`khi`($data_analysis_expr),($region_xt)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[J=`jlo`:`jhi`,K=`klo`:`khi`($data_analysis_expr)]
      ENDIF
   ENDIF

   IF `STRINDEX("($viewshape)", "YT") GT 0` THEN
      IF ($region_xz"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[J=`jlo`:`jhi`,L=`llo`:`lhi`($data_analysis_expr),($region_xz)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[J=`jlo`:`jhi`,L=`llo`:`lhi`($data_analysis_expr)]
      ENDIF
      DEFINE SYMBOL axtype_command TXTYPE,mon,day 
      DEFINE SYMBOL axtype_command TXTYPE,yr,mon 
   ENDIF

   IF `STRINDEX("($viewshape)", "ZT") GT 0` THEN
      IF ($region_xy"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[K=`klo`:`khi`L=`llo`:`lhi`($data_analysis_expr),($region_xy)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[K=`klo`:`khi`L=`llo`:`lhi`($data_analysis_expr)]
      ENDIF
      DEFINE SYMBOL axtype_command TXTYPE,mon,day 
      DEFINE SYMBOL axtype_command TXTYPE,yr,mon 
   ENDIF

ENDIF   ! degenerated to a point.

! Requested regions that degenerate to a line.

IF `STRLEN("($varshape)") EQ 1` THEN

!  DEFINE SYMBOL error_status = ERROR
!  DEFINE SYMBOL error_type = REGION
!  LET indx = `STRINDEX("($viewshape)", "($varshape)")`
!  IF `indx EQ 1` THEN DEFINE SYMBOL shortdir = `SUBSTRING ("($viewshape)" , 2, 1)`
!  IF `indx EQ 2` THEN DEFINE SYMBOL shortdir = `SUBSTRING ("($viewshape)" , 1, 1)`
!  DEFINE SYMBOL error_action = Checking grid and region
!  DEFINE SYMBOL error_string = The requested ($shortdir) region is too small to draw a 2D plot
!  GO LAS_error_result
!  EXIT/PROGRAM


DEFINE SYMBOL ferret_set_aspect = no
DEFINE SYMBOL ferret_use_graticules = 0
IF ($ferret_contour_style"0|contour_lines>1|*>0) THEN 
   DEFINE SYMBOL ferret_palette = white
   DEFINE SYMBOL qualifiers = ($qualifiers)/NOKEY
ENDIF
DEFINE SYMBOL ferret_contour_style  = raster
DEFINE SYMBOL ferret_size = 0.4
DEFINE SYMBOL tnmlab_command TXNMTC,0

IF `STRINDEX("($viewshape)", "XY") GT 0` THEN
   IF ($varshape"0|Y>1|*>0") THEN 
      LET addx = xbox[gx=the_plot_var,x=($region_x_lo)]
      IF ($region_zt"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         X=`($region_x_lo)-addx`:`($region_x_hi)+addx`($data_analysis_expr),($region_y),($region_zt)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         X=`($region_x_lo)-addx`:`($region_x_hi)+addx`($data_analysis_expr),($region_y)]
      ENDIF

      DEFINE SYMBOL axlab_command AXLINT 10, 
      DEFINE SYMBOL axtic_command TICS -0,0
      DEFINE SYMBOL axlen_command AXLEN,2
      
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested X region is smaller than 1 grid cell]
      
      DEFINE SYMBOL region_small_($n_left_labels"0") = yes

   ENDIF

   IF ($varshape"0|X>1|*>0") THEN 
      LET addy = ybox[gy=the_plot_var,y=($region_y_lo)]
      LET has_vlim = STRINDEX("($ferret_curvi_quals%0%)", "VLIM")
      IF `STRCMP("($region_y_lo)", "($region_y_hi)") NE 0 AND \
          has_vlim EQ 0` THEN \
             DEFINE SYMBOL qualifiers = \
             ($qualifiers)/VLIM=`($region_y_lo)`:`($region_y_hi)`

      IF ($region_zt"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         Y=`($region_y_lo)-addy`:`($region_y_hi)+addy`($data_analysis_expr),($region_x),($region_zt)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         Y=`($region_y_lo)-addy`:`($region_y_hi)+addy`($data_analysis_expr),($region_x)]
      ENDIF

      DEFINE SYMBOL axlab_command AXLINT ,10 
      DEFINE SYMBOL axtic_command TICS ,,0,0
      DEFINE SYMBOL axlen_command AXLEN,,2
      
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested Y region is smaller than 1 grid cell]
      DEFINE SYMBOL region_small_($n_left_labels"0") = yes
   ENDIF

   GO margins 2 2 2 2

ENDIF !XY

IF `STRINDEX("($viewshape)", "XZ") GT 0` THEN
   IF ($varshape"0|Z>1|*>0") THEN 
      LET addx = xbox[gx=the_plot_var,x=($region_x_lo)]
      IF ($region_yt"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         X=`($region_x_lo)-addx`:`($region_x_hi)+addx`($data_analysis_expr),($region_z),($region_yt)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         X=`($region_x_lo)-addx`:`($region_x_hi)+addx`($data_analysis_expr),($region_z)]
      ENDIF

      DEFINE SYMBOL axlab_command AXLINT 10, 
      DEFINE SYMBOL axtic_command TICS -0,0
      DEFINE SYMBOL axlen_command AXLEN,2
      
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested X region is smaller than 1 grid cell]
      DEFINE SYMBOL region_small_($n_left_labels"0") = yes

   ENDIF

   IF ($varshape"0|X>1|*>0") THEN 
      LET addz = zbox[gz=the_plot_var,z=($region_z_lo)]
      LET has_vlim = STRINDEX("($ferret_curvi_quals%0%)", "VLIM")
      IF `STRCMP("($region_z_lo)", "($region_z_hi)") NE 0 AND \
          has_vlim EQ 0` THEN \
             DEFINE SYMBOL qualifiers = \
             ($qualifiers)/VLIM=`($region_z_lo)`:`($region_z_hi)`

      IF ($region_yt"0|*>1") THEN 
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         Z=`($region_z_lo)-addz`:`($region_z_hi)+addz`($data_analysis_expr),($region_x),($region_yt)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         Z=`($region_z_lo)-addz`:`($region_z_hi)+addz`($data_analysis_expr),($region_x)]
      ENDIF

      DEFINE SYMBOL axlab_command AXLINT ,10 
      DEFINE SYMBOL axtic_command TICS ,,0,0
      DEFINE SYMBOL axlen_command AXLEN,,2
      
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested Z region is smaller than 1 grid cell]
      DEFINE SYMBOL region_small_($n_left_labels"0") = yes
   ENDIF

   GO margins 2 2 2 2

ENDIF ! xz

IF `STRINDEX("($viewshape)", "XT") GT 0` THEN
   IF ($varshape"0|T>1|*>0") THEN 
      LET addx = xbox[gx=the_plot_var,x=($region_x_lo)]
      IF ($region_yz"0|*>1") THEN 
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         X=`($region_x_lo)-addx`:`($region_x_hi)+addx`($data_analysis_expr),($region_t),($region_yz)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         X=`($region_x_lo)-addx`:`($region_x_hi)+addx`($data_analysis_expr),($region_t)]
      ENDIF

      DEFINE SYMBOL axlab_command AXLINT 10, 
      DEFINE SYMBOL axtic_command TICS -0,0
      DEFINE SYMBOL axlen_command AXLEN,2
      
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested X region is smaller than 1 grid cell]
      DEFINE SYMBOL region_small_($n_left_labels"0") = yes

   ENDIF

   IF ($varshape"0|X>1|*>0") THEN 
      LET addt = tbox[gt=the_plot_var,t=($region_t_lo)]
      LET tt = T[GT=($data_var),d=($data_num)]
      LET tlo = `TT[T=($region_t_lo)@ITP]`
      LET thi = `TT[T=($region_t_hi)@ITP]`
      LET has_vlim = STRINDEX("($ferret_curvi_quals%0%)", "VLIM")
      IF `STRCMP("($region_t_lo)", "($region_t_hi)") NE 0 AND \
          has_vlim EQ 0` THEN \
             DEFINE SYMBOL qualifiers = \
             ($qualifiers)/VLIM=`tlo`:`thi`

      IF ($region_yz"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         T=`tlo-addt`:`thi+addt`($data_analysis_expr),($region_x),($region_yz)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         T=`tlo-addt`:`thi+addt`($data_analysis_expr),($region_x)]
      ENDIF

      DEFINE SYMBOL axlab_command AXLINT ,10 
      DEFINE SYMBOL axtic_command TICS ,,0,0
      DEFINE SYMBOL axlen_command AXLEN,,2
      DEFINE SYMBOL axtype_command TXTYPE,mon,day 
      DEFINE SYMBOL axtype_command TXTYPE,yr,mon 
      
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested T region is smaller than 1 grid cell]
      DEFINE SYMBOL region_small_($n_left_labels"0") = yes
   ENDIF

!   GO margins 2 2 2 2

ENDIF ! XT

IF `STRINDEX("($viewshape)", "YZ") GT 0` THEN

   IF ($varshape"0|Z>1|*>0") THEN 
      LET addy = ybox[gy=the_plot_var,y=($region_y_lo)]
      IF ($region_xt"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         Y=`($region_y_lo)-addy`:`($region_y_hi)+addy`($data_analysis_expr),($region_z),($region_xt)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         Y=`($region_y_lo)-addy`:`($region_y_hi)+addy`($data_analysis_expr),($region_z)]
      ENDIF

      DEFINE SYMBOL axlab_command AXLINT 10, 
      DEFINE SYMBOL axtic_command TICS -0,0
      DEFINE SYMBOL axlen_command AXLEN,2
      
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested Y region is smaller than 1 grid cell]
      DEFINE SYMBOL region_small_($n_left_labels"0") = yes

   ENDIF

   IF ($varshape"0|Y>1|*>0") THEN 
      LET addz = zbox[gz=the_plot_var,z=($region_z_lo)]
      LET has_vlim = STRINDEX("($ferret_curvi_quals%0%)", "VLIM")
      IF `STRCMP("($region_z_lo)", "($region_z_hi)") NE 0 AND \
          has_vlim EQ 0` THEN \
             DEFINE SYMBOL qualifiers = \
             ($qualifiers)/VLIM=`($region_z_lo)`:`($region_z_hi)`

      IF ($region_xt"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         Z=`($region_z_lo)-addz`:`($region_z_hi)+addz`($data_analysis_expr),($region_y),($region_xt)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         Z=`($region_z_lo)-addz`:`($region_z_hi)+addz`($data_analysis_expr),($region_y)]
      ENDIF

      DEFINE SYMBOL axlab_command AXLINT ,10 
      DEFINE SYMBOL axtic_command TICS ,,0,0
      DEFINE SYMBOL axlen_command AXLEN,,2
      
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested Z region is smaller than 1 grid cell]
      DEFINE SYMBOL region_small_($n_left_labels"0") = yes
   ENDIF
message GO MARGINS for YZ
   GO margins 2 2 2 2

ENDIF ! YZ


IF `STRINDEX("($viewshape)", "YT") GT 0` THEN
   IF ($varshape"0|T>1|*>0") THEN 
      LET addy = ybox[gy=the_plot_var,y=($region_y_lo)]
      LET has_vlim = STRINDEX("($ferret_curvi_quals%0%)", "VLIM")
      IF `STRCMP("($region_y_lo)", "($region_y_hi)") NE 0 AND \
          has_vlim EQ 0` THEN \
             DEFINE SYMBOL qualifiers = \
             ($qualifiers)/VLIM=`($region_y_lo)`:`($region_y_hi)`

      IF ($region_xz"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         Y=`($region_y_lo)-addy`:`($region_y_hi)+addy`($data_analysis_expr),($region_t),($region_xz)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         Y=`($region_y_lo)-addy`:`($region_y_hi)+addy`($data_analysis_expr),($region_t)]
      ENDIF

      DEFINE SYMBOL axlab_command AXLINT 10, 
      DEFINE SYMBOL axtic_command TICS -0,0
      DEFINE SYMBOL axlen_command AXLEN,,2
      
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested Y region is smaller than 1 grid cell]
      DEFINE SYMBOL region_small_($n_left_labels"0") = yes

   ENDIF

   IF ($varshape"0|Y>1|*>0") THEN 
      LET addt = tbox[gt=the_plot_var,t=($region_t_lo)]

      LET tt = T[GT=($data_var),d=($data_num)]
      LET tlo = `TT[T=($region_t_lo)@ITP]`
      LET thi = `TT[T=($region_t_hi)@ITP]`
      IF ($region_xz"0|*>1") THEN 
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         T=`tlo-addt`:`thi+addt`($data_analysis_expr),($region_y),($region_xz)]
      ELSE
      DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
        T=`tlo-addt`:`thi+addt`($data_analysis_expr),($region_y)]
      ENDIF

      DEFINE SYMBOL axlab_command AXLINT ,10 
      DEFINE SYMBOL axtic_command TICS ,,0,0
      DEFINE SYMBOL axlen_command AXLEN,2
      DEFINE SYMBOL axtype_command TXTYPE,mon,day 
      DEFINE SYMBOL axtype_command TXTYPE,yr,mon 
      
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested T region is smaller than 1 grid cell]
      DEFINE SYMBOL region_small_($n_left_labels"0") = yes
   ENDIF

   GO margins 2 2 2 2

ENDIF ! YT

IF `STRINDEX("($viewshape)", "ZT") GT 0` THEN
   IF ($varshape"0|T>1|*>0") THEN 
      LET addz = zbox[gz=the_plot_var,z=($region_z_lo)]
      LET has_vlim = STRINDEX("($ferret_curvi_quals%0%)", "VLIM")
      IF `STRCMP("($region_z_lo)", "($region_z_hi)") NE 0 AND \
          has_vlim EQ 0` THEN \
             DEFINE SYMBOL qualifiers = \
             ($qualifiers)/VLIM=`($region_z_lo)`:`($region_z_hi)`

      IF ($region_xy"0|*>1") THEN
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         Z=`($region_z_lo)-addz`:`($region_z_hi)+addz`($data_analysis_expr),($region_t),($region_xy)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         Z=`($region_z_lo)-addz`:`($region_z_hi)+addz`($data_analysis_expr),($region_t)]
      ENDIF

      DEFINE SYMBOL axlab_command AXLINT 10, 
      DEFINE SYMBOL axtic_command TICS -0,0
      DEFINE SYMBOL axlen_command AXLEN,2
      
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested Z region is smaller than 1 grid cell]
      DEFINE SYMBOL region_small_($n_left_labels"0") = yes

   ENDIF

   IF ($varshape"0|Z>1|*>0") THEN 
      LET addt = tbox[gt=the_plot_var,t=($region_t_lo)]

      LET tt = T[GT=($data_var),d=($data_num)]
      LET tlo = `TT[T=($region_t_lo)@ITP]`
      LET thi = `TT[T=($region_t_hi)@ITP]`
      IF ($region_xy"0|*>1") THEN 
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         T=`tlo-addt`:`thi+addt`($data_analysis_expr),($region_z),($region_xy)]
      ELSE
         DEFINE SYMBOL ferret_plot_var = the_plot_var_noregion[ \
         T=`tlo-addt`:`thi+addt`($data_analysis_expr),($region_z)]
      ENDIF

      DEFINE SYMBOL axlab_command AXLINT ,10 
      DEFINE SYMBOL axtic_command TICS ,,0,0
      DEFINE SYMBOL axlen_command AXLEN,,2
      DEFINE SYMBOL axtype_command TXTYPE,mon,day 
      DEFINE SYMBOL axtype_command TXTYPE,yr,mon 
      
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels"0") = @CR[The requested T region is smaller than 1 grid cell]
      DEFINE SYMBOL region_small_($n_left_labels"0") = yes
   ENDIF

   GO margins 2 2 2 2

ENDIF ! ZT

ENDIF

! If the region is smaller than a grid cell in one or more directions then
! we cannot make an overlay plot.
DEFINE SYMBOL region_small = `(($region_small_1"0|*>1") +($region_small_2"0|*>1") + ($region_small_3"0|*>1") )`

IF `($data_count"0") GT 1` THEN
   IF ($ferret_format"0|overlay>1|*>0") THEN 
      IF `($region_small"0") GT 0` THEN DEFINE SYMBOL data_count = 1
   ENDIF 
ENDIF

! End of file ------------LAS_check_2d_region.jnl-------------------------------

