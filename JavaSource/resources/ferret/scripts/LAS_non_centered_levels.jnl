! LAS_non_centered_levels.jnl
!
! Called by LAS_auto_levels.jnl and LAS_auto_levels_animate.jnl
! Compute levels based on data statistics already computed:
! stat_min, stat_max, stat_mean, stat_std
! Levels not constrained to be centered

! If num_fine_levels_contour has been set, use it to set how many
! contour levels per color level. Never more contours than colors
! on a contour-over-colors plot.


PPL %RANGE `($stat_mean)-nstd`, `($stat_mean)+nstd`, ($num_fine_levels)

LET lines_to_levels = 2
IF ($num_fine_levels_contour"0|*>1") THEN 
   LET lines_to_levels = INT( ($num_fine_levels)/($num_fine_levels_contour) )
   IF `lines_to_levels LT 1` THEN LET lines_to_levels = 1
ENDIF

DEFINE SYMBOL fine_lo = `MAX(($ppl$range_low), ($stat_min) )`
DEFINE SYMBOL fine_hi = `MIN(($ppl$range_high), ($stat_max) )`

! Use PPL RANGE to round these values off to nice numbers
PPL %RANGE ($fine_lo), ($fine_hi), ($num_fine_levels)

DEFINE SYMBOL fine_lo = ($ppl$range_low)
DEFINE SYMBOL fine_hi = ($ppl$range_high)
DEFINE SYMBOL fine_inc = ($ppl$range_inc)

DEFINE SYMBOL lines_fine_inc = `lines_to_levels*($fine_inc)`

DEFINE SYMBOL coarse_delta = `($fine_inc)*density_factor`
DEFINE SYMBOL coarse_lo = `($fine_lo) - ($half_num_coarse_levels) * ($coarse_delta)`
DEFINE SYMBOL coarse_hi = `($fine_hi) + ($half_num_coarse_levels) * ($coarse_delta)`

DEFINE SYMBOL lines_coarse_delta = `lines_to_levels*($coarse_delta)`
IF `STRINDEX("($lines_coarse_delta)", "+") GT 0` THEN GO remove_plus_exponent lines_coarse_delta ($lines_coarse_delta)

IF `( ($stat_min) GE ($coarse_lo) ) OR ( ($stat_max) LE ($coarse_hi)) ` THEN
   PPL %RANGE `($stat_min)`, `($stat_max)`, `2*($num_coarse_levels)`
   DEFINE SYMBOL lowest_lev = ($ppl$range_low)
   DEFINE SYMBOL highest_lev = ($ppl$range_high)
ENDIF

! Require that if there is (-inf) there is also (inf) and vice versa
! unless the data min or max lies inside the computed levels.
! In that case, chop off the levels at the right location and do 
! not have open levels on that end.

DEFINE SYMBOL matching_inf = 1
DEFINE SYMBOL lower_inf = (-inf)
DEFINE SYMBOL upper_inf = (inf)

IF `($stat_min) GT ($coarse_lo)` THEN

   LET alev = ($coarse_lo)
   REPEAT/RANGE=1:($num_coarse_levels)/NAME=ilev (\
      IF `($stat_min) LT alev` THEN EXIT/LOOP;\
      LET alev = `alev + ($coarse_delta)` )
   DEFINE SYMBOL coarse_lo = `alev`
   IF `($stat_min) LT ($coarse_lo)` THEN DEFINE SYMBOL coarse_lo = `($coarse_lo) - ($coarse_delta)`

   CANCEL SYMBOL lower_inf
   DEFINE SYMBOL matching_inf = 0

   IF `($coarse_lo) GT ($fine_lo)` THEN
      CANCEL SYMBOL coarse_lo
      LET alev = ($fine_lo)
      REPEAT/RANGE=1:($num_fine_levels)/NAME=ilev (\
      IF `($stat_min) GE alev` THEN EXIT/LOOP;\
      LET alev = `alev + ($fine_inc)` )

      LET fine_lo = `alev`
      IF `($stat_min) LT ($fine_lo)` THEN DEFINE SYMBOL fine_lo = `($fine_lo) - ($fine_inc)`
      IF `($fine_lo) LT ($fine_hi)` THEN
         CANCEL SYMBOL fine_lo
	 DEFINE SYMBOL fine_hi = `alev`
      ENDIF
   ENDIF

   IF `($coarse_lo"0|*>1") GE ($fine_lo"0|*>1")` THEN CANCEL SYMBOL coarse_lo 

ENDIF

IF `($stat_max) LT ($coarse_hi)` THEN

   LET alev = ($fine_hi)
   REPEAT/RANGE=1:($num_coarse_levels)/NAME=ilev (\
      IF `($stat_max) LE alev` THEN EXIT/LOOP;\
      LET alev = `alev + ($coarse_delta)` )
   DEFINE SYMBOL coarse_hi = `alev`
   
   IF `($stat_max) GT ($coarse_hi)` THEN DEFINE SYMBOL coarse_hi = `($coarse_hi) + ($coarse_delta)`

   CANCEL SYMBOL upper_inf

   DEFINE SYMBOL matching_inf = 0

   IF `($coarse_hi) LT ($fine_hi)` THEN
      CANCEL SYMBOL coarse_hi
      LET alev = ($fine_lo)
      REPEAT/RANGE=1:($num_fine_levels)/NAME=ilev (\
      IF `($stat_min) LE alev` THEN EXIT/LOOP;\
      LET alev = `alev + ($fine_inc)` )
      DEFINE SYMBOL fine_hi = `alev`
   ENDIF

   IF `($coarse_hi"0|*>1") LE ($fine_hi"0|*>1")` THEN CANCEL SYMBOL coarse_hi 

ENDIF

! Remove plus in exponent
! if levels symbols have exponents with + in them, then that + is
! lost in the animation sequence and Ferret chokes on levels symbols
! containing a blank when it goes to make the plots for frames.
IF ($remove_plus"0") THEN
   IF `STRINDEX("($fine_lo)", "+") GT 0` THEN GO remove_plus_exponent fine_lo ($fine_lo)
   IF `STRINDEX("($fine_hi)", "+") GT 0` THEN GO remove_plus_exponent fine_hi ($fine_hi)
   IF `STRINDEX("($fine_inc)", "+") GT 0` THEN GO remove_plus_exponent fine_inc ($fine_inc)
   IF `STRINDEX("($coarse_lo)", "+") GT 0` THEN GO remove_plus_exponent coarse_lo ($coarse_lo)
   IF `STRINDEX("($coarse_hi)", "+") GT 0` THEN GO remove_plus_exponent coarse_hi ($coarse_hi)
   IF `STRINDEX("($coarse_delta)", "+") GT 0` THEN GO remove_plus_exponent coarse_delta ($coarse_delta)
   IF `STRINDEX("($lowest_lev)", "+") GT 0` THEN GO remove_plus_exponent lowest_lev ($lowest_lev)
   IF `STRINDEX("($highest_lev)", "+") GT 0` THEN GO remove_plus_exponent highest_lev ($highest_lev)
   IF `STRINDEX("($lines_fine_inc)", "+") GT 0` THEN GO remove_plus_exponent lines_fine_inc ($lines_fine_inc)
   IF `STRINDEX("($lines_coarse_delta)", "+") GT 0` THEN GO remove_plus_exponent lines_coarse_delta ($lines_coarse_delta)
ENDIF

IF ($coarse_lo"0|*>1") THEN

   DEFINE SYMBOL ferret_auto_levels = \
($lower_inf)(($coarse_lo),($fine_lo),($coarse_delta))\
(($fine_lo),($fine_hi),($fine_inc))

   DEFINE SYMBOL ferret_auto_lines = \
($lower_inf)(($coarse_lo),($fine_lo),`($lines_coarse_delta)`)\
(($fine_lo),($fine_hi),($lines_fine_inc))
ELSE

   IF ($fine_lo"0|*>1") THEN
      DEFINE SYMBOL ferret_auto_levels = (($fine_lo),($fine_hi),($fine_inc))
      DEFINE SYMBOL ferret_auto_lines = (($fine_lo),($fine_hi),($lines_fine_inc))
   ENDIF

ENDIF

IF ($coarse_hi"0|*>1") THEN

   DEFINE SYMBOL ferret_auto_levels = \
($ferret_auto_levels)(($fine_hi),($coarse_hi),($coarse_delta))($upper_inf)
   DEFINE SYMBOL ferret_auto_lines = \
($ferret_auto_lines)(($fine_hi),($coarse_hi),($lines_coarse_delta))($upper_inf)

ENDIF

! Require that if there is (-inf) there is also (inf) and vice versa
IF ($matching_inf) THEN

   IF `STRINDEX("($ferret_auto_levels)", "(-inf)") GT 0` THEN
      IF `STRINDEX("($ferret_auto_levels)", "(inf)") EQ 0` THEN \
         DEFINE SYMBOL ferret_auto_levels = ($ferret_auto_levels)(inf)
   ENDIF
   IF `STRINDEX("($ferret_auto_lines)", "(-inf)") GT 0` THEN
      IF `STRINDEX("($ferret_auto_lines)", "(inf)") EQ 0` THEN \
         DEFINE SYMBOL ferret_auto_lines = ($ferret_auto_lines)(inf)
   ENDIF

   IF `STRINDEX("($ferret_auto_levels)", "(inf)") GT 0` THEN
      IF `STRINDEX("($ferret_auto_levels)", "(-inf)") EQ 0` THEN \
         DEFINE SYMBOL ferret_auto_levels = (-inf)($ferret_auto_levels)
   ENDIF
   IF `STRINDEX("($ferret_auto_lines)", "(inf)") GT 0` THEN
      IF `STRINDEX("($ferret_auto_lines)", "(-inf)") EQ 0` THEN \
         DEFINE SYMBOL ferret_auto_lines = (-inf)($ferret_auto_lines)
   ENDIF

ENDIF

! End of file ------------LAS_non_centered_levels.jnl--------------------------

