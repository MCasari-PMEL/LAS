go QC2_setup ($data_0_url)

! Year and day-of-year

! First draw a top axis labeled with # obs
go QC2_top_axis

! Plot  Year and day-of-year, each with its own vertical axis


let/title=Year yr = tax_year (tt,tt)
let/title="Day of Year" day = reshape(day_of_year, tt)


! Year

set view full
let ax1 = `yr[L=@min]-1`
let ax2 = `yr[L=@max]+1`
let dy = 1
let yint = 1
if `ax2-ax1 GT 6` then 
   let dy = 2
   let yint = 2
endif
plot($linestyle)/line/color=blue/axes=0,1,1,0/title=" "/set  yr
  ($taxis_setting)
  ppl axnsig,,0
  ppl yaxis,`ax1`,`ax2`,`dy`
  ppl axlint,,`yint`
  ppl ylab Year
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
ppl plot

! Day

set view full2

plot/axes=0,1,0,1/color=green/line($linestyle)/title=" "/set day
  ppl ylab Day
  ($taxis_setting)
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
  go unlabel ($labnum_dset)
  go unlabel ($labnum_datitl)
ppl plot

! Make the color key - draw a plot withlines off the page, but 
! with the Ferret key made by Ferret PLOT/KEY=TITLE
! Redefine color 2 to blue (and afterwards back to red)

define view/x=0:1/y=0:1 full3

set view full3
ppl color,2,0,0,100
plot($linestyle)/line/key=title/color/noax/set/vlim=1000:2000/L=1:5 yr, day
  ppl ylab " "
  ppl xlab " "
  ($taxis_setting)
  IF ($labnum_year"0|*>1") THEN go unlabel ($labnum_year)
  go unlabel ($labnum_dset)
  go unlabel ($labnum_datitl)
PPL PLOT

ppl color,2,100,0,0

ppl axnsig,2,2

def sym lab_top = Expocode `EXPOCODE`
label/nouser  `($ppl$xlen)/2`,`($ppl$ylen)+0.6*($ppl$yorg)`, 0,0,0.13,  ($lab_top)

DEFINE SYMBOL result_plot_image_filename = `expocode`_($result_plot_image_filename)
FRAME($trans)/FORMAT=gif/FILE="($result_plot_image_filename)"
