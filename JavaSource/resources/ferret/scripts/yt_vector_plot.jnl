!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! file: yt_vector_plot.jnl 
! 
! Author: ansley 
! Date: 2010/06/29
!
! yt_vector_plot.jnl creates a YT vector plot for use with the Back End Server
! code that sits behind a Live Access Server (LAS). 
!
! Calls LAS_vector_stickplot_t to plot the variable as stacked vector stick 
! plots at each time.

! Set any Ferret modes

IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL itsa_vector_plot = 1

! Define symbols associated with the region and data and perform any
! initialization needed for this dataset.

DEFINE SYMBOL var_num = 0
GO LAS_initialize_region ($var_num)
GO LAS_initialize_data ($var_num)
DEFINE SYMBOL ferret_vector_1 ($ferret_plot_var)
DEFINE SYMBOL ferret_plot_title_1 "($data_title) ($data_units)"

DEFINE SYMBOL var_num = 1
GO LAS_initialize_data ($var_num)
DEFINE SYMBOL ferret_vector_2 ($ferret_plot_var)
DEFINE SYMBOL ferret_plot_title "($ferret_plot_title_1), ($data_title) ($data_units)"

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small; reduces to a single grid point.
! (Region is a point)
GO LAS_check_2d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Open the window, apply size, scale options
GO LAS_open_window

! Build up the plot qualifiers

DEFINE SYMBOL qualifiers = ($qualifiers)/NOLAB

IF ($ferret_plot_key"0|0|nokey>0|*>1") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/NOKEY
ENDIF

! Set up for degrees-minutes-seconds labels on axes with units of degrees

IF ($ferret_deg_min_sec"0|false>0|*>1") THEN GO LAS_set_deg_min_sec.jnl

! Line color

IF `($ferret_line_color%0|default>1|*>2%) LT 2` THEN DEFINE SYMBOL ferret_line_color = blue

IF `($ferret_line_color%0|default>0|*>1%)` THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/COLOR=($ferret_line_color%0|default>black|red|blue|green|lightblue|purple|*>black%)
ENDIF

! line thickness

IF `($ferret_line_thickness%0|default>1|*>1%)` THEN
   DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_line_thickness%/THICK=1|default>/THICK=1|1>/THICK=1|2>/THICK=2|3>/THICK=3|*>/THICK=1%)
ENDIF
! Set the URL label for the first dataset.
GO LAS_url_label 0

GO LAS_vector_stickplot_t

! Save the results
GO LAS_results box

!  ----------------- End of yt_vector_plot.jnl ------------------------------------
