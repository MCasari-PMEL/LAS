!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: xy_compare_difference.jnl,v $
! 
! $Author: ansley $
! $Date: 2006/08/04 03:31:37 $
! 3/7/2007: ACM. Put code to check that the region was not too small, resulting 
!           in degenerate plot into LAS_initialize_data.jnl rather than in the 
!           individual plot scripts.
! 3/29/2007 ACM Fix logic interpreting contents of ferret_interpolate_data 
! 3/2008 ACM Use SHADE/TRIM for shade underlays of FILL plots (Ferret v6.1)

! xy_compare_difference.jnl creates an XY plot for use with the 
! Back End Server code that sits behind a Live Access Server (LAS). 
! It produces a difference plot of 2 variables which are on the same
! xy grid
!
! Set any Ferret modes
!
! NOTE:  Should we support any of the following Ferrt modes?
! NOTE:    ASCII_FONT, CALENDAR, DEPTH_LABEL, LABELS, LATIT_LABEL, LONG_LABEL 

IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL fview = `UPCASE("($ferret_view)")`

! Define symbols associated with the regign and data and perform any
! initialization needed for this dataset.
!
! NOTE:  Adding support for a 'base layer' variable would mean passing
! NOTE:  some other argument.  For now, though, we always use 'data_0'
! NOTE:  for the base layer and region.

GO LAS_initialize_region 0

GO LAS_initialize_data 0

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small to make a 2D plot on this grid.
GO LAS_check_2d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Here is variable 0 and its title

DEFINE SYMBOL ferret_var_0 = ($ferret_plot_var)
LET ferret_var_0_noregion = ($ferret_plot_var_noregion)
DEFINE SYMBOL ferret_title_0 = ($ferret_plot_title"($data_var)")

! Initialize next dataset. 
! (The interface guarantees that the match in X and Y)

! Apply any expression only to the first variable not to this one.
CANCEL SYMBOL ferret_expression 

GO LAS_initialize_region 1

GO LAS_initialize_data  1 

! Check for errors 

IF ($error_status"0|*>1") THEN
   MESSAGE/ERROR **ERROR ($error_string)
   EXIT/PROGRAM
ENDIF

! Here is variable 1 and its title

DEFINE SYMBOL ferret_var_1 = ($ferret_plot_var)
LET ferret_var_1_noregion = ($ferret_plot_var_noregion)
DEFINE SYMBOL ferret_title_1 = ($ferret_plot_title"($data_var)")

! Compare the X and Y axes of the two variables. They must be the same.

GO LAS_compare_axes "($data_0_var)[d=($data_0_num)]" "($data_1_var)[d=($data_1_num)]" ($fview)

! Check for errors 

IF ($error_status"0|*>1") THEN   
   DEFINE SYMBOL error_string = ($fview) grid of ($data_1_var) must match ($fview) grid of ($data_0_var)
   MESSAGE/ERROR **ERROR ($error_string)
   EXIT/PROGRAM
ENDIF

! Define the plot variable 

DEFINE SYMBOL ferret_diff_var = ($ferret_var_0) - ($ferret_var_1)

IF ($native_curvilinear_xy"0|*>1") THEN \
  DEFINE SYMBOL ferret_diff_var = ( ($ferret_plot_base_var_0)  - ($ferret_plot_base_var_1)), xcoord, ycoord


! Set the title, and the labels for the upper left listing the locations
! and time in directions normal to the plot, and in the upper right listing
! urls if dataset are opendap.

GO LAS_set_diff_labels ($fview)
DEFINE SYMBOL ferret_plot_title ($ferret_diff_title)

! Open the window, apply size, scale options
GO LAS_open_window

! TODO:  Should the creation of the ($qualifiers) symbol for standard
! TODO:  2D plots be separated out in a separate script called:
! TODO:    LAS_2D_plot_qualifiers.jnl
!
! Build up the plot qualifiers

! Get levels based on ferret_diff_var
GO LAS_auto_levels

! Build up the plot qualifiers

! Curvilinear plots may have more qualifiers. 
IF ($ferret_curvi_quals"0|*>1") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/($ferret_curvi_quals)
ENDIF

!ACM note: All color plots use $ferret_fill_levels. Contour plots use $ferret_contour_levels.

IF ($ferret_contour_style"0|contour_lines>1|*>0") THEN
   IF ($ferret_contour_levels"0|*>1") THEN
     DEFINE SYMBOL qualifiers = ($qualifiers)/LEVELS=($ferret_contour_levels)
   ENDIF
ELSE
   IF ($ferret_fill_levels"0|*>1") THEN
     DEFINE SYMBOL qualifiers = ($qualifiers)/LEVELS=($ferret_fill_levels)
   ENDIF
ENDIF

IF ($ferret_plot_key"0|0|nokey>0|*>1") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/NOKEY
ENDIF

IF ($ferret_use_graticules"1|0|*>1) THEN
  SET MODE GRATICULE:(DASH,COLOR=black)
ENDIF

DEFINE SYMBOL qualifiers = ($qualifiers)/PALETTE=($ferret_palette"rainbow|default>rainbow|*>*)


! Do the plot (SHADE or CONTOUR)
!
!ACM note: restore more types. Filled should be FILL plot; RASTER should be shade.
! DEFINE SYMBOL plot_type = ($ferret_contour_style"contour_lines>CONTOUR|*>SHADE")

DEFINE SYMBOL plot_type =($ferret_contour_style"FILL|default>FILL|raster>SHADE|color_filled_contours>FILL|color_filled_plus_lines>FILL|contour_lines>CONTOUR|raster_plus_lines>SHADE")

! For all FILL plots, we will first do a SHADE underlay to get the plot to 
! draw all the way to the edges.

DEFINE SYMBOL shade_underlay = FALSE
IF ($plot_type"0|FILL>1|*>0) THEN DEFINE SYMBOL shade_underlay = TRUE

! For a SHADE plot in curvilinear coordinates we may want the /MODULO qualifier
!IF `($ferret_curvi_quals"0|*>1") AND ($plot_type"0|SHADE>1|*>0)` THEN 
IF ($ferret_curvi_quals"0|*>1") THEN 
   IF ($ferret_lon_modulo"1|1|0|yes>1|no>0|*>0") THEN  DEFINE SYMBOL qualifiers = ($qualifiers)/MODULO
ENDIF

! Set up for degrees-minutes-seconds labels on independent variable axis
! If this was not requested, the script just returns.

IF ($ferret_deg_min_sec"0|false>0|*>1") THEN GO LAS_set_deg_min_sec.jnl

! Draw the plot
IF ($shade_underlay) THEN 

   SHADE($qualifiers)/TRIM/TITLE="($ferret_plot_title)"/SET ($ferret_diff_var)
      IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
      IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
      IF ($axlab_command"0|*>1") THEN PPL ($axlab_command)
      IF ($tnmlab_command"0|*>1") THEN PPL ($tnmlab_command)  
      IF ($axtic_command"0|*>1") THEN PPL ($axtic_command)
      IF ($axlen_command"0|*>1") THEN PPL ($axlen_command)
      IF ($axtype_command"0|*>1") THEN PPL ($axtype_command)
      if ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
      if ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
      if ($labnum_dods"0|*>1") THEN go unlabel ($labnum_dods)
      IF `($title_size%0|*>1%)` THEN PPL LABSET ($title_size)
   PPL SHADE
   IF `($LEV_NUM"0") GT 245/2` THEN PPL SHASET RESET
   ($plot_type)($qualifiers)/OVER/NOLAB ($ferret_diff_var)
ELSE
   ($plot_type)($qualifiers)/TITLE="($ferret_plot_title)"/SET ($ferret_diff_var)
      IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
      IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
      IF ($axlab_command"0|*>1") THEN PPL ($axlab_command)
      IF ($tnmlab_command"0|*>1") THEN PPL ($tnmlab_command)  
      IF ($axtic_command"0|*>1") THEN PPL ($axtic_command)
      IF ($axlen_command"0|*>1") THEN PPL ($axlen_command)
      IF ($axtype_command"0|*>1") THEN PPL ($axtype_command)
      if ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
      if ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
      if ($labnum_dods"0|*>1") THEN go unlabel ($labnum_dods)
      IF `($title_size%0|*>1%)` THEN PPL LABSET ($title_size)
   PPL ($plot_type)
ENDIF
   
! One of the results is the data min and max. Save those here.
   DEFINE SYMBOL data_plotted_min = ($ppl$zmin)
   DEFINE SYMBOL data_plotted_max = ($ppl$zmax)

LABEL/NOUSER `($ppl$xlen)/2`, `($ppl$ylen)+1.2`, 0, 0, 0.12, "LAS 7.+/Ferret ($FERRET_VERSION)     NOAA/PMEL"

! Put the labels for time/location in the upper left

! For comparison plots get rid of the long ugly URL labels at the upper right.
DEFINE SYMBOL n_right_labels = 0

GO labels_above_plot

IF ($ferret_contour_style"0|default>0|color_filled_plus_lines>1|raster_plus_lines>1|*>0") THEN
  IF ($ferret_contour_levels"0|*>1") THEN DEFINE SYMBOL levsym =  /LEVELS=($ferret_contour_levels)
  CONTOUR/OVER/NOLAB($levsym) ($ferret_diff_var)
ENDIF

! Overlay additional cartography data (land mask, outlines, rivers, ...)

IF ($ferret_land_type"1|none>0|contour>0|filled>1|default>1|*>0") THEN
  GO LAS_fland 
ELIF ($ferret_land_type"0|none>0|contour>1|filled>0|*>0") THEN
  GO land 
ENDIF

! Mark grid points. Options are no, subsample (tests if subsampling needed)
! or all to force marking all points.

IF ($ferret_mark_grid"0|no>0|all>1|subsample>1|*>0") THEN GO mark_grid

! Save the results
GO LAS_results box

! End of $RCSfile -----------------------------------------------------

