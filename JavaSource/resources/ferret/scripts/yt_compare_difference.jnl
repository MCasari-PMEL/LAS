!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: yt_compare_difference.jnl,v $
! 
! $Author: ansley $
! $Date: 2006/08/04 03:31:38 $
! 3/7/2007: ACM. Put code to check that the region was not too small, resulting 
!           in degenerate plot into LAS_initialize_data.jnl rather than in the 
!           individual plot scripts.
! 3/29/2007 ACM Fix logic interpreting contents of ferret_interpolate_data 

! yt_compare_difference.jnl creates an YT plot for use with the 
! Back End Server code that sits behind a Live Access Server (LAS). 
! It produces a difference plot of 2 variables which are on the same
! YT grid
!
! Set any Ferret modes
!
! NOTE:  Should we support any of the following Ferrt modes?
! NOTE:    ASCII_FONT, CALENDAR, DEPTH_LABEL, LABELS, LATIT_LABEL, LONG_LABEL 

IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL fview = `UPCASE("($ferret_view)")`

! Define symbols associated with the regign and data and perform any
! initialization needed for this dataset.
!
GO LAS_initialize_region 0
GO LAS_initialize_data 0

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small to make a 2D plot on this grid.
GO LAS_check_2d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Here is variable 0 and its title

DEFINE SYMBOL ferret_var_0 = ($ferret_plot_var)
DEFINE SYMBOL ferret_title_0 = ($ferret_plot_title"($data_var)")

! Initialize next dataset. 

IF `($data_count"0") LE 0` THEN
   DEFINE SYMBOL error_status = ERROR
   DEFINE SYMBOL error_type = DATA
   DEFINE SYMBOL error_string = This script requires two data variables
   MESSAGE/ERROR **ERROR ($error_string)
   EXIT/PROGRAM
ENDIF

! Apply any expression only to the first variable not to this one.
CANCEL SYMBOL ferret_expression 

GO LAS_initialize_region 1
GO LAS_initialize_data  1 

! Check for errors 

IF ($error_status"0|*>1") THEN
   MESSAGE/ERROR **ERROR ($error_string)
   EXIT/PROGRAM
ENDIF

! Here is variable 1 and its title

DEFINE SYMBOL ferret_var_1 = ($ferret_plot_var)
DEFINE SYMBOL ferret_title_1 = ($ferret_plot_title"($data_var)")

! Compare the Y and T axes of the two variables. They must be the same.

GO LAS_compare_axes "($data_0_var)[d=($data_0_num)]" "($data_1_var)[d=($data_1_num)]" ($fview)

! Check for errors 

IF ($error_status"0|*>1") THEN   
   DEFINE SYMBOL error_string = ($fview) grid of ($data_1_var) must match ($fview) grid of ($data_0_var)
   MESSAGE/ERROR **ERROR ($error_string)
   EXIT/PROGRAM
ENDIF

! Define the plot variable 

DEFINE SYMBOL ferret_diff_var = ($ferret_var_0) - ($ferret_var_1)

! Set the title, and the labels for the upper left listing the locations
! and time in directions normal to the plot.

GO LAS_set_diff_labels ($fview)

! Open the window, apply size, scale options
GO LAS_open_window

! TODO:  Should the creation of the ($qualifiers) symbol for standard
! TODO:  2D plots be separated out in a separate script called:
! TODO:    LAS_2D_plot_qualifiers.jnl
!
! Build up the plot qualifiers

!ACM note: All color plots use $ferret_fill_levels. Contour plots use $ferret_contour_levels.

IF ($ferret_contour_style"0|contour_lines>1|*>0") THEN
   IF ($ferret_contour_levels"0|*>1") THEN
     DEFINE SYMBOL qualifiers = ($qualifiers)/LEVELS=($ferret_contour_levels)
   ENDIF
ELSE
   IF ($ferret_fill_levels"0|*>1") THEN
     DEFINE SYMBOL qualifiers = ($qualifiers)/LEVELS=($ferret_fill_levels)
   ENDIF
ENDIF

IF ($ferret_plot_key"0|0|nokey>0|*>1") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/NOKEY
ENDIF

IF ($ferret_use_graticules"1|0|*>1) THEN
  SET MODE GRATICULE:(DASH,COLOR=black)
ENDIF

IF ($ferret_palette"0|*>1") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/PALETTE=($ferret_palette"rainbow|default>rainbow|*>*)
ENDIF

! Do the plot (SHADE or CONTOUR)
!
!ACM note: restore more types. Filled should be FILL plot; RASTER should be shade.
! DEFINE SYMBOL plot_type = ($ferret_contour_style"contour_lines>CONTOUR|*>SHADE")

DEFINE SYMBOL plot_type =($ferret_contour_style"FILL|default>FILL|raster>SHADE|color_filled_contours>FILL|color_filled_plus_lines>FILL|contour_lines>CONTOUR|raster_plus_lines>SHADE")

! Set up for degrees-minutes-seconds labels on independent variable axis
! If this was not requested, the script just returns.

IF ($ferret_deg_min_sec"0|false>0|*>1") THEN GO LAS_set_deg_min_sec.jnl

($plot_type)($qualifiers)/TITLE="($ferret_diff_title)"/SET ($ferret_diff_var)
  IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
  IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
  IF `($labnum_x%0|*>1%)` THEN GO unlabel ($labnum_x)
  IF `($labnum_z%0|*>1%)` THEN GO unlabel ($labnum_z)
  IF `($title_size%0|*>1%)` THEN PPL LABSET ($title_size)
PPL ($plot_type)

LABEL/NOUSER `($ppl$xlen)/2`, `($ppl$ylen)+1.2`, 0, 0, 0.12, "LAS 7.+/Ferret ($FERRET_VERSION)     NOAA/PMEL"
IF ($needs_url"0|*>1") THEN \
LABEL/NOUSER `($ppl$xlen)*.33`, `($ppl$ylen)+1.`, -1, 0, 0.10, "DATA SET: @AS($data_var_url)"

! Put the labels in the upper left

LET labdel = 0.2
LET labyloc = `($ppl$ylen) + labdel*($n_left_labels) `
LET labsiz = 0.1

LABEL/NOUSER 0, `labyloc`, -1, 0.0, `labsiz`, ($upper_left_1)
LET labyloc = `labyloc - labdel`
IF `($n_left_labels) GT 1` THEN LABEL/NOUSER 0, `labyloc`, -1, 0.0, `labsiz`, ($upper_left_2)

! Put any URL labels in the upper right

IF `($n_right_labels) GT 0` THEN
   LET labxloc = `($ppl$xlen)`
   LET labdel = 0.2
   LET labsiz = 0.1
   LET labyloc = `($ppl$ylen) + labdel*($n_left_labels) `  ! start with loc of left-side labels
   LET labyloc = `labyloc + labdel*($n_right_labels)`

   LABEL/NOUSER `labxloc`, `labyloc`, 1, 0.0, `labsiz`, ($upper_right_1)
   LET labyloc = `labyloc - labdel`
   IF `($n_right_labels) GT 1` THEN LABEL/NOUSER `labxloc`, `labyloc`, 1, 0.0, `labsiz`, ($upper_right_2)
ENDIF 

! Mark grid points. Options are no, subsample (tests if subsampling needed)
! or all to force marking all points.

IF ($ferret_mark_grid"0|no>0|all>1|subsample>1|*>0") THEN GO mark_grid


! Save the results
GO LAS_results box

! End of $RCSfile -----------------------------------------------------

