! LAS_centered_levels.jnl
!
! Called by LAS_auto_levels.jnl and LAS_auto_levels_animate.jnl
! Compute centered levels based on data statistics already computed:
! stat_min, stat_max, stat_mean, stat_std

! Adjust if the data values are not naturally centered near 0
! If num_fine_levels_contour has been set, use it to set how many
! contour levels per color level. Never more contours than colors
! on a contour-over-colors plot.

LET twostd = 2*nstd
LET absmin = ABS( ($stat_min) )
LET absmax = ABS( ($stat_max) )

LET lines_to_levels = 2
IF ($num_fine_levels_contour"0|*>1") THEN 
   LET lines_to_levels = INT( ($num_fine_levels)/($num_fine_levels_contour) )
   IF `lines_to_levels LT 1` THEN LET lines_to_levels = 1
ENDIF

! Compute rounded-off levels with the PPL %RANGE command.

IF `($stat_min) GT 0 AND ($stat_max) GT 0` THEN
   PPL %RANGE `-1*twostd`, `twostd`, ($num_fine_levels)
ELIF `absmin GT twostd OR absmax GT twostd` THEN 
   PPL %RANGE `-1*twostd`, `twostd`, ($num_fine_levels)
ELSE
   PPL %RANGE `-1*nstd`, `nstd`, ($num_fine_levels)
ENDIF

DEFINE SYMBOL fine_lo = ($ppl$range_low) 
DEFINE SYMBOL fine_hi = ($ppl$range_high) 
DEFINE SYMBOL fine_inc = ($ppl$range_inc)

DEFINE SYMBOL lines_fine_inc = `lines_to_levels*($fine_inc)`

! Create centered fine levels surrounded by centered coarse levels.

DEFINE SYMBOL coarse_delta = `($fine_inc)*density_factor`
DEFINE SYMBOL coarse_lo = `($fine_lo) - ($half_num_coarse_levels) * ($coarse_delta)`
DEFINE SYMBOL coarse_hi = `($fine_hi) + ($half_num_coarse_levels) * ($coarse_delta)`

DEFINE SYMBOL lines_coarse_delta = `lines_to_levels*($coarse_delta)`

! Remove plus in exponent
! if levels symbols have exponents with + in them, then that + is
! lost in the animation sequence and Ferret chokes on levels symbols
! containing a blank when it goes to make the plots for frames.
IF ($remove_plus"0") THEN
   IF `STRINDEX("($fine_lo)", "+") GT 0` THEN GO remove_plus_exponent fine_lo ($fine_lo)
   IF `STRINDEX("($fine_hi)", "+") GT 0` THEN GO remove_plus_exponent fine_hi ($fine_hi)
   IF `STRINDEX("($fine_inc)", "+") GT 0` THEN GO remove_plus_exponent fine_inc ($fine_inc)
   IF `STRINDEX("($coarse_lo)", "+") GT 0` THEN GO remove_plus_exponent coarse_lo ($coarse_lo)
   IF `STRINDEX("($coarse_hi)", "+") GT 0` THEN GO remove_plus_exponent coarse_hi ($coarse_hi)
   IF `STRINDEX("($coarse_delta)", "+") GT 0` THEN GO remove_plus_exponent coarse_delta ($coarse_delta)
   IF `STRINDEX("($lowest_lev)", "+") GT 0` THEN GO remove_plus_exponent lowest_lev ($lowest_lev)
   IF `STRINDEX("($highest_lev)", "+") GT 0` THEN GO remove_plus_exponent highest_lev ($highest_lev)
   IF `STRINDEX("($lines_fine_inc)", "+") GT 0` THEN GO remove_plus_exponent lines_fine_inc ($lines_fine_inc)
   IF `STRINDEX("($lines_coarse_delta)", "+") GT 0` THEN GO remove_plus_exponent lines_coarse_delta ($lines_coarse_delta)
ENDIF

IF `($stat_min) LT ($coarse_lo)  OR  ($stat_max) GT ($coarse_hi)` THEN

   DEFINE SYMBOL ferret_auto_levels = (-inf)(($coarse_lo),($fine_lo),($coarse_delta))\
(($fine_lo),($fine_hi),($fine_inc))\
(($fine_hi),($coarse_hi),($coarse_delta))(inf)
   DEFINE SYMBOL ferret_auto_lines =(-inf)(($coarse_lo),($fine_lo),($lines_coarse_delta))\
(($fine_lo),($fine_hi),($lines_fine_inc))\
(($fine_hi),($coarse_hi),($lines_coarse_delta))(inf)
EXIT/SCRIPT

ELSE

   PPL %RANGE `($stat_min)`, `($stat_max)`, 2*($num_coarse_levels)
   LET lohi = MAX(ABS( ($ppl$range_low)), ABS(($ppl$range_high)) )
   DEFINE SYMBOL lowest_lev = `-1*lohi`
   DEFINE SYMBOL highest_lev = `lohi`

   DEFINE SYMBOL ferret_auto_levels = (-inf)(($fine_lo),($fine_hi),($fine_inc))(inf)
   DEFINE SYMBOL ferret_auto_lines = (-inf)(($fine_lo),($fine_hi),($fine_inc))(inf)
   IF `ABS( ($fine_lo)-($lowest_lev) )  GT ($coarse_delta)  OR \
       ABS( ($highest_lev)-($fine_hi) ) GT ($coarse_delta)` THEN

      DEFINE SYMBOL ferret_auto_levels = (-inf)(($lowest_lev),($fine_lo),($coarse_delta))\
(($fine_lo),($fine_hi),($fine_inc))\
(($fine_hi),($highest_lev),($coarse_delta))(inf)
      DEFINE SYMBOL ferret_auto_lines = (-inf)(($lowest_lev),($fine_lo),($lines_coarse_delta))\
(($fine_lo),($fine_hi),($lines_fine_inc))\
(($fine_hi),($highest_lev),($lines_coarse_delta))(inf)

   ENDIF

ENDIF

! End of file ------------LAS_centered_levels.jnl--------------------------

