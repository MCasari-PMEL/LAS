!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: LAS_initialize_variable.jnl
! $Revision: 1.1
! 
! $Author: Ansley, Jing $
! $Date: 2007/29/2007
! ACM 4/2007 apply ferret_expression2 to second plot of comparisons.
! ACM 8/2007 Call LAS_auto_levels to set up auto contour/fill levels.

! this is the second part of LAS_initialize_data.jnl

! Define the plot variable. For comparison plots, we want to save the dataset number
! for each dataset.

DEFINE SYMBOL data_num = `($data_var),RETURN=dsetnum`
DEFINE SYMBOL data_($num)_num = `($data_var),RETURN=dsetnum`
 
! Be sure units and title are defined.
! If units are not defined, try to get units from the data variable.
! If the variable in the file has no units, leave data_units symbol undefined.
 
LET anames = ($data_var).attnames
IF `IS_ELEMENT_OF_STR (anames, "units") OR IS_ELEMENT_OF_STR (anames, "UNITS")` THEN
   DEFINE SYMBOL data_var_units `($data_var),RETURN=units`123   ! check for empty units string.
   IF `STRLEN("($data_var_units)" ) EQ 3` THEN
      CANCEL SYMBOL data_var_units
   ELSE
      DEFINE SYMBOL data_var_units `($data_var),RETURN=units`
      DEFINE SYMBOL data_units (`($data_var),RETURN=units`)
   ENDIF
ENDIF

DEFINE SYMBOL ferret_plot_title "($data_title) ($data_units)"

IF `($data_title"0|*>1") EQ 0` THEN
  LET has_units = STRCMP (" ", "`($data_var),return=units`")
  LET has_units = `has_units OR ($data_units"0|*>1")`
  IF `has_units NE 0` THEN \
  DEFINE SYMBOL ferret_plot_title "($data_var) ($data_units)"
  CANCEL VARIABLE has_units
ENDIF

DEFINE SYMBOL ferret_plot_var = ($data_var)[d=($data_num),($region_full)]
DEFINE SYMBOL ferret_plot_var_noregion = ($data_var)[d=($data_num)]
DEFINE SYMBOL ferret_plot_base_var = ($ferret_plot_var) 
DEFINE SYMBOL ferret_plot_base_var_noregion = ($data_var)[d=($data_num)]

! Needed for transform_seasonal, save variable definition without any
! time-region information that may be added, and also with the time region only

DEFINE SYMBOL ferret_xyz_var = ($data_var)[d=($data_num),($region_xyz)]
DEFINE SYMBOL ferret_t_var = ($data_var)[d=($data_num),($region_t)]

! Apply the expression in ferret_expression. $ represents the variable.

! Check if we are working with the second of two variables in comparison mode
IF ($variables_initialized"0|*>1") THEN
   IF ($ferret_expression2"0|*>1") THEN DEFINE SYMBOL ferret_expression ($ferret_expression2)
ENDIF

! The side by side plots pretend that the second plot is plot 1, so 
IF `STRCMP("($operation_service_action)", "Side2") EQ 0` THEN
   IF ($ferret_expression2"0|*>1") THEN DEFINE SYMBOL ferret_expression ($ferret_expression2)
ENDIF

IF ($ferret_expression"0|*>1") THEN GO LAS_expression

! Apply any transforms that may have been specified.
GO LAS_transforms ($variables_initialized"0|*>1")

! When doing 2D plots, we want the plot region +1 grid cell on all sides, 
! for shade-under-fill.
GO LAS_define_region_plus_1

! Get any DEFINE VARIABLE analysis definitions
! This renames the data variable, and so redefines the symbol data_var
! and sets up the analysis: e.g. ,z=0:2000@ave

! unComment this for testing.

IF ($data_0_analysis_label"0|*>1") THEN 
   GO LAS_analysis
   DEFINE SYMBOL ferret_plot_var = ($data_var)[d=($data_num),($region_full)($data_analysis_expr)]
   DEFINE SYMBOL ferret_plot_var_noregion = ($data_var)[d=($data_num)($data_analysis_expr)]
   IF ($data_0_analysis_oceanmask"0|*>1") THEN 
     LET/d=($data_num) plot_var =  ($data_var)[d=($data_num)] * analysis_mask
     DEFINE SYMBOL ferret_plot_var = plot_var[($region_full)($data_analysis_expr)]
     DEFINE SYMBOL ferret_plot_var_noregion = plot_var[($data_analysis_expr)]
   ENDIF
ENDIF

! Keep track of variables initialized, to tell us if next time we
! want ferret_expression or ferret_expression2

DEFINE SYMBOL variables_initialized `1 + ($variables_initialized"0")`

IF `($variables_initialized) EQ ($data_count)` THEN

! From the variable we can define striding. 
! TODO: script works only for rectangular data so far...

!test stride for Google Earth
   IF `STRINDEX( "($operation_ID)", "GE" ) NE 0` THEN
      GO LAS_set_strides_GE
   ELSE
      GO LAS_set_strides.jnl
   ENDIF
   
ENDIF

! IF contour and fill levels havent been set in the configuration or
! the user interface, set levels based on the variable after it is strided and its region set.

!IF `( ($ferret_contour_levels"0|*>1") EQ 0 ) AND\ 
!      ( ($ferret_fill_levels"0|*>1")    EQ 0 ) ` THEN
!     DEFINE SYMBOL ferret_auto_levels ($ferret_num_contour_levels"50")
!ENDIF

! Mark whether there are contour levels set on first entry. 
! Compute automatic open-ended levels.

IF `($data_num) EQ 1` THEN
   IF ($ferret_contour_levels"0|*>1") THEN DEFINE SYMBOL input_contour_levels = 1
   IF ($ferret_fill_levels"0|*>1") THEN DEFINE SYMBOL input_fill_levels = 1

   IF `STRLEN("($ferret_view)") EQ 2 AND STRINDEX("($operation_ID)", "Plot") GT 0` THEN
   IF `STRINDEX("($op)", "ANIMATION") EQ 0` THEN GO LAS_auto_levels  ! animation levels done elsewhere
ENDIF

! End of $RCSfile ------------LAS_initialize_variable.jnl--------------------------
