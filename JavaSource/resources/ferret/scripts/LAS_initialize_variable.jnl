!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: LAS_initialize_variable.jnl
! $Revision: 1.1
! 
! $Author: Ansley, Jing $
! $Date: 2007/29/2007
! ACM 4/2007 apply ferret_expression2 to second plot of comparisons.
! ACM 8/2007 Call LAS_auto_levels to set up auto contour/fill levels.
! 3/2008 ACM Use SHADE/TRIM for shade underlays of FILL plots (Ferret v6.1),
!            so no need to call LAS_define_region_plus_1.jnl
! 10-Jul-2008 ACM Define ferret_x_var, needed for listing data to files

! this is the second part of LAS_initialize_data.jnl

! Define the plot variable. For comparison plots, we want to save the dataset number
! for each dataset.

DEFINE SYMBOL data_num = `($data_var),RETURN=dsetnum`
DEFINE SYMBOL data_($num)_num = `($data_var),RETURN=dsetnum`

LET attlist = ($data_var).attnames
DEFINE SYMBOL data_var_only = `UPCASE("($data_var)")`

! setup for analysis ops on curvilinear grids 
IF `IS_ELEMENT_OF_STR (attlist, "ferret_definition") GT 0 AND\
    ($ferret_curvi_coord_lon"0|*>1")` THEN
   LET data_var_definition = `($data_var).ferret_definition`
   DEFINE SYMBOL data_var_def = `($data_var).ferret_definition`
   DEFINE SYMBOL data_var = data_var_definition

! If there is an X or Y analysis operation then the X or Y region has
! Not been set. Get the X and/or Y analysis request, set up 
! x an y region definitions, and prepare to do the request either
! by finding the region in the rectiliear portion of the grid
! or by first doing a curvi-to-rectilnear regridding operation.

   GO LAS_set_x_y_analysis.jnl

ENDIF

! Be sure units and title are defined.
! If units are not defined, try to get units from the data variable.
! If the variable in the file has no units, leave data_units symbol undefined.

! data_units should have been defined in LAS_initialize_dataset

IF ($data_units"0|*>1") THEN
   DEFINE SYMBOL has_units = 1
ELSE
   LET anames = ($data_var).attnames
   IF `IS_ELEMENT_OF_STR (anames, "units") OR IS_ELEMENT_OF_STR (anames, "UNITS")` THEN
      DEFINE SYMBOL data_var_units `($data_var),RETURN=units`123   ! check for empty units string.
      IF `STRLEN("($data_var_units)" ) EQ 3` THEN
         CANCEL SYMBOL data_var_units
      ELSE
         DEFINE SYMBOL data_var_units `($data_var),RETURN=units`
         DEFINE SYMBOL data_units (`($data_var),RETURN=units`)
      ENDIF
   ENDIF
ENDIF

DEFINE SYMBOL ferret_plot_title "($data_title) ($data_units)"

IF `($data_title"0|*>1") EQ 0` THEN
  IF `($has_units"0|*>1") EQ 0` THEN 
     SYMBOL has_units = STRCMP (" ", "`($data_var),return=units`")
     SYMBOL has_units = `($has_units) OR ($data_units"0|*>1")`
  ENDIF
  IF ($has_units"0|*>1") THEN \
  DEFINE SYMBOL ferret_plot_title "($data_var) ($data_units)"
  CANCEL SYMBOL has_units
ENDIF


DEFINE SYMBOL ferret_plot_var = ($data_var)[d=($data_num),($region_full)]
IF ($analysis_def"0|*>1") THEN  ! analysis_def from curvi data or transform
   LET fplot_var = ($ferret_plot_var)
   DEFINE SYMBOL ferret_plot_var = fplot_var($analysis_def)
ENDIF

DEFINE SYMBOL ferret_plot_var_noregion = ($data_var)[d=($data_num)]
DEFINE SYMBOL ferret_plot_var_($num)_noregion = ($data_var)[d=($data_num)]

DEFINE SYMBOL ferret_plot_base_var = ($ferret_plot_var) 
DEFINE SYMBOL ferret_plot_base_var_($num) = ($ferret_plot_var) 
DEFINE SYMBOL ferret_plot_base_var_noregion = ($data_var)[d=($data_num)]

! Needed for transform_seasonal, save variable definition without any
! time-region information that may be added, and also with the time region only

DEFINE SYMBOL ferret_xyz_var = ($data_var)[d=($data_num),($region_xyz)]
DEFINE SYMBOL ferret_t_var = ($data_var)[d=($data_num),($region_t)]

! ferret_x_var needed for listing data to files
DEFINE SYMBOL ferret_x_var = ($data_var)[d=($data_num),($region_x)]
IF `($region_x"0|*>1) EQ 0` THEN DEFINE SYMBOL ferret_x_var = ($data_var)[d=($data_num)]

! Apply the expression in ferret_expression. $ represents the variable.

! Check if we are working with the second of two variables in comparison mode
IF ($variables_initialized"0|*>1") THEN
   IF ($ferret_expression2"0|*>1") THEN DEFINE SYMBOL ferret_expression ($ferret_expression2)
ENDIF

! The side by side plots pretend that the second plot is plot 1, so 
IF `STRCMP("($operation_service_action)", "Side2") EQ 0` THEN
   IF ($ferret_expression2"0|*>1") THEN DEFINE SYMBOL ferret_expression ($ferret_expression2)
ENDIF

IF ($ferret_expression"0|*>1") THEN GO LAS_expression

! Apply any transforms that may have been specified.
GO LAS_transforms ($variables_initialized"0|*>1")

! Get any DEFINE VARIABLE analysis definitions
! This renames the data variable, and so redefines the symbol data_var
! and sets up the analysis: e.g. ,z=0:2000@ave

! unComment this for testing.

IF ($data_0_analysis_label"0|*>1") THEN 
   GO LAS_analysis
   DEFINE SYMBOL ferret_plot_var = ($data_var)[d=($data_num),($region_full)($data_analysis_expr)]
   DEFINE SYMBOL ferret_plot_var_noregion = ($data_var)[d=($data_num)($data_analysis_expr)]
   IF ($data_0_analysis_oceanmask"0|*>1") THEN 
     LET/d=($data_num) plot_var =  ($data_var)[d=($data_num)] * analysis_mask
     DEFINE SYMBOL ferret_plot_var = plot_var[($region_full)($data_analysis_expr)]
     DEFINE SYMBOL ferret_plot_var_noregion = plot_var[($data_analysis_expr)]
   ENDIF
ENDIF

! Keep track of variables initialized, to tell us if next time we
! want ferret_expression or ferret_expression2

DEFINE SYMBOL variables_initialized `1 + ($variables_initialized"0")`

! From the variable we can define striding. 
! TODO: script works only for rectangular data so far...

! (check for Google Earth plots is inside LAS_set_strides)
IF `($num)+1 EQ ($data_count)` THEN GO LAS_set_strides.jnl

! to set non-native strides for dataset 1 on a difference plot.
IF `($no_native_strides_xy"0|*>1") AND ($set_ndx_strides"0|*>1") EQ 0` THEN 
   GO LAS_set_strides
ENDIF

! Mark whether there are contour levels set on first entry. 
! Compute automatic open-ended levels.

IF `($data_num) EQ 1` THEN
   IF ($ferret_contour_levels"0|*>1") THEN DEFINE SYMBOL input_contour_levels = 1
   IF ($ferret_fill_levels"0|*>1") THEN DEFINE SYMBOL input_fill_levels = 1

   IF `STRLEN("($ferret_view)") EQ 2 AND STRINDEX("($operation_ID)", "Plot") GT 0` THEN 
      IF `STRINDEX("($op)", "ANIMATION") EQ 0` THEN GO LAS_auto_levels  ! animation levels done elsewhere
   ENDIF
   IF `STRLEN("($ferret_view)") EQ 2 AND STRINDEX("($operation_ID)", "zoom") GT 0` THEN GO LAS_auto_levels

ENDIF

! End of $RCSfile ------------LAS_initialize_variable.jnl--------------------------
