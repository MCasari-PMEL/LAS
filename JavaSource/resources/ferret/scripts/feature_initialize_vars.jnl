!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $Header$
! Feature_initialize.jnl
!
! Define variables and symbols based on feature identification variables
! in a DSG dataset
!
! feature_ID     -- unique identifier for each trajectory or profile

!acm 2/2009 Additions to the symbol definitions at the end to
!           allow for null strings: replace these with "none"

! Cancel the XY region.   These limits have nothing to do with the 
! I and J axes of the intermediate data file.

IF `($ferret_memory"25.6") LT 200` THEN SET MEM/siz=200

! Create the 'feature_evnt' variable which increments for each new 
! trajectory or profile.

! NOTE:  The calculation of individual trajectories or profiles only works if the 
! NOTE:  intermediate netCDF files is ordered by feature_ID.  Make sure that
! NOTE:  the SQL query (dsg request?) ends with:  'ORDER BY feature_ID'.

DEFINE SYMBOL feature_max_labels = 200
DEFINE SYMBOL numobs `($data_var),return = isize`
LET num_e = `($data_var),return = msize`

IF `num_e GT ($numobs)` THEN DEFINE SYMBOL numobs = `num_e`

! Are the rowsize variables in the E direction?
DEFINE SYMBOL rdir = `($rowvar),RETURN=shape` 
DEFINE SYMBOL rind = ($rdir"|X>I|E>M|POINT>M")

! If (for prop-prop plots) there are mask-by settings, apply them

let nmask_labels = 0
GO feature_mask_by_var ($data_var)


LET n_subset = `($rowvar),RETURN=msize`
DEFINE AXIS/X=1:`n_subset`:1 xmask_axis

IF `nmask_labels GT 0` THEN
   LET dvar_to_mask = ($data_var)
   DEFINE SYMBOL data_var = mask_on_var* dvar_to_mask
ENDIF


IF `($numobs) EQ 1` THEN
   LET nfeatures = 1
   DEFINE SYMBOL feature_max = 1
   
   LET feature_numbers = i[gx=xmask_axis]
   LET feature_mask = 1 + 0*feature_numbers

   LET longest = 1

   LET data_var_z =  EXPNDI_BY_Z_COUNTS(($data_var), XSEQUENCE(($rowvar)),`longest`)

   LET feature_n = x[i=1:1]
   LET subset_mask = x[i=1:1]

   LET feature_mask_obs = 1 
   DEFINE SYMBOL have_feature_mask = 1

ELSE
   LET nfeatures = 1
   IF `($rind"0|I>1|*>0")` THEN LET nfeatures = `($rowvar),RETURN=isize`
   IF `($rind"0|M>1|*>0")` THEN LET nfeatures = `($rowvar),RETURN=msize`
   LET feature_numbers = i[gx=xmask_axis]
   IF `($subset_mask"0|*>1") EQ 0` THEN LET feature_mask = 1 + 0*feature_numbers  ! may redefine later

   DEFINE SYMBOL feature_max = `nfeatures`

   LET longest = ($rowvar)[($rind)=@max]+2

   LET data_var_z =  EXPNDI_BY_Z_COUNTS(($data_var), xsequence(($rowvar)),`longest`)

! If  a subset of feature ids is given then plot only those on a Prop/Prop 
! plot, but using the symbols and colors as determined by the whole dataset.

! Format the feature list and define the mask. 
   GO feature_list_mask
ENDIF

! For SOCAT server labels, make time symbols w/o the trailing 00:00:00

IF ($region_0_t_lo"0|*>1") THEN
   DEFINE SYMBOL t_lab_lo = ($region_0_t_lo)
   LET iz = STRINDEX("($region_0_t_lo)", " 00:00:00")
   IF `iz GT 0` THEN 
      DEFINE SYMBOL t_lab_lo = `SUBSTRING("($region_0_t_lo)", 1, iz-1)`
   ELSE
      LET iz = STRINDEX("($region_0_t_lo)", ":00:00")
      IF `iz GT 0` THEN DEFINE SYMBOL t_lab_lo = `SUBSTRING("($region_0_t_lo)", 1, iz-1)`
   ENDIF
ENDIF 

IF `STRCMP("($region_0_t_lo)", "($region_0_t_hi)") EQ 0` THEN  EXIT/SCRIPT

IF ($region_0_t_hi"0|*>1") THEN
   DEFINE SYMBOL t_lab_hi = ($region_0_t_hi)
   LET iz = STRINDEX("($region_0_t_hi)", " 00:00:00")
   IF `iz GT 0` THEN 
      DEFINE SYMBOL t_lab_hi = `SUBSTRING("($region_0_t_hi)", 1, iz-1)`
   ELSE
      LET iz = STRINDEX("($region_0_t_hi)", ":00:00")
      IF `iz GT 0` THEN DEFINE SYMBOL t_lab_hi = `SUBSTRING("($region_0_t_hi)", 1, iz-1)`
   ENDIF
ENDIF

!---------------- end of Feature_initialize.jnl -------------------------
