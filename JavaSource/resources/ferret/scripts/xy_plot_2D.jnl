!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: xy_plot_2D.jnl,v $
! $Revision: 1.12 $
! 
! $Author: ansley $
! $Date: 2006/08/11 16:45:54 $
! 2006/07/19 Fix test of ($ferret_contour_levels"0|*>1")
! 3/7/2007: ACM. Put code to check that the region was not too small, resulting 
!           in degenerate plot into LAS_initialize_data.jnl rather than in the 
!           individual plot scripts.
! 3/29/2007 ACM Fix logic interpreting contents of ferret_interpolate_data 
! 7/15/2007 ACM Add lines to remove labels when a degenerate 2D plot is made 
!               This happens when the region chosen is smaller than a grid cell.
! 3/2008 ACM Use SHADE/TRIM for shade underlays of FILL plots (Ferret v6.1)
! 3/2008 ACM For native curvilinear plots use MODULO qualifier only on SHADE plots

! XY_plot.jnl creates an XY plot for use with the Back End Server
! code that sits behind a Live Access Server (LAS). 
!
! NOTE:  This code handles overlays but not differencing.
!
! TODO:  We should support user choice of the 'base layer' variable.
! TODO:  From the Ferret script writers' perspective it would be nice
! TODO:  if this were handled in the UI so that ($data_0_...) always
! TODO:  referred to the base layer.


! Set any Ferret modes
!
! NOTE:  Should we support any of the following Ferrt modes?
! NOTE:    ASCII_FONT, CALENDAR, DEPTH_LABEL, LABELS, LATIT_LABEL, LONG_LABEL 

IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL fview = `UPCASE("($ferret_view)")`

! Define symbols associated with the regign and data and perform any
! initialization needed for this dataset.
!
! NOTE:  Adding support for a 'base layer' variable would mean passing
! NOTE:  some other argument.  For now, though, we always use 'data_0'
! NOTE:  for the base layer and region.

GO LAS_initialize_region 0
GO LAS_initialize_data 0

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small to make a 2D plot on this grid.
GO LAS_check_2d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Here is variable 0 and its title

DEFINE SYMBOL ferret_var_0 = ($ferret_plot_var)
DEFINE SYMBOL ferret_title_0 = ($ferret_plot_title"($data_var)")
IF ($ferret_plot_var_m360"0|*>1") THEN DEFINE SYMBOL ferret_var_m360_0 = ($ferret_plot_var_m360)

! Open the window, apply size, scale and WMS view options
GO LAS_open_window

! TODO:  Should the creation of the ($qualifiers) symbol for standard
! TODO:  2D plots be separated out in a separate script called:
! TODO:    LAS_2D_plot_qualifiers.jnl
!
! Build up the plot qualifiers

!ACM note: All color plots use $ferret_fill_levels. Contour plots use $ferret_contour_levels.

IF ($ferret_contour_style"0|contour_lines>1|*>0") THEN
   IF ($ferret_contour_levels"0|*>1") THEN
     DEFINE SYMBOL qualifiers = ($qualifiers)/LEVELS=($ferret_contour_levels)
   ENDIF
ELSE
   IF ($ferret_fill_levels"0|*>1") THEN
     DEFINE SYMBOL qualifiers = ($qualifiers)/LEVELS=($ferret_fill_levels)
   ENDIF
ENDIF

IF ($ferret_plot_key"0|0|nokey>0|*>1") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/NOKEY
ENDIF

IF ($ferret_use_graticules"1|0|*>1) THEN
  SET MODE GRATICULE:(DASH,COLOR=black)
ENDIF

DEFINE SYMBOL qualifiers = ($qualifiers)/PALETTE=($ferret_palette"rainbow|default>rainbow|*>*)

IF ($ferret_curvi_quals"0|*>1") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_curvi_quals)
ENDIF

! Set up for degrees-minutes-seconds labels on axes with units of degrees

IF ($ferret_deg_min_sec"0|false>0|*>1") THEN GO LAS_set_deg_min_sec.jnl

! Set the plot type (SHADE or CONTOUR)

DEFINE SYMBOL plot_type =($ferret_contour_style"FILL|default>FILL|raster>SHADE|color_filled_contours>FILL|color_filled_plus_lines>FILL|contour_lines>CONTOUR|raster_plus_lines>SHADE")

! For all FILL plots, we will first do a SHADE underlay to get the plot to draw all the way to the edges.
DEFINE SYMBOL shade_underlay = FALSE
IF ($plot_type"0|FILL>1|*>0) THEN DEFINE SYMBOL shade_underlay = TRUE

! Set the URL label for the first dataset.
GO LAS_url_label 0

! Set up for second variable, overlaid as contour lines.
! If there were more than 2 data sets, then use a loop (below) with
! CONTOUR/OVERLAY and rely on automatic Ferret labelling of the contours.
! For just two data sets, set up fancier titles and labels.

IF `($data_count"0") EQ 2` THEN
   ! Apply any expression only to the first variable not to subsequent ones
   CANCEL SYMBOL ferret_expression 

   GO LAS_initialize_region 1
   GO LAS_initialize_data 1 
   DEFINE SYMBOL qualifiers_over = /LINE=1 

   ! Check for errors (They often occur during dataset initialization.)

   IF ($error_status"0|*>1") THEN
      MESSAGE/ERROR **ERROR ($error_string)
      EXIT/PROGRAM
    ENDIF

  ! Here is variable 1 and its title

   DEFINE SYMBOL ferret_var_1 = ($ferret_plot_var)
   DEFINE SYMBOL ferret_title_1 = ($ferret_plot_title"($data_var)")

   ! Set the title, and the labels for the upper left listing the locations
   ! and time in directions normal to the plot, and in the upper right listing
   ! urls if dataset are opendap.

   GO LAS_set_diff_labels ($fview)

ENDIF

! If there is just one variable, draw the plot and we are done

IF ($ferret_curvi_quals"0|*>1") THEN 
   IF ($ferret_lon_modulo"1|1|0|yes>1|no>0|*>0") THEN  DEFINE SYMBOL shade_qualifiers = ($shade_qualifiers)/MODULO
ENDIF

IF `($data_count"0") EQ 1` THEN
   DEFINE SYMBOL split_title = ($ferret_plot_title)
   GO LAS_split_title
   DEFINE SYMBOL ferret_plot_title = ($split_title)

   IF ($shade_underlay) THEN 
      IF `($ferret_google_plot"0") EQ 0` THEN \
         DEFINE SYMBOL shade_qualifiers = ($shade_qualifiers)/TRIM

      SHADE($qualifiers)($shade_qualifiers)/TITLE="($ferret_plot_title)"/SET ($ferret_var_0)
         IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
         IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
         IF ($axlab_command"0|*>1") THEN PPL ($axlab_command)
         IF ($axtic_command"0|*>1") THEN PPL ($axtic_command)
         IF ($axlen_command"0|*>1") THEN PPL ($axlen_command)
         if ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
         if ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
         if ($labnum_dods"0|*>1") THEN go unlabel ($labnum_dods)
      PPL SHADE
      IF `($LEV_NUM"0") GT 245/2` THEN PPL SHASET RESET
      ($plot_type)($qualifiers)/OVER/NOLAB ($ferret_var_0)
      IF ($native_curvilinear_xy"0|*>1") THEN 
         PPL SHASET RESET
	 ($plot_type)($qualifiers)/OVER/NOLAB ($ferret_var_m360_0)
      ENDIF

   ELSE
   
      ($plot_type)($qualifiers)/TITLE="($ferret_plot_title)"/SET ($ferret_var_0)
         IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
         IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
         IF ($axlab_command"0|*>1") THEN PPL ($axlab_command)
         IF ($axtic_command"0|*>1") THEN PPL ($axtic_command)
         IF ($axlen_command"0|*>1") THEN PPL ($axlen_command)
         if ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
         if ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
         if ($labnum_dods"0|*>1") THEN go unlabel ($labnum_dods)
         ppl list labels
      PPL ($plot_type)
      IF ($native_curvilinear_xy"0|*>1") THEN 
         PPL SHASET RESET
	 ($plot_type)($qualifiers)/OVER/NOLAB ($ferret_var_m360_0)
      ENDIF

   ENDIF

! One of the results is the data min and max. Save those here.
   DEFINE SYMBOL data_plotted_min = ($ppl$zmin)
   DEFINE SYMBOL data_plotted_max = ($ppl$zmax)

   IF ($ferret_contour_style"0|default>0|color_filled_plus_lines>1|raster_plus_lines>1|*>0") THEN
      IF ($ferret_contour_levels"0|*>1") THEN 
         DEFINE SYMBOL levsym = /LEVELS=($ferret_contour_levels)
      ELSE
         IF ($ferret_fill_levels"0|*>1") THEN DEFINE SYMBOL levsym = /LEVELS=($ferret_fill_levels)
      ENDIF

      IF ($ferret_curvi_quals"0|*>1") THEN
         CONTOUR/OVER/($ferret_curvi_quals)/COLOR=black/NOLAB($levsym) ($ferret_var_0)
      ELSE
         CONTOUR/OVER/COLOR=black/NOLAB($levsym) ($ferret_var_0)
      ENDIF
   ENDIF
   
! One of the results is the data min and max. Save those here.
   DEFINE SYMBOL data_plotted_min = ($ppl$zmin)
   DEFINE SYMBOL data_plotted_max = ($ppl$zmax)

! If there are two variables draw the first, removing the labels at the top
! and then overlay the second. 

ELIF  `($data_count"0") EQ 2` THEN
   DEFINE SYMBOL ferret_contour_title ($ferret_over_title)(contours)
   ($plot_type)($qualifiers)/TITLE="($ferret_contour_title)"/SET ($ferret_var_0)
      IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
      IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
      IF `($labnum_z%0|*>1%)` THEN GO unlabel ($labnum_z)
      IF `($labnum_t%0|*>1%)` THEN GO unlabel ($labnum_t)
      IF `($labnum_dset%0|*>1%)` THEN GO unlabel ($labnum_dset)
      IF `($labnum_datitl%0|*>1%)` THEN GO unlabel ($labnum_datitl)
      IF `($labnum_dods%0|*>1%)` THEN GO unlabel ($labnum_dods)
      IF `($title_size%0|*>1%)` THEN PPL LABSET ($title_size)
   PPL ($plot_type)
   
! If there are two variables draw the first, removing the labels at the top
! and then overlay the second. 

   PPL SHASET RESET
   CONTOUR/OVER($qualifiers_over)/NOKEY/NOLAB ($ferret_var_1) 

   GO LAS_url_label 1
ENDIF

! Overlay additional data

! TODO:  Support user options for contour overlays:  color, labeling, ...

! If there were more than 2 data sets, then use a loop and rely on 
!   automatic Ferret labelling of the contours.
! IF `($data_count"0") GT 0` THEN
! 
! REPEAT/range=1:`($data_count)-1`/NAME=m (  \
!   GO LAS_initialize_region `m` ;  \
!   GO LAS_initialize_data  `m` ; \
!   DEFINE SYMBOL qualifiers_over = /LINE=`m` ;\
!   CONTOUR/OVER($qualifiers_over) ($ferret_plot_var) \
! )

! Overall header at the very top
GO LAS_ferret_las_version_header

! Add labels at the top for the location of overlaid lines
! and for URLs if they exist  

GO labels_above_plot

! Overlay additional cartography data (land mask, outlines, rivers, ...)
! For land outline, if the region is smaller than 60 degrees in x or 30 degrees in y,
! use the more-detailed land outline script land_detail.jnl

IF ($ferret_land_type"1|none>0|contour>0|filled>1|default>1|*>0") THEN
  GO LAS_fland 
ELIF ($ferret_land_type"0|none>0|contour>1|filled>0|*>0") THEN
  IF `( ($region_x_range"360") LT 60) OR ( ($region_y_range) LT 30)` THEN
     GO land_detail
  ELSE
     GO land 
  ENDIF
ENDIF

! Mark grid points. Options are no, subsample (tests if subsampling needed)
! or all to force marking all points.

IF ($ferret_mark_grid"0|no>0|all>1|subsample>1|*>0") THEN GO mark_grid

! Save the results
GO LAS_results box

! End of $RCSfile -----------------------------------------------------


