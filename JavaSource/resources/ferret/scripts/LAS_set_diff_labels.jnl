!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! LAS_set_diff_labels.jnl
! Revision: 1.1
! 
! $Author: ansley $
! $Date: Feb 2007 
!
! Set the title and labels for a difference plot, reproducing the behavior
! of diff plots from old LAS
! 3/7/2007: ACM. Remove extra units labels. Make title smaller when it is 4- lines.
!
! Set upper-left labels, listing location normal to the plot
! Argumnent is the view of this plot: for a 2d xy plot, XY; for a time series plot T
!
! Create the title (Use ascii font so any underscores in the variable or 
! url show up. Escape character before @P2 lets us get red text.)
!
! If there is a long URL, put URLs at the top, and use dataset names
! in the title
! 
! 10-Oct-2007 Remove extra units labels from 2nd dataset. Allow for one dataset
! to be a URL and  the other a simple dataset name. Add upper-left label for
! locations or times normal to the plot, previously missed if region was defined 
! for variable 2 but not variable 1

DEFINE SYMBOL ferret_diff_title = \
@AC($ferret_title_0) from ($data_0_dataset_name)(1) \
<NL> -  @P2($ferret_title_1)) from ($data_1_dataset_name)(2)

DEFINE SYMBOL ferret_over_title = \
@AC($ferret_title_0) from ($data_0_dataset_name)(1) \
<NL>  @P2($ferret_title_1)) from ($data_1_dataset_name)(2)

IF `STRLEN("($ferret_diff_title)") GT 150` THEN
  DEFINE SYMBOL ferret_diff_title = ($ferret_title_0) \
  <NL>  from @AC($data_0_dataset_name)(1) <NL> -  @P2($ferret_title_1)) \
  <NL>  from @AC($data_1_dataset_name)(2)
  DEFINE SYMBOL title_size 0.12   ! make title text smaller...
ENDIF

IF `STRLEN("($ferret_over_title)") GT 150` THEN
  DEFINE SYMBOL ferret_over_title = ($ferret_title_0) \
  <NL>  from @AC($data_0_dataset_name)(1) <NL>  @P2($ferret_title_1)) \
  <NL>  from @AC($data_1_dataset_name)(2)
  DEFINE SYMBOL title_size 0.12   ! make title text smaller...
ENDIF

! Set the upper-left labels, locations/time normal to the plot 

IF `($n_left_labels"0|*>1") EQ 0` THEN DEFINE SYMBOL n_left_labels = 0

DEFINE SYMBOL plotshape = $1

IF `STRINDEX("($plotshape)", "X") EQ 0` THEN
   IF ($region_0_x_lo"0|*>1") THEN
      DEFINE SYMBOL n_left_labels = `($n_left_labels)+1`
      DEFINE SYMBOL upper_left_($n_left_labels) = "Longitude(1): ($region_0_x_lo) @P2Longitude(2): ($region_1_x_lo)"
   ELIF ($region_1_x_lo"0|*>1") THEN 
      DEFINE SYMBOL n_left_labels = `($n_left_labels)+1`
      DEFINE SYMBOL upper_left_($n_left_labels) = "Longitude(2): ($region_1_x_lo)"
   ENDIF
ENDIF

IF `STRINDEX("($plotshape)", "Y") EQ 0` THEN
   IF ($region_0_y_lo"0|*>1") THEN 
      DEFINE SYMBOL n_left_labels = `($n_left_labels)+1`
      DEFINE SYMBOL upper_left_($n_left_labels) = "Latitude(1): ($region_0_y_lo) @P2Latitude(2): ($region_1_y_lo)"
   ELIF ($region_1_y_lo"0|*>1") THEN 
      DEFINE SYMBOL n_left_labels = `($n_left_labels)+1`
      DEFINE SYMBOL upper_left_($n_left_labels) = "Latitude(2): ($region_1_y_lo)"
   ENDIF
ENDIF

IF `STRINDEX("($plotshape)", "Z") EQ 0` THEN   
   IF ($region_0_z_lo"0|*>1") THEN
      DEFINE SYMBOL n_left_labels = `($n_left_labels)+1`
      DEFINE SYMBOL upper_left_($n_left_labels) = "Depth(1): ($region_0_z_lo) @P2Depth(2): ($region_1_z_lo)"
   ELIF ($region_1_z_lo"0|*>1") THEN 
      DEFINE SYMBOL n_left_labels = `($n_left_labels)+1`
      DEFINE SYMBOL upper_left_($n_left_labels) = "Depth(2): ($region_1_z_lo)"
   ENDIF
ENDIF

IF `STRINDEX("($plotshape)", "T") EQ 0` THEN   
   IF ($region_0_t_lo"0|*>1") THEN 
      DEFINE SYMBOL n_left_labels = `($n_left_labels)+1`
      DEFINE SYMBOL upper_left_($n_left_labels) = "Time(1): ($region_0_t_lo) @P2TIME(2): ($region_1_t_lo)"
   ELIF ($region_1_t_lo"0|*>1") THEN 
      DEFINE SYMBOL n_left_labels = `($n_left_labels)+1`
      DEFINE SYMBOL upper_left_($n_left_labels) = "TIME(2): ($region_1_t_lo)"
   ENDIF
ENDIF

! If there are URLs set up to write them in the upper right

CANCEL SYMBOL upper_right_*
DEFINE SYMBOL n_right_labels = 0

GO LAS_url_label 0
IF `STRCMP("($data_0_dataset_url%0%)", "($data_1_dataset_url%0%)") NE 0` THEN  GO LAS_url_label 1

