! LAS_auto_levels.jnl
! ACM 8/22/2007
! acm 9/11/07 If its a descriptor file Ferret wont allow striding. 
!             check for that case and just set num levels.

! Based on a variables value over its entire range, set strides
! if needed to quickly get stats on the variable. Use this to set levels.

! Argument: the variable to use in setting the levels.

! The symbol ferret_center_levels, if set to 1, forces the levels
! to be 2 std above and below zero. Otherwise use 2 std above 
! and below the data mean

! This script should be done BEFORE any other striding is done.
! It operates on the variable on its entire native unstrided grid.

! input_fill_levels and input_contour_levels are symbols that are defined in 
! LAS_initialize_variable.jnl, telling whether settings for levels were
! made 

! Determine striding for speed in operating the STAT command
LET memsize = 0.5* 1.e6

! Make sure its a 2D plot operation.

DEFINE SYMBOL op = `UPCASE("($operation_ID)")`

IF `STRLEN("($ferret_view)") NE 2` THEN EXIT/SCRIPT
IF `STRINDEX("($op)", "VECTOR") GT 0` THEN EXIT/SCRIPT

IF `STRINDEX("($op)", "PLOT") EQ 0 AND \
  STRINDEX ("($op)", "POLY") EQ 0 AND \
  STRINDEX ("($op)", "ZOOM") EQ 0 AND \
  STRINDEX ("($op)", "CRUISE_SUMMARY") EQ 0` THEN EXIT/SCRIPT

! Levels for animations set in LAS_auto_levels_animate, 
! called in list_frames.jnl
IF `STRINDEX("($op)", "ANIMATION") GT 0`  THEN EXIT/SCRIPT

! If its a compare plot, and if the ($ferret_diff_var) is not yet defined, 
! just return.

IF `($ferret_diff_var"1|*>0") AND STRINDEX("($op)", "COMPARE_PLOT") NE 0` THEN
   EXIT/SCRIPT
ENDIF 

DEFINE SYMBOL ferret_center_levels ($ferret_center_levels"0|1|TRUE>1|*>0")

! See if centered levels were requested, either by setting the symbol ferret_center_levels
! or with a contour_levels or fill_levels that is centered.

! If contour levels requested with levels of nC, e.g. 20C, then get the value to
! set num_fine_levels in the %RANGE command below

! In general if other levels spec is given, return. However if it is a simple number, 
! use that to set the number of fine levels.

IF ($ferret_contour_levels"0|*>1") THEN
   PPL AXNMTC 0,0 ! Turn on PPL so we can do the edit to remove any spaces (can occur in animations)
   PPL SET nospaces $EDIT (ferret_contour_levels, COLLAPSE)
   DEFINE SYMBOL ferret_contour_levels = ($nospaces)

   DEFINE SYMBOL up_contour_levels = `UPCASE("($ferret_contour_levels)")`
   LET nc = `STRINDEX("($up_contour_levels)", "C")`
   DEFINE SYMBOL ferret_center_levels = `nc GT 0`
   IF `nc GT 0` THEN
      LET slen = STRLEN ("($up_contour_levels)") 
      IF `slen GT 1` THEN DEFINE SYMBOL num_fine_levels = `SUBSTRING ("($up_contour_levels)", 1, slen-1)`
      IF `slen GT 1` THEN DEFINE SYMBOL num_fine_levels_contour = ($num_fine_levels)
      CANCEL SYMBOL input_contour_levels  ! we will redefine the levels in this script
   ENDIF
   LET other = `STRINDEX("($up_contour_levels)", "D")` + \
      `STRINDEX("($up_contour_levels)", "(")`+ \
      `STRINDEX("($up_contour_levels)", ".")`
   IF `nc EQ 0 AND other EQ 0` THEN 
      DEFINE SYMBOL num_fine_levels = ($ferret_contour_levels) ! How to test that this is just a number??
      DEFINE SYMBOL num_fine_levels_contour = ($num_fine_levels)
      CANCEL SYMBOL input_contour_levels  ! we will redefine the levels in this script
   ENDIF
   IF `nc EQ 0 AND other NE 0` THEN  EXIT/SCRIPT
ENDIF

IF ($ferret_fill_levels"0|*>1") THEN

   PPL AXNMTC 0,0 ! Turn on PPL so we can do the edit to remove any spaces (can occur in animations)
   PPL SET nospaces $EDIT (ferret_fill_levels, COLLAPSE)
   DEFINE SYMBOL ferret_fill_levels = ($nospaces)

   DEFINE SYMBOL up_fill_levels = `UPCASE("($ferret_fill_levels)")`
   DEFINE SYMBOL ferret_fill_levels = ($up_fill_levels) 
   LET nc = `STRINDEX("($up_fill_levels)", "C")`
   DEFINE SYMBOL ferret_center_levels = `nc GT 0`
   IF `nc GT 0` THEN 
      LET slen = STRLEN ("($up_fill_levels)") 
      IF `slen GT 1` THEN DEFINE SYMBOL num_fine_levels = `SUBSTRING ("($up_fill_levels)", 1, slen-1)`
      CANCEL SYMBOL input_fill_levels  ! we will redefine the levels in this script
   ENDIF
   LET other = `STRINDEX("($ferret_fill_levels)", "D")` + \
      `STRINDEX("($ferret_fill_levels)", ",")`+ \
      `STRINDEX("($ferret_fill_levels)", ".")`

   IF `nc EQ 0 AND other EQ 0` THEN 
      DEFINE SYMBOL num_fine_levels = ($ferret_fill_levels)  ! How to test that this is just a number??
      CANCEL SYMBOL input_fill_levels  ! we will redefine the levels in this script
   ENDIF
   IF `nc EQ 0 AND other NE 0` THEN  EXIT/SCRIPT
ENDIF

! Apply to the variable on the requested region

IF `($ferret_plot_var"0|*>1") EQ 0` THEN EXIT/SCRIPT  !e.g. for insitu plots may not have a plot_var

! When its a curvi plot, base the levels on the variable using coordinates 
! in the region. 

IF `($native_curvilinear_xy"0") OR ($region_sample"0|*>1")` THEN
   LET pvar = \
    ($data_var)[d=($data_num),i=($clon_imin):($clon_imax),j=($clat_jmin):($clat_jmax)]
   IF ($region_zt"0|*>1") THEN LET pvar = \
    ($data_var)[d=($data_num),i=($clon_imin):($clon_imax),j=($clat_jmin):($clat_jmax),($region_zt)]
ELSE
   LET pvar = ($ferret_plot_var)
   IF ($set_ndx_strides"0|*>1") THEN LET pvar = ($data_var)[d=($data_num),($region_xyzt)]
   IF ($auto_lev_var"0|*>1") THEN LET pvar = ($auto_lev_var)
ENDIF

IF ($ferret_plot_var"0|sampled_var>1|*>0) THEN LET pvar = ($ferret_plot_var)


! If the ferret_diff_var has been defined, use it.
! When no levels are defined, use centered ones for a diff variable

IF ($ferret_diff_var"0|*>1") THEN 
   IF `($input_contour_levels"0|0|1") EQ 0` THEN 
      CANCEL SYMBOL ferret_contour_levels
      DEFINE SYMBOL ferret_contour_levels = 20c
      DEFINE SYMBOL ferret_center_levels = 1
   ENDIF

   IF `($input_fill_levels"0|0|1") EQ 0` THEN
      CANCEL SYMBOL ferret_fill_levels
      DEFINE SYMBOL ferret_fill_levels = 20c
      DEFINE SYMBOL ferret_center_levels = 1
   ENDIF

   
   IF ($native_curvilinear_xy"0|*>1") THEN 
      LET pvar = ( ($ferret_plot_base_var_0)  - ($ferret_plot_base_var_1))
   ELSE
      LET pvar = ($ferret_diff_var)
   ENDIF

ENDIF

LET var_size = ABS( `pvar,RETURN=size`)
DEFINE SYMBOL var_dim = `pvar,RETURN=shape`

LET ndims = STRLEN("($var_dim)")

LET stride_for_speed = `INT( 1+ ((var_size/memsize))/ndims )`

! If axes are not already strided, apply simple striding which
! we will undo later.

LET sampled_in_x = 0
LET sampled_in_y = 0

IF `stride_for_speed GT 1` THEN
   LET sampled_in_x = STRCMP("`pvar,RETURN=xaxis`", "SAMPLE_XLON") EQ 0 OR\
                     STRCMP("`pvar,RETURN=xaxis`", "SAMPLE_X") EQ 0
   LET sampled_in_y = STRCMP("`pvar,RETURN=yaxis`", "SAMPLE_YLAT") EQ 0 OR\
                     STRCMP("`pvar,RETURN=yaxis`", "SAMPLE_Y") EQ 0
ENDIF


 DEFINE SYMBOL ppvar = pvar


IF ($native_curvilinear_xy"0") THEN DEFINE SYMBOL ppvar = pvar

! IF ($ferret_var_autolevels_0"0|*>1") THEN DEFINE SYMBOL ppvar = ($ferret_var_autolevels_0)
! IF `($data_count"0") GT 1` THEN DEFINE SYMBOL ppvar = \
!    ($ferret_var_autolevels_0) - ($ferret_var_autolevels_1)

! Execute the stat command to load symbols with the variables
! mean, max, min, std.

DEFINE SYMBOL stat_ngood= 0
set mode ignore_error
SET MEM/SIZ=400
set mode/last ignore_error
STAT ($ppvar)

IF `($stat_ngood"0") EQ 0` THEN EXIT/SCRIPT

! If the data values are so big we cant compute stats correctly, just
! set nominal levels and exit

IF `STRINDEX("($stat_std)", "too big") GT 0 OR \
  STRINDEX("($stat_mean)", "too big") GT 0 OR \
  STRINDEX("($stat_min)", "too big") GT 0 OR \
  STRINDEX("($stat_max)", "too big") GT 0 OR \
  STRINDEX("($stat_std)", "Inf") GT 0 OR \
  STRINDEX("($stat_mean)", "Inf") GT 0 OR \
  STRINDEX("($stat_min)", "Inf") GT 0 OR \
  STRINDEX("($stat_max)", "Inf") GT 0` THEN 

   IF `($input_fill_levels"0|0|1") EQ 0` THEN 
      DEFINE SYMBOL ferret_fill_levels = 40
      IF ($ferret_center_levels) THEN DEFINE SYMBOL ferret_fill_levels = 40c
   ENDIF
   IF `($input_contour_levels"0|0|1") EQ 0` THEN 
      DEFINE SYMBOL ferret_contour_levels = 40
      IF ($ferret_center_levels) THEN DEFINE SYMBOL ferret_contour_levels = 40c
   ENDIF

   EXIT/SCRIPT
ENDIF


! Compute rounded-off levels with the PPL %RANGE command.

!!!  Suggestions from Hankin
! Determine contour levels to use such that a fine density of levels is used within +/- 1 std dev
! and a coarse density is used out to +/- 2 std dev.
! The ratio of densities is density_factor = fine/coarse.

LET density_factor = 4
! fraction_that_are_fine = density_factor/(1+density_factor)

! This can happen if the data is a constant value...
IF `($stat_min) EQ ($stat_max)` THEN EXIT/SCRIPT

IF `($num_fine_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL num_fine_levels = `(density_factor/(1+density_factor)) * ($num_contour_levels"30")`
DEFINE SYMBOL num_coarse_levels =  `($num_contour_levels"30") - ($num_fine_levels)`
IF `($num_coarse_levels) LT 0` THEN DEFINE SYMBOL num_coarse_levels = 2
DEFINE SYMBOL half_num_coarse_levels = `($num_coarse_levels)/2`

! Compute rounded-off levels with the PPL %RANGE command.

LET nstd = 2*($stat_std)
LET nstd = 1*($stat_std)

IF ($stat_mean"1|bad>1|*>0") THEN
  DEFINE SYMBOL error_string "The ($data_var)[d=($data_num)] variable has no valid data"
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Use the statistics above to actually compute the levels

IF ($ferret_center_levels) THEN
   GO LAS_centered_levels
ELSE
   GO LAS_non_centered_levels
ENDIF

! Use these levels to reset ferret_contour_levels and ferret_fill_levels 

IF `($input_contour_levels"0|0|1") EQ 0` THEN \
   DEFINE SYMBOL ferret_fill_levels = ($ferret_auto_levels)
IF `($input_contour_levels"0|0|1") EQ 0` THEN \
   DEFINE SYMBOL ferret_contour_levels = ($ferret_auto_lines)

! End of file ------------LAS_auto_levels.jnl--------------------------
