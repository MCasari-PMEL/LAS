! LAS_auto_levels.jnl
! ACM 8/22/2007
! acm 9/11/07 If its a descriptor file Ferret wont allow striding. 
!             check for that case and just set num levels.

! Based on a variables value over its entire range, set strides
! if needed to quickly get stats on the variable. Use this to set levels.

! Argument: the variable to use in setting the levels.

! The symbol ferret_center_levels, if set to 1, forces the levels
! to be 2 std above and below zero. Otherwise use 2 std above 
! and below the data mean

! This script should be done BEFORE any other striding is done.
! It operates on the variable on its entire native unstrided grid.

! input_fill_levels and input_contour_levels are symbols that are defined in 
! LAS_initialize_variable.jnl, telling whether settings for levels were
! made 

! Determine striding for speed in operating the STAT command
LET memsize = 0.5* 1.e6

! Make sure its a 2D plot operation.

DEFINE SYMBOL op = `UPCASE("($operation_ID)")`

IF `STRLEN("($ferret_view)") NE 2` THEN EXIT/SCRIPT
IF `STRINDEX("($op)", "VECTOR") GT 0` THEN EXIT/SCRIPT

IF `STRINDEX("($op)", "PLOT") EQ 0 AND \
  STRINDEX ("($operation_ID)", "zoom") EQ 0` THEN EXIT/SCRIPT

! Levels for animations set in LAS_auto_levels_animate, 
! called in list_frames.jnl
IF `STRINDEX("($op)", "ANIMATION") GT 0`  THEN EXIT/SCRIPT

! If its a compare plot, and if the ($ferret_diff_var) is not yet defined, 
! just return.

IF `($ferret_diff_var"1|*>0") AND STRINDEX("($op)", "COMPARE_PLOT") NE 0` THEN ! 1
   EXIT/SCRIPT
ENDIF !1 

DEFINE SYMBOL ferret_center_levels ($ferret_center_levels"0|1|TRUE>1|*>0")

! If levels were requested but not centered ones, just return.

LET nc = 0
IF ($input_fill_levels"0") THEN ! 2
   DEFINE SYMBOL up_fill_levels = `UPCASE("($ferret_fill_levels)")`
   LET nc = `STRINDEX("($up_fill_levels)", "C")`
   DEFINE SYMBOL ferret_center_levels = `nc GT 0`
   IF `nc EQ 0` THEN ! 3
      IF `($ferret_contour_levels"0|*>1") EQ 0` THEN \
        DEFINE SYMBOL ferret_contour_levels ($ferret_fill_levels)
     
      EXIT/SCRIPT
   ENDIF ! 3
 ENDIF ! 2

! See if centered levels were requested, either by setting the symbol ferret_center_levels
! or with a contour_levels or fill_levels that is centered.

! If contour levels requested with levels of nC, e.g. 20C, then get the value to
! set num_fine_levels in the %RANGE command below

IF ($ferret_contour_levels"0|*>1") THEN ! 4
   DEFINE SYMBOL up_contour_levels = `UPCASE("($ferret_contour_levels)")`
   LET nc = `STRINDEX("($up_contour_levels)", "C")`
   DEFINE SYMBOL ferret_center_levels = `nc GT 0`
   IF `nc GT 0` THEN ! 5
      LET slen = STRLEN ("($up_fill_levels)") 
      IF `slen GT 1` THEN DEFINE SYMBOL num_fine_levels = `SUBSTRING ("($up_fill_levels)", 1, slen-1)`
   ENDIF ! 5
ENDIF ! 4

IF ($ferret_fill_levels"0|*>1") THEN ! 6
   DEFINE SYMBOL up_fill_levels = `UPCASE("($ferret_fill_levels)")`
   LET nc = `STRINDEX("($up_fill_levels)", "C")`
   DEFINE SYMBOL ferret_center_levels = `nc GT 0`
   IF `nc GT 0` THEN  ! 7
      LET slen = STRLEN ("($up_fill_levels)") 
      IF `slen GT 1` THEN DEFINE SYMBOL num_fine_levels = `SUBSTRING ("($up_fill_levels)", 1, slen-1)`
   ENDIF ! 7
ENDIF ! 6
!! PUNT

! If levels other than centered were already given, exit
IF `nc EQ 0` THEN ! 8
  IF `($ferret_fill_levels"0|*>1") OR ($ferret_contour_levels"0|*>1")` THEN EXIT/SCRIPT
ENDIF ! 8

! Apply to the variable on the requested region

IF `($ferret_plot_var"0|*>1") EQ 0` THEN EXIT/SCRIPT  !e.g. for insitu plots may not have a plot_var

! When its a curvi plot, base the levels on the
! variable masked by the coordinates in the region.

IF ($native_curvilinear_xy"0") THEN
   STAT ($ferret_curvi_coord_lon)
   LET xreg_lo = ($region_x_lo)
   LET xreg_hi = ($region_x_hi)
   IF `xreg_hi LT ($stat_min)` THEN
      LET xreg_lo = `xreg_lo + 360`
      LET xreg_hi = `xreg_hi + 360`
   ENDIF
   IF `xreg_lo GT ($stat_max)` THEN
      LET xreg_lo = `xreg_lo - 360`
      LET xreg_hi = `xreg_hi - 360`
   ENDIF
   
   let maskx1 = IF ($ferret_curvi_coord_lon)[d=($data_num)] LT `xreg_hi` THEN 1
   let maskx =  IF ($ferret_curvi_coord_lon)[d=($data_num)] GE `xreg_lo` THEN maskx1
   let masky1 = IF ($ferret_curvi_coord_lat)[d=($data_num)] LT ($region_y_hi) THEN 1
   let masky =  IF ($ferret_curvi_coord_lat)[d=($data_num)] GT ($region_y_lo) THEN masky1
   LET pvar = ($data_var)[d=($data_num)]* maskx* masky
   IF ($region_zt"0|*>1") THEN LET pvar = ($data_var)[d=($data_num),($region_zt)]* maskx* masky

! ??
   IF ($do_curvi_xy_and_hybrid_z"0") THEN LET pvar = plot_var* maskx* masky

ELSE

   LET pvar = ($ferret_plot_var)

ENDIF

IF ($ferret_plot_var"0|sampled_var>1|*>0) THEN LET pvar = ($ferret_plot_var)

! IF ($do_curvi_xy_and_hybrid_z"0") THEN LET pvar = plot_var

DEFINE SYMBOL have_pvar = 1

! cannot do native striding if this is a descriptor dataset. In that case
! just set the number of levels

DEFINE SYMBOL check_for_des =  `pvar,return=dsetpath`
IF `STRINDEX("($check_for_des)", ".des") GT 0` THEN ! 9
   IF `($input_contour_levels"0|0|1") EQ 0` THEN \
   DEFINE SYMBOL ferret_fill_levels = ($num_fine_levels"30")
   IF `($input_contour_levels"0|0|1") EQ 0` THEN \
   DEFINE SYMBOL ferret_contour_levels = ($num_fine_levels"30")
   EXIT/SCRIPT
ENDIF ! 9

! If the ferret_diff_var has been defined, use it.
! When no levels are defined, use centered ones.
IF ($ferret_diff_var"0|*>1") THEN  ! 10
   IF `($input_contour_levels"0|0|1") EQ 0` THEN  ! 11
      CANCEL SYMBOL ferret_contour_levels
      DEFINE SYMBOL ferret_contour_levels = 20c
      DEFINE SYMBOL ferret_center_levels = 1
   ENDIF ! 11

   IF `($input_fill_levels"0|0|1") EQ 0` THEN ! 12
      CANCEL SYMBOL ferret_fill_levels
      DEFINE SYMBOL ferret_fill_levels = 20c
      DEFINE SYMBOL ferret_center_levels = 1
   ENDIF ! 12
   LET pvar = ($ferret_diff_var)

ENDIF ! 10

LET var_size = ABS( `pvar,RETURN=size`)
DEFINE SYMBOL var_dim = `pvar,RETURN=shape`
LET ndims = STRLEN("($var_dim)")

LET stride_for_speed = `INT( 1+ ((var_size/memsize))/ndims )`

! If axes are not already strided, apply simple striding which
! we will undo later.
IF `stride_for_speed GT 1` THEN ! 13

   IF `($xstride"0|*>1") EQ 0` THEN ! 14
     SET AXIS/STRIDE=`stride_for_speed` `pvar,RETURN=xaxis`
     DEFINE SYMBOL undo_x = 1
   ENDIF ! 14

   IF `($ystride"0|*>1") EQ 0` THEN  ! 15
     SET AXIS/STRIDE=`stride_for_speed` `pvar,RETURN=yaxis`
     DEFINE SYMBOL undo_y = 1
   ENDIF ! 15

   IF `($tstride"0|*>1") EQ 0` THEN  ! 16
     SET AXIS/STRIDE=`stride_for_speed` `pvar,RETURN=taxis`
     DEFINE SYMBOL undo_t = 1
   ENDIF ! 16

ENDIF ! 13

! If already strided, do index strides.
IF `($native_curvilinear_xy"0") EQ 0` THEN  ! region syms dont apply to native curv grid directly

IF ($xstride"0|1>0|*>1") THEN  ! 17
   LET xsize  `pvar,RETURN=isize`
   IF `xsize GT 50` THEN ! 18
     let strd_lo = `i[gx=pvar,x=($region_x_lo)]`
     let strd_hi = `i[gx=pvar,x=($region_x_hi)]`
     DEFINE SYMBOL index_strides = i=`strd_lo`:`strd_hi`:`INT(xsize/10)`
   ENDIF ! 18
ENDIF ! 17

IF ($ystride"0|1>0|*>1") THEN  ! 19
   LET ysize  `pvar,RETURN=jsize`
   IF `ysize GT 50` THEN  ! 20
     let strd_lo = `j[gy=pvar,y=($region_y_lo)]`
     let strd_hi = `j[gy=pvar,y=($region_y_hi)]`
     IF ($index_strides"0|*>1") THEN  ! 21
        DEFINE SYMBOL index_strides = ($index_strides),j=`strd_lo`:`strd_hi`:`INT(ysize/10)`
     ELSE
        DEFINE SYMBOL index_strides = j=`strd_lo`:`strd_hi`:`INT(ysize/10)`
     ENDIF ! 21
   ENDIF ! 20
ENDIF ! 19

IF ($zstride"0|1>0|*>1") THEN  ! 23
   LET zsize  `pvar,RETURN=ksize`
   IF `zsize GT 50` THEN  ! 24
     let strd_lo = `k[gz=pvar,z=($region_z_lo)]`
     let strd_hi = `k[gz=pvar,z=($region_z_hi)]`
     IF ($index_strides"0|*>1") THEN  ! 25
        DEFINE SYMBOL index_strides = ($index_strides),k=`strd_lo`:`strd_hi`:`INT(zsize/10)`
     ELSE
        DEFINE SYMBOL index_strides = k=`strd_lo`:`strd_hi`:`INT(zsize/10)`
     ENDIF ! 25
   ENDIF ! 24
ENDIF ! 23

IF ($tstride"0|1>0|*>1") THEN  ! 26
   LET tsize  `pvar,RETURN=lsize`
   IF `tsize GT 50` THEN  ! 27
     IF ($index_strides"0|*>1") THEN  ! 28
        let strd_lo = `L[gt=pvar,t=($region_t_lo)]`
        let strd_hi = `L[gt=pvar,t=($region_t_hi)]`
        DEFINE SYMBOL index_strides = ($index_strides),`strd_lo`:`strd_hi`:`INT(tsize/10)`
     ELSE
        DEFINE SYMBOL  index_strides = `strd_lo`:`strd_hi`:`INT(tsize/10)`
     ENDIF ! 28
   ENDIF ! 27
ENDIF ! 26

ENDIF ! region syms dont apply to native curv grid directly


IF ($index_strides"0|*>1") THEN  ! 29
   DEFINE SYMBOL ppvar = pvar[($index_strides)]
ELSE
   DEFINE SYMBOL ppvar = pvar
ENDIF ! 29

! Execute the stat command to load symbols with the variables
! mean, max, min, std.

DEFINE SYMBOL stat_ngood= 0
STAT ($ppvar)

! Undo the striding. Other striding may be set elsewhere

IF `stride_for_speed GT 1` THEN  ! 30
   IF `($undo_x"0") GT 0` THEN CANCEL AXIS/STRIDE `pvar,RETURN=xaxis`
   IF `($undo_y"0") GT 0` THEN CANCEL AXIS/STRIDE `pvar,RETURN=yaxis`
   IF `($undo_z"0") GT 0` THEN CANCEL AXIS/STRIDE `pvar,RETURN=zaxis`
   IF `($undo_t"0") GT 0` THEN CANCEL AXIS/STRIDE `pvar,RETURN=taxis`
ENDIF ! 30

sh sym stat*
IF `($stat_ngood"0") EQ 0` THEN EXIT/SCRIPT

! Compute rounded-off levels with the PPL %RANGE command.

!!!  Suggestions from Hankin
! Determine contour levels to use such that a fine density of levels is used within +/- 1 std dev
! and a coarse density is used out to +/- 2 std dev.
! The ratio of densities is density_factor = fine/coarse.

LET density_factor = 4
! fraction_that_are_fine = density_factor/(1+density_factor)

! This can happen if the data is a constant value...
IF `($stat_min) EQ ($stat_max)` THEN EXIT/SCRIPT

DEFINE SYMBOL num_fine_levels = `(density_factor/(1+density_factor)) * ($num_contour_levels"30")`
DEFINE SYMBOL num_coarse_levels =  `($num_contour_levels"30") - ($num_fine_levels)`
DEFINE SYMBOL half_num_coarse_levels = `($num_coarse_levels)/2`

! Compute rounded-off levels with the PPL %RANGE command.

LET nstd = 2*($stat_std)
LET nstd = 1*($stat_std)

IF ($stat_mean"bad>1|bad>1|*>0") THEN  ! 31
  DEFINE SYMBOL error_string "The ($data_var)[d=($data_num)] variable has no valid data"
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF ! 31

! Use the statistics above to actually compute the levels

IF ($ferret_center_levels) THEN ! 32
   GO LAS_centered_levels
ELSE
   GO LAS_non_centered_levels
ENDIF ! 32

! Use these levels to reset ferret_contour_levels and ferret_fill_levels 

IF `($input_contour_levels"0|0|1") EQ 0` THEN \
   DEFINE SYMBOL ferret_fill_levels = ($ferret_auto_levels)
IF `($input_contour_levels"0|0|1") EQ 0` THEN \
   DEFINE SYMBOL ferret_contour_levels = ($ferret_auto_lines)

! End of file ------------LAS_auto_levels.jnl--------------------------

