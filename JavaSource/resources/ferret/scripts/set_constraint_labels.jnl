! set_constraint_labels.jnl
! Make labels from the constraints. Call a second
! script as the command line from a single REPEAT
! statement containing all the commands could get too long.

! For prop-prop plots, dont make labels about constraining
! the variables that make up the 2 axes of the plot.

! If all labeling turned off just exit.
IF `($ferret_label"1|0|1|*>1") EQ 0` THEN EXIT/SCRIPT

IF ($data_prop_var"0|*>1") THEN

REPEAT/RANGE=0:3:1/NAME=num ( \
  DEFINE SYMBOL num = `num`; \
  IF ($constraint_($num)_lhs"1|*>0") THEN EXIT/LOOP ; \
  DEFINE SYMBOL lhs = ($constraint_($num)_lhs); \
  IF ($lhs"0|($data_0_var)>1|*>0") THEN EXIT/CYCLE;\
  IF ($lhs"0|($data_1_var)>1|*>0") THEN EXIT/CYCLE;\
  IF ($lhs"0|($data_prop_var)>1|*>0") THEN EXIT/CYCLE;\
  DEFINE SYMBOL op = ($constraint_($num)_op); \
  DEFINE SYMBOL rhs = ($constraint_($num)_rhs); \
  IF `($lhs"99|*>0") EQ 99` THEN EXIT/CYCLE; \
  GO make_c_lab)
ELSE

REPEAT/RANGE=0:3:1/NAME=num ( \
  DEFINE SYMBOL num = `num`; \
  IF ($constraint_($num)_lhs"1|*>0") THEN EXIT/LOOP ; \
  DEFINE SYMBOL lhs = ($constraint_($num)_lhs); \
  DEFINE SYMBOL op = ($constraint_($num)_op); \
  DEFINE SYMBOL rhs = ($constraint_($num)_rhs); \
  IF `($lhs"99|*>0") EQ 99` THEN EXIT/CYCLE; \
  GO make_c_lab)

ENDIF

!-----------------End of set_constraint_labels ---------------------------------
