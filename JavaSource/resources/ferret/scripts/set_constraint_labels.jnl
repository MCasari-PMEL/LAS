! set_constraint_labels.jnl
! Make labels from the constraints. Call a second
! script as the command line from a single REPEAT
! statement containing all the commands could get too long.

! If all labeling turned off just exit.
IF `($ferret_label"1|0|1|*>1") EQ 0` THEN EXIT/SCRIPT

! How many constraints could there be?  Should probably get 
! this from the server...

DEF SYM has_missing = 0

! The first EXIT should really be EXIT/LOOP...

REPEAT/RANGE=0:101:1/NAME=v ( \
  DEFINE SYMBOL v = `v`; \
  IF ($constraint_($v)_lhs"1|*>0") THEN EXIT/CYCLE ; \
  DEFINE SYMBOL lhs = ($constraint_($v)_lhs); \
  DEFINE SYMBOL op = ($constraint_($v)_op); \
  DEFINE SYMBOL rhs = ($constraint_($v)_rhs); \
  IF ($rhs"NaN>1|*>0") THEN DEFINE SYMBOL rhs = valid; \
  IF `($lhs"99|*>0") EQ 99` THEN EXIT/CYCLE; \
  GO make_c_lab)

IF `($nmask_labels"0") GT 0` THEN
   REPEAT/RANGE=1:($nmask_labels)/NAME=mlab (\
   DEFINE SYMBOL mlab = `mlab`;\
   DEFINE SYMBOL note_num `($note_num"0") + 1`; \
   IF ($mask_title_($mlab)"0|*>1") THEN DEFINE SYMBOL note_($note_num)_lab =  ($mask_title_($mlab)); \
   IF `STRINDEX("($note_($note_num)_lab)", "expocode") GT 0` THEN DEFINE SYMBOL cruises_labeled = 1; \
   )
ENDIF

! Label the cruises that came in with ferret_cruise_list.
! If there was also a constraint on expocode, then that 
! takes precedence; list only that.

IF `($ferret_cruise_list"0|*>1") AND (($cruises_labeled"0|*>1") EQ 0)` THEN
   DEFINE SYMBOL note_num `($note_num"0") + 1`

   LET slen = STRLEN("($ferret_cruise_list)")
   DEFINE SYMBOL lastletter = `SUBSTRING("($ferret_cruise_list)", slen, 1)`
   IF `STRCMP("($lastletter)", ",") EQ 0` THEN
      DEFINE SYMBOL note_($note_num)_lab = Where cruise expocode = `SUBSTRING("($ferret_cruise_list)", 1, slen-1)`
   ELSE
      DEFINE SYMBOL note_($note_num)_lab = Where cruise expocode = ($ferret_cruise_list)
   ENDIF

ENDIF

!-----------------End of set_constraint_labels ---------------------------------

