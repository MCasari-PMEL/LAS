! set_linesym.jnl
! set plot type: line, symbols, or both.  If there are up to 120 
! points use line-and-symbol.Use STAT to get # of points, and count 
! both bad andgood -- avoids inconsistencies if there are a few 
! missing points in one series and not in another.

LET plotvar  = ($ferret_plot_var)
LET filledvar = missing(plotvar,0)
let ngood = plotvar[i=@ngd,j=@ngd,k=@ngd,l=@ngd,m=@ngd,n=@ngd]
let nbad =  plotvar[i=@nbd,j=@nbd,k=@nbd,l=@nbd,m=@nbd,n=@nbd]

LET npoints = ngood + nbad

LET nminpts = 120
IF ($itsa_multi_lineplot"0|*>1") THEN LET nminpts = 15

IF `npoints LT nminpts AND ($plotsymbol"0|*>1")` THEN
   DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_line_or_sym%/LINE/SYM=($plotsymbol)|default>/LINE/SYM=($plotsymbol)|line>/LINE|both>/LINE/SYM=($plotsymbol)|sym>/SYM=($plotsymbol)%)
   DEFINE SYMBOL legend_qual = ($legend_qual)($ferret_line_or_sym%/LINE/SYM=($plotsymbol)|default>/LINE/SYM=($plotsymbol)|line>/LINE|both>/LINE/SYM=($plotsymbol)|sym>/SYM=($plotsymbol)%)
ELSE 
   DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_line_or_sym%/LINE|default>/LINE|line>/LINE|both>/LINE/SYM=($plotsymbol)|sym>/SYM=($plotsymbol)%)
   DEFINE SYMBOL legend_qual = ($legend_qual)($ferret_line_or_sym%/LINE|default>/LINE|line>/LINE|both>/LINE/SYM=($plotsymbol)|sym>/SYM=($plotsymbol)%)
ENDIF
