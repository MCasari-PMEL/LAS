! LAS_open_window.jnl
! 
! Author: ansley 
! Date: 2007/02/12 
!
! The LAS_open_windw.jnl script takes care of everything having to
! do with starting the window.
!
!
! - Open the window with the desired size
! - Issue CANCEL MODE LOGO command
! - If an XY plot and if this has been requested, set the aspect 
!   ratio to match the region
! - If no margins (WMS style plot) is requested, set that up, 
!   including the /NOYADJUST plot qualifier.


SET WINDOW/SIZE = ($ferret_size"0.5")
CANCEL MODE LOGO

IF ($ferret_view"|xy>1|*>0") THEN
  IF ($ferret_set_aspect"1|default>1|no>0|yes>1|0|1") THEN SET WINDOW/ASPECT = `($region_Y_range)/($region_X_range)`:axis
ENDIF

IF `($ferret_margins"1|0|1|false>0|true>1") EQ 0` THEN
   DEFINE VIEW/AXES/XLIM=0:1/YLIM=0:1 WMSview
   SET VIEW WMSview
   IF `STRLEN("($ferret_view)") LT 2` THEN DEFINE SYMBOL qualifiers = ($qualifiers)/NOYADJUST
!   DEFINE SYMBOL qualifiers ($qualifiers)/NOAXES

ELSE
! For 2D plots, make room for the colorbar labels which sometimes get cut off.
   IF `STRLEN("($ferret_view)") EQ 2` THEN 
      DEFINE VIEW/X=0:0.95/Y=0:1 xspace
      SET VIEW xspace
   ENDIF

ENDIF
