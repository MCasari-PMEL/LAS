! LAS_open_window.jnl
! 
! Author: ansley 
! Date: 2007/02/12 
! 7/2010    ACM updates for V7.2 multi-variable plots
!
! The LAS_open_windw.jnl script takes care of everything having to
! do with starting the window.
!
!  This script should be called AFTER LAS_set_plot_qualifiers.jnl,
!  if the set-qualifiers script is called. 
!
! - Open the window with the desired size
! - Issue CANCEL MODE LOGO command
! - If an XY plot and if this has been requested, set the aspect 
!   ratio to match the region
! - If no margins (WMS style plot) is requested, set that up, 
!   including the /NOYADJUST plot qualifier.


IF `($PROGRAM_NAME"0|PyFerret>1|*>0")` THEN DEFINE SYMBOL ferret_size = 1.5

SET WINDOW/SIZE = ($ferret_size"0.5")
CANCEL MODE LOGO

IF ($ferret_view"|xy>1|xyt>1|*>0") THEN
  IF ($ferret_set_aspect"1|default>1|no>0|yes>1|0|1") THEN
     IF `($region_x_range"0") NE 0 AND ($region_y_range"0") NE 0` THEN \
     SET WINDOW/ASPECT = `($region_Y_range)/($region_X_range)`:axis
  ENDIF
  IF `($region_Y_range)/($region_X_range) GT 100` THEN \
     SET WINDOW/ASPECT = 1:axis
  IF `($region_Y_range)/($region_X_range) LT 0.01` THEN \
     SET WINDOW/ASPECT = 1:axis
ENDIF

IF `($ferret_margins"1|0|1|false>0|true>1") EQ 0` THEN
   DEFINE VIEW/AXES/XLIM=0:1/YLIM=0:1 WMSview
   SET VIEW WMSview
   IF `STRLEN("($ferret_view)") LT 2` THEN DEFINE SYMBOL qualifiers = ($qualifiers)/NOYADJUST
!   DEFINE SYMBOL qualifiers ($qualifiers)/NOAXES

ELSE
! For 2D plots, make room for the colorbar labels which sometimes get cut off.
   IF `STRLEN("($ferret_view)") GE 2` THEN 
      DEFINE VIEW/X=0:0.95/Y=0:1 xspace
      SET VIEW xspace
   ELSE
! For 1D plots we may be doing a line key in a new viewport, so set the initial viewport.
      SET VIEW full
      IF ($ferret_view"0|z>1|*>0") THEN  
         SET WIN/asp=.8
      ELSE
         SET WIN/asp=0.6
      ENDIF
   ENDIF

ENDIF

! If the annotations are going to be collected and sent back to LAS for presentation,
! set up the viewport for small margins around the edge.
IF ($ferret_annotations"0|*>1") THEN GO set_full_margins
