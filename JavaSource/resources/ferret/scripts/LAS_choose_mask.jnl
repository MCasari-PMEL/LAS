! LAS_choose mask.jnl
! Given a data source, data variable, x-range and y-range  determine which bathymetry dataset 
! to use, and try opening it.

! Assumes symbols 
!   data_source
!   data_var 
!   region_x_lo, region_x_hi
!   region_y_lo, region_y_hi

! Sets the following output symbol.
!  ferret_masking_dataset

! The input data source is some dataset or URL.

SET DATA ($data_source)

! Subscript limits in XY plane

LET istart `i[gx=($data_var), x=($region_x_lo)]`
LET iend `i[gx=($data_var), x=($region_x_hi)]`
LET jstart `j[gy=($data_var), y=($region_y_lo)]`
LET jend `j[gy=($data_var), y=($region_y_hi)]`

! Coordinate limits in the XY plane

LET xstart `x[GX=($data_var), x=($region_x_lo)]`
LET xend `x[gx=($data_var), x=($region_x_hi)]`
LET ystart `y[GY=($data_var), y=($region_y_lo)]`
LET yend `y[gy=($data_var), y=($region_y_hi)]`

! Decide on a bathymetry dataset to use
!  Etopo  boxlen  boxsize=dx*dy   value between areas           value between lengths
!    05    0.083   0.00694      (0.00694+0.1111)/2 = 0.059   ((0.083+0.333)/2)^2 = 0.0433
!    20    0.333   0.1111       (0.1111+0.4444)/2 = 0.278    ((.333+.667)/2)^2 = 0.25
!    40    0.667   0.4444       (0.4444+1)/2 = 0.722         ((0.667+1)/2)^2 = 0.695
!    60    1.0     1.0          (1+4)/2 = 2.5                ((1+2)/2)^2 = 2.25
!   120    2.0     4.0

! An approximation for the fineness of the input data grid.
LET dat_box_area = ( (xend-xstart)*(yend-ystart) ) / ( (iend-istart)*(jend-jstart) )

IF `dat_box_area LE 0.0433` THEN DEFINE SYMBOL mask_resolution = 05
IF `dat_box_area GT 0.0433  AND dat_box_area LE 0.250` THEN DEFINE SYMBOL mask_resolution = 20
IF `dat_box_area GT 0.250   AND dat_box_area LE 0.695` THEN DEFINE SYMBOL mask_resolution = 40
IF `dat_box_area GT 0.695   AND dat_box_area LE 2.25`  THEN DEFINE SYMBOL mask_resolution = 60
IF `dat_box_area GT 2.25`  THEN DEFINE SYMBOL mask_resolution = 120

! Try opening the file. If not available, the error_string gives an explanation.

DEFINE SYMBOL error_string "ETOPO($mask_resolution) dataset is used for masking"
SET DATA etopo($mask_resolution)

DEFINE SYMBOL ferret_masking_dataset = etopo($mask_resolution)

! Clean up
CANCEL DATA etopo($mask_resolution)
IF ($data_source"0|($ferret_masking_dataset)>0|*>1") THEN CANCEL DATA ($data_source)
