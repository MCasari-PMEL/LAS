!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! xy_vector_plot.jnl
! 
! $Author: ansley $
! $Date: 2006/10/05 $
!
! 3/7/2007: ACM. Put code to check that the region was not too small, resulting 
!           in degenerate plot into LAS_initialize_data.jnl rather than in the 
!           individual plot scripts.
! 3/29/2007 ACM Fix logic interpreting contents of ferret_interpolate_data 
! 3/17/2008 ACM Add /FLOWLINE[/DENSITY] options and also line color and thickness
!
! xy_vector_plot.jnl creates an XY vector plot for use with the Back End Server
! code that sits behind a Live Access Server (LAS). 
!
! NOTE:  This code handles overlays but not differencing.
!
! Set any Ferret modes
!
! NOTE:  Should we support any of the following Ferrt modes?
! NOTE:    ASCII_FONT, CALENDAR, DEPTH_LABEL, LABELS, LATIT_LABEL, LONG_LABEL 

IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL fview = `UPCASE("($ferret_view)")`

! Define symbols associated with the regign and data and perform any
! initialization needed for this dataset.
!
! NOTE:  Adding support for a 'base layer' variable would mean passing
! NOTE:  some other argument.  For now, though, we always use 'data_0'
! NOTE:  for the base layer and region.

GO LAS_initialize_region 0
GO LAS_initialize_data 0

DEFINE SYMBOL ferret_vector_1 ($ferret_plot_var)
DEFINE SYMBOL ferret_plot_title_1 "($data_title) ($data_units)"
DEFINE SYMBOL ferret_vector_1_noregion ($ferret_plot_var_0_noregion)

GO LAS_initialize_data 1
DEFINE SYMBOL ferret_vector_2 ($ferret_plot_var)
DEFINE SYMBOL ferret_plot_title "($ferret_plot_title_1), ($data_title) ($data_units)"
DEFINE SYMBOL ferret_vector_2_noregion ($ferret_plot_var_1_noregion)

! See if the vector components are on staggered grids
GO LAS_check_staggered

! Testing the length-animation script
!GO LAS_auto_veclen_animate

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small; reduces to a single grid point.
! (Region is a point)
GO LAS_check_2d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Trying a hack to close the gap on GE plots. Not very effective
!IF `(($native_curvilinear_xy"0") EQ 0) AND (($its_GE"0|*>1") EQ 1)` THEN 
!   LET delx = `xbox[gx=($ferret_plot_var),x=($region_x_lo)]`
!
!   IF `($region_x_range) GT (360-1.1*delx)` THEN
!      DEFINE SYMBOL region_0_x_lo = `($region_0_x_lo) - delx`
!      DEFINE SYMBOL region_0_x_hi = `($region_0_x_hi) + delx`
!      DEFINE SYMBOL qualifiers = ($qualifiers)/HLIM=($region_0_x_lo):($region_0_x_hi)
!      GO LAS_initialize_region 0
!   ENDIF
!ENDIF   

IF `($its_GE"0|*>1") ` THEN 

! This when we get transparency working. Land outlinen setup in land_overlay.jnl
!   SET MODE LINECOLORS:7
!   PPL COLOR,7,100,50,0  ! orange lines
!   IF `($ferret_line_color%1|default>1|*>2%) EQ 1` THEN DEFINE SYMBOL ferret_line_color=7

   IF `($ferret_line_color%1|default>1|*>2%) EQ 1` THEN DEFINE SYMBOL ferret_line_color = blue

ENDIF

! Open the window, apply size, scale options
GO LAS_open_window


! Build up the plot qualifiers

! Always use /ASPECT adjusts scaling based upon the ratio of the 
! axis scale factors (meters on the Earth to inches on the plot) and 
! it preserves the vector magnitudes -- adjusting only the orientation. 

!DEFINE SYMBOL qualifiers  = ($qualifiers)/ASPECT/NOLAB  ! but dont make a label about it.

IF `($ferret_line_color%0|default>1|*>2%) LT 2` THEN DEFINE SYMBOL ferret_line_color = blue

! Curvilinear plots may have more qualifiers. 
IF ($ferret_curvi_quals"0|*>1") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_curvi_quals)
ENDIF

IF ($ferret_use_graticules"1|0|*>1) THEN
  SET MODE GRATICULE:(DASH,COLOR=black)
ENDIF

! Set up for degrees-minutes-seconds labels on axes with units of degrees

IF ($ferret_deg_min_sec"0|false>0|*>1") THEN GO LAS_set_deg_min_sec.jnl

! Vector style arrow (default) or flowline=1
DEFINE SYMBOL flowline_vectors = 0
IF `($ferret_vector_style"0|default>0|1|*>0") GT 0` THEN  
   DEFINE SYMBOL qualifiers = ($qualifiers)/FLOWLINE
   DEFINE SYMBOL flowline_vectors = 1
ENDIF

! Vector density for flowline plots

IF `($ferret_vector_density"0|*>1") GT 0` THEN  
   DEFINE SYMBOL qualifiers = ($qualifiers)/DENSITY=($ferret_vector_density)
ENDIF

! Vector length scale

IF `($ferret_vector_length"0") GT 0` THEN  
   DEFINE SYMBOL qualifiers = ($qualifiers)/LENGTH=($ferret_vector_length)
ENDIF

! Vector xskip and yskip set by the user
IF ($ferret_vector_skip"0|*>1") THEN DEFINE SYMBOL ferret_vector_subsampling = ($ferret_vector_skip)

IF ($ferret_vector_subsampling"0|*>1") THEN
   LET comloc = `STRINDEX("($ferret_vector_subsampling)", ",")`
   LET nlen = `STRLEN("($ferret_vector_subsampling)")`
   IF `comloc GT 1` THEN
       DEFINE SYMBOL xskp = `SUBSTRING ("($ferret_vector_subsampling)", 1, comloc-1)`
       DEFINE SYMBOL yskp = `SUBSTRING ("($ferret_vector_subsampling)", comloc+1, nlen-comloc)`
    ELSE
       DEFINE SYMBOL xskp = ($ferret_vector_subsampling)
       DEFINE SYMBOL yskp = ($ferret_vector_subsampling)
    ENDIF

    LET xs = STRFLOAT ("($xskp)")
    LET ys = STRFLOAT ("($yskp)")

    IF ($error_status"0|*>1") THEN
       MESSAGE/ERROR **ERROR format of vector subsampling is xsub,ysub
       EXIT/PROGRAM
    ENDIF

    DEFINE SYMBOL qualifiers = ($qualifiers)/XSKIP=`xs`/YSKIP=`ys`

ENDIF

! Line color

IF `($ferret_line_color%0|default>0|*>1%)` THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/COLOR=($ferret_line_color%0|default>black|red|blue|green|lightblue|purple|7|*>black%)
ENDIF

! line thickness

IF `($ferret_line_thickness%0|default>1|*>1%)` THEN
   DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_line_thickness%/THICK=1|default>/THICK=1|1>/THICK=1|2>/THICK=2|3>/THICK=3|*>/THICK=1%)
ENDIF

! Set the URL label for the first dataset.
GO LAS_url_label 0

! If the title is long, split it before the second-component label
DEFINE SYMBOL split_title = @AS($ferret_plot_title)
DEFINE SYMBOL split_string = ($data_title)
GO LAS_split_vector_title.jnl 
DEFINE SYMBOL ferret_plot_title ($split_title)

! Draw  the plot. On VECTOR/SET the /XSKIP and /YSKIP are ignored. 

IF ($its_GE"0|*>1") THEN
   CANCEL MODE nodata_lab

! Do a shade underlay for GE plots
 	
   DEFINE SYMBOL shade_var = ($ferret_vector_1)
   IF ($native_curvilinear_xy"0") THEN DEFINE SYMBOL  shade_var = ($ferret_plot_base_var_0)
 	
   SHADE/pal=white/nokey/TITLE=" "/SET  ($shade_var)
      IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
      IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
   PPL SHADE

   DEFINE SYMBOL qualifiers = ($qualifiers)/OVER/NOLAB

ENDIF

! use XSKIP and YSKIP as qualifiers if given
IF ($ferret_vector_subsampling"0") THEN

   IF ($native_curvilinear_xy"0") THEN 

      VECTOR($qualifiers)/TITLE=" "/XSKIP=`xs`/YSKIP=`ys`/SET \
       ($ferret_plot_base_var_0), ($ferret_plot_base_var_1), xcoord, ycoord
       IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
       IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
       IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
       IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
       IF ($labnum_dods"0|*>1") THEN go unlabel ($labnum_dods)
      PPL VECTOR,`xs`,`ys` 
      
      VECTOR($qualifiers)/TITLE=" "/XSKIP=`xs`/YSKIP=`ys`/OVER/NOLAB \
       ($ferret_plot_base_var_0), ($ferret_plot_base_var_1), xcoord-360, ycoord

   ELSE
      VECTOR($qualifiers)/TITLE=" "/XSKIP=`xs`/YSKIP=`ys`/SET \
       ($ferret_vector_1), ($ferret_vector_2)
       IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
       IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
       IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
       IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
       IF ($labnum_dods"0|*>1") THEN go unlabel ($labnum_dods)
      PPL VECTOR,`xs`,`ys` 

   ENDIF

ELSE
   IF ($native_curvilinear_xy"0") THEN 
   ! Get the automatic vector skip values, then plot, making PPL settings.
      VECTOR($qualifiers)/TITLE=" "/NOLAB/COLOR=white \
      ($ferret_plot_base_var_0), ($ferret_plot_base_var_1), xcoord, ycoord

      VECTOR($qualifiers)/TITLE=" "/SET \
      ($ferret_plot_base_var_0), ($ferret_plot_base_var_1), xcoord, ycoord
       IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
       IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
       IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
       IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
       IF ($labnum_dods"0|*>1") THEN go unlabel ($labnum_dods)
       PPL VECTOR, ($PPL_VEC_XSKIP), ($PPL_VEC_YSKIP)

      VECTOR($qualifiers)/TITLE=" "/OVER/NOLAB/SET \
       ($ferret_plot_base_var_0), ($ferret_plot_base_var_1), xcoord-360, ycoord
       IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
       IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
       IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
       IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
       IF ($labnum_dods"0|*>1") THEN go unlabel ($labnum_dods)
       PPL VECTOR/over, ($PPL_VEC_XSKIP), ($PPL_VEC_YSKIP)

   ELSE
   ! Get the automatic vector skip values, then plot, making PPL settings.
      VECTOR($qualifiers)/TITLE=" "/NOLAB/COLOR=white \
      ($ferret_plot_base_var_0), ($ferret_plot_base_var_1)

      VECTOR($qualifiers)/TITLE=" "/SET ($ferret_vector_1), ($ferret_vector_2)
       IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
       IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
       IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
       IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
       IF ($labnum_dods"0|*>1") THEN go unlabel ($labnum_dods)
       PPL VECTOR, ($PPL_VEC_XSKIP), ($PPL_VEC_YSKIP)


   ENDIF
ENDIF

! Label with skip, if style not flowline=1 and if skip >1 in either direction.
! If striding and subsampling have both been done, combine for one label.

IF `($flowline_vectors"0") EQ 0` THEN  
   IF `( ($PPL_VEC_XSKIP"0") GT 1)  OR (($PPL_VEC_YSKIP"0") GT 1)` THEN

      LET xs = ($PPL_VEC_XSKIP)
      LET ys = ($PPL_VEC_YSKIP)
      IF `STRINDEX("($upper_left_($n_left_labels))", "Subsampled") GT 0` THEN
         DEFINE SYMBOL n_left_labels = `($n_left_labels"0")-1`
         IF `($AXIS_STRIDE_Y"0|*>1") GT 0` THEN LET ys = `ys* ($AXIS_STRIDE_Y)`
         IF `($AXIS_STRIDE_X"0|*>1") GT 0` THEN LET xs = `xs* ($AXIS_STRIDE_X)`
      ENDIF

      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels) = \
      Vector subsampling x=`xs`, y=`ys`

   ELIF ($ferret_vector_subsampling"0") THEN

      IF `STRINDEX("($upper_left_($n_left_labels))", "Subsampled") GT 0` THEN
         DEFINE SYMBOL n_left_labels = `($n_left_labels"0")-1`
         IF `($AXIS_STRIDE_Y"0|*>1") GT 0` THEN LET ys = `ys* ($AXIS_STRIDE_Y)`
         IF `($AXIS_STRIDE_X"0|*>1") GT 0` THEN LET xs = `xs* ($AXIS_STRIDE_X)`
      ENDIF

      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels) = \
      Vector subsampling x=`xs`, y=`ys`

   ENDIF
ENDIF

! Overall header at the very top
GO LAS_ferret_las_version_header

LABEL/NOUSER `($ppl$xlen)/2`,-0.8, 0, 0, 0.13, ($ferret_plot_title)


! Overlay additional cartography data (land mask, outlines, rivers, ...)

go land_overlay

GO labels_above_plot

! Mark grid points. Options are no, subsample (tests if subsampling needed)
! or all to force marking all points.

IF ($ferret_mark_grid"0|no>0|all>1|subsample>1|*>0") THEN GO mark_grid

! Save the results
GO LAS_results box

! End of $RCSfile -----------------------------------------------------
