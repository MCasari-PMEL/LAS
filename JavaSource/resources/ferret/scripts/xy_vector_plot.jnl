!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! xy_vector_plot.jnl
! 
! $Author: ansley $
! $Date: 2006/10/05 $
!
! 3/7/2007: ACM. Put code to check that the region was not too small, resulting 
!           in degenerate plot into LAS_initialize_data.jnl rather than in the 
!           individual plot scripts.
! 3/29/2007 ACM Fix logic interpreting contents of ferret_interpolate_data 
! 3/17/2008 ACM Add /FLOWLINE[/DENSITY] options and also line color and thickness
!
! xy_vector_plot.jnl creates an XY vector plot for use with the Back End Server
! code that sits behind a Live Access Server (LAS). 
!
! NOTE:  This code handles overlays but not differencing.
!
! Set any Ferret modes
!
! NOTE:  Should we support any of the following Ferrt modes?
! NOTE:    ASCII_FONT, CALENDAR, DEPTH_LABEL, LABELS, LATIT_LABEL, LONG_LABEL 

IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL fview = `UPCASE("($ferret_view)")`

! Define symbols associated with the regign and data and perform any
! initialization needed for this dataset.
!
! NOTE:  Adding support for a 'base layer' variable would mean passing
! NOTE:  some other argument.  For now, though, we always use 'data_0'
! NOTE:  for the base layer and region.

GO LAS_initialize_region 0
GO LAS_initialize_data 0
DEFINE SYMBOL ferret_vector_1 ($ferret_plot_var)
DEFINE SYMBOL ferret_plot_title_1 "($data_title) ($data_units)"

GO LAS_initialize_data 1
DEFINE SYMBOL ferret_vector_2 ($ferret_plot_var)
DEFINE SYMBOL ferret_plot_title "($ferret_plot_title_1), ($data_title) ($data_units)"

! See if the vector components are on staggered grids
GO LAS_check_staggered

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small; reduces to a single grid point.
! (Region is a point)
GO LAS_check_2d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Open the window, apply size, scale options
GO LAS_open_window

! Build up the plot qualifiers

! Always use /ASPECT adjusts scaling based upon the ratio of the 
! axis scale factors (meters on the Earth to inches on the plot) and 
! it preserves the vector magnitudes -- adjusting only the orientation. 

DEFINE SYMBOL qualifiers  = ($qualifiers)/ASPECT

IF ($ferret_plot_key"0|0|nokey>0|*>1") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/NOKEY
ENDIF

! Curvilinear plots may have more qualifiers. 
IF ($ferret_curvi_quals"0|*>1") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_curvi_quals)
ENDIF

IF ($ferret_use_graticules"1|0|*>1) THEN
  SET MODE GRATICULE:(DASH,COLOR=black)
ENDIF

! Set up for degrees-minutes-seconds labels on axes with units of degrees

IF ($ferret_deg_min_sec"0|false>0|*>1") THEN GO LAS_set_deg_min_sec.jnl

! Vector style arrow (default) or flowline

IF `($ferret_vector_style"0|default>0|1|*>0") GT 0` THEN  
   DEFINE SYMBOL qualifiers = ($qualifiers)/FLOWLINE
ENDIF
IF `($ferret_land_type"0|contour>1|*>0") EQ 1` THEN DEFINE SYMBOL ferret_land_arg = thick

! Vector density for flowline plots

IF `($ferret_vector_density"0|*>1") GT 0` THEN  
   DEFINE SYMBOL qualifiers = ($qualifiers)/DENSITY=($ferret_vector_density)
ENDIF

! Vector length scale

IF `($ferret_vector_length"0") GT 0` THEN  
   DEFINE SYMBOL qualifiers = ($qualifiers)/LENGTH=($ferret_vector_length)
ENDIF

! Vector xskip and yskip

IF ($ferret_vector_skip"0|*>1") THEN
   LET comloc = `STRINDEX("($ferret_vector_skip)", ",")`
   LET nlen = `STRLEN("($ferret_vector_skip)")`
   IF `comloc GT 1` THEN
       DEFINE SYMBOL xskp = `SUBSTRING ("($ferret_vector_skip)", 1, comloc-1)`
       DEFINE SYMBOL yskp = `SUBSTRING ("($ferret_vector_skip)", comloc+1, nlen-comloc)`
    ELSE
       DEFINE SYMBOL xskp = ($ferret_vector_skip)
       DEFINE SYMBOL yskp = ($ferret_vector_skip)
    ENDIF

    LET xs = STRFLOAT ("($xskp)")
    LET ys = STRFLOAT ("($yskp)")

    IF ($error_status"0|*>1") THEN
       MESSAGE/ERROR **ERROR format of vector skip is xskip,yskip
       EXIT/PROGRAM
    ENDIF

    DEFINE SYMBOL qualifiers = ($qualifiers)/XSKIP=`xs`/YSKIP=`ys`

ENDIF

! Line color

IF `($ferret_line_color%0|default>0|*>1%)` THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/COLOR=($ferret_line_color%0|default>black|red|blue|green|lightblue|purple|*>black%)
ENDIF

! line thickness

IF `($ferret_line_thickness%0|default>1|*>1%)` THEN
   DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_line_thickness%/THICK=1|default>/THICK=1|1>/THICK=1|2>/THICK=2|3>/THICK=3|*>/THICK=1%)
ENDIF

! Do the plot. On VECTOR/SET the /XSKIP and /YSKIP are ignored. 

! Do a shade underlay if deg-min labelling is requested, then we dont have
! to do VECTOR/SET which ignores the automatic skip 

DEFINE SYMBOL shade_var = ($ferret_vector_1)
IF ($native_curvilinear_xy"0") THEN DEFINE SYMBOL  shade_var = ($ferret_plot_base_var_0)

SHADE/pal=white/nokey/TITLE="($ferret_plot_title)"/SET  ($ferret_vector_1)
   IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
   IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))
PPL SHADE

! use XSKIP and YSKIP as qualifiers if given
IF ($ferret_vector_skip"0") THEN

   IF ($native_curvilinear_xy"0") THEN 
      VECTOR/OVER/NOLAB($qualifiers)/TITLE="($ferret_plot_title)"/XSKIP=`xs`/YSKIP=`ys` \
       ($ferret_plot_base_var_0), ($ferret_plot_base_var_1), xcoord, ycoord

   ELSE
      VECTOR/OVER/NOLAB($qualifiers)/TITLE="($ferret_plot_title)"/XSKIP=`xs`/YSKIP=`ys` \
       ($ferret_vector_1), ($ferret_vector_2)

   ENDIF

   IF `($ferret_vector_style"1|default>1|*>0") GT 0` THEN  
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels) = Vector xskip=`xs`,yskip=`ys`
   ENDIF

ELSE
   IF ($native_curvilinear_xy"0") THEN 
      VECTOR/OVER/NOLAB($qualifiers)/TITLE="($ferret_plot_title)" \
      ($ferret_plot_base_var_0), ($ferret_plot_base_var_1), xcoord, ycoord

   ELSE
   VECTOR/OVER/NOLAB($qualifiers)/TITLE="($ferret_plot_title)" ($ferret_vector_1), ($ferret_vector_2)

   ENDIF
   IF `($ferret_vector_style"1|default>1|*>0") GT 0` THEN  
      DEFINE SYMBOL n_left_labels = `($n_left_labels"0")+1`
      DEFINE SYMBOL upper_left_($n_left_labels) = Automatic vector xskip,yskip 
   ENDIF
ENDIF

! Overall header at the very top
GO LAS_ferret_las_version_header
IF ($needs_url"0|*>1") THEN \
LABEL/NOUSER `($ppl$xlen)*.33`, `($ppl$ylen)+1.`, -1, 0, 0.10, "DATA SET: @AS($data_var_url)"

! Overlay additional cartography data (land mask, outlines, rivers, ...)
! For land outline, if the region is smaller than 60 degrees in x or 30 degrees in y,
! use the more-detailed land outline script land_detail.jnl

IF ($ferret_land_type"1|none>0|contour>0|filled>1|default>1|*>0") THEN
  GO LAS_fland 
ELIF ($ferret_land_type"0|none>0|contour>1|filled>0|*>0") THEN
  IF `( ($region_x_range"360") LT 60) OR ( ($region_y_range) LT 30)` THEN
     GO land_detail ($ferret_land_arg)
  ELSE
     GO land  ($ferret_land_arg)
  ENDIF
ENDIF

go labels_above_plot

! Mark grid points. Options are no, subsample (tests if subsampling needed)
! or all to force marking all points.

IF ($ferret_mark_grid"0|no>0|all>1|subsample>1|*>0") THEN GO mark_grid

! Save the results
GO LAS_results box

! End of $RCSfile -----------------------------------------------------

