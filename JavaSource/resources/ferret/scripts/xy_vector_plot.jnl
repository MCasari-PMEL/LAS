!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: xy_plot_2D.jnl,v $
! 
! $Author: ansley $
! $Date: 2006/10/05 $
!
! 3/7/2007: ACM. Put code to check that the region was not too small, resulting 
!           in degenerate plot into LAS_initialize_data.jnl rather than in the 
!           individual plot scripts.
! 3/29/2007 ACM Fix logic interpreting contents of ferret_interpolate_data 
!
! xy_vector_plot.jnl creates an XY vector plot for use with the Back End Server
! code that sits behind a Live Access Server (LAS). 
!
! NOTE:  This code handles overlays but not differencing.
!
! Set any Ferret modes
!
! NOTE:  Should we support any of the following Ferrt modes?
! NOTE:    ASCII_FONT, CALENDAR, DEPTH_LABEL, LABELS, LATIT_LABEL, LONG_LABEL 

IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL fview = `UPCASE("($ferret_view)")`

! Define symbols associated with the regign and data and perform any
! initialization needed for this dataset.
!
! NOTE:  Adding support for a 'base layer' variable would mean passing
! NOTE:  some other argument.  For now, though, we always use 'data_0'
! NOTE:  for the base layer and region.

GO LAS_initialize_region 0
GO ($ferret_init_script"LAS_initialize_data|*>*") 0
DEFINE SYMBOL ferret_vector_1 ($ferret_plot_var)
DEFINE SYMBOL ferret_plot_title "($data_title) (($data_units))"

GO ($ferret_init_script"LAS_initialize_data|*>*") 1
DEFINE SYMBOL ferret_vector_2 ($ferret_plot_var)
DEFINE SYMBOL ferret_plot_title "($ferret_plot_title), ($data_title) (($data_units))"


! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small; reduces to a single grid point.
! (Region is a point)
GO LAS_check_2d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Open the window, apply size, scale options
GO LAS_open_window

! TODO:  Should the creation of the ($qualifiers) symbol for standard
! TODO:  2D plots be separated out in a separate script called:
! TODO:    LAS_2D_plot_qualifiers.jnl
!
! Build up the plot qualifiers

IF ($ferret_plot_key"0|0|nokey>0|*>1") THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/NOKEY
ENDIF

IF ($ferret_use_graticules"1|0|*>1) THEN
  SET MODE GRATICULE:(DASH,COLOR=black)
ENDIF


! Vectoro length scale

IF `($ferret_vector_length"0") GT 0` THEN  
   DEFINE SYMBOL qualifiers = ($qualifiers)/LENGTH=($ferret_vector_length)
ENDIF

! Vector xskip and yskip

IF ($ferret_vector_skip"0|*>1") THEN
   LET comloc = `STRINDEX("($ferret_vector_skip)", ",")`
   LET nlen = `STRLEN("($ferret_vector_skip)")`
   IF `comloc GT 1` THEN
       DEFINE SYMBOL xskp = `SUBSTRING ("($ferret_vector_skip)", 1, comloc-1)`
       DEFINE SYMBOL yskp = `SUBSTRING ("($ferret_vector_skip)", comloc+1, nlen-comloc)`
    ELSE
       DEFINE SYMBOL xskp = ($ferret_vector_skip)
       DEFINE SYMBOL yskp = ($ferret_vector_skip)
    ENDIF

    LET xs = STRFLOAT ("($xskp)")
    LET ys = STRFLOAT ("($yskp)")

    IF ($error_status"0|*>1") THEN
       MESSAGE/ERROR **ERROR format of vector skip is xskip,yskip
       EXIT/PROGRAM
    ENDIF

    DEFINE SYMBOL qualifiers = ($qualifiers)/XSKIP=`xs`/YSKIP=`ys`

ENDIF

! Set up for degrees-minutes-seconds labels on axes with units of degrees

IF ($ferret_deg_min_sec"0|false>0|*>1") THEN GO LAS_set_deg_min_sec.jnl

! Do the plot

VECTOR($qualifiers)/TITLE="($ferret_plot_title)"/SET ($ferret_vector_1), ($ferret_vector_2)
   IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
   IF ($yform_dms"0|*>1") THEN PPL YFOR (($yform_dms))

PPL VECTOR

LABEL/NOUSER `($ppl$xlen)/2`, `($ppl$ylen)+1.2`, 0, 0, 0.12, "LAS 7.+/Ferret ($FERRET_VERSION)     NOAA/PMEL"
IF ($needs_url"0|*>1") THEN \
LABEL/NOUSER `($ppl$xlen)*.33`, `($ppl$ylen)+1.`, -1, 0, 0.10, "DATA SET: @AS($data_var_url)"

! Overlay additional data

IF `($data_count"0") GT 1` THEN

REPEAT/range=2:`($data_count)-1`/NAME=m (  \
  GO LAS_initialize_region `m` ;  \
  GO ($ferret_init_script"LAS_initialize_data|*>*")  `m` ; \
  DEFINE SYMBOL qualifiers_over = /LINE=`m` ;\
  CONTOUR/OVER($qualifiers_over) ($ferret_plot_var) \
)

! Overlay additional cartography data (land mask, outlines, rivers, ...)

IF ($ferret_land_type"1|none>0|contour>0|filled>1|default>1|*>0") THEN
  GO LAS_fland 
ELIF ($ferret_land_type"0|none>0|contour>1|filled>0|*>0") THEN
  GO land 
ENDIF

! Mark grid points. Options are no, subsample (tests if subsampling needed)
! or all to force marking all points.

IF ($ferret_mark_grid"0|no>0|all>1|subsample>1|*>0") THEN GO mark_grid

! Save the results
GO LAS_results box

! End of $RCSfile -----------------------------------------------------

