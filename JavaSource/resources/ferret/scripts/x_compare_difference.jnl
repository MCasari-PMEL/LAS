!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! $RCSfile: x_compare_difference.jnl,v $
! $Revision: 1.6 $
! 
! $Author: ansley $
! $Date: 2007/02/02 $
! 3/7/2007: ACM. Put code to check that the region was not too small, resulting 
!           in degenerate plot into LAS_initialize_data.jnl rather than in the 
!           individual plot scripts.
! 3/29/2007 ACM Fix logic interpreting contents of ferret_interpolate_data 
! 11/9/2007 ACM If ferret_dep_axis_scale is defined, apply vertical axis scaling.
!
! x_compare_difference.jnl creates an X line plot for use with the Back 
! End Server code that sits behind a Live Access Server (LAS). 
! It produces a difference plot of 2 variables which are on the same
! x grid
!
! Set any Ferret modes

IF ($ferret_interpolate_data%0|false>0|true>1|1|0|*>1%) THEN SET MODE INTERPOLATE

DEFINE SYMBOL fview = `UPCASE("($ferret_view)")`

! Define symbols associated with the region and data and perform any
! initialization needed for this dataset.

GO LAS_initialize_region 0
GO LAS_initialize_data 0

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small; reduces to a single grid point.
! (Region is a point)
GO LAS_check_1d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Here is variable 0 and its title

DEFINE SYMBOL ferret_var_0 = ($ferret_plot_var)
DEFINE SYMBOL ferret_title_0 = ($ferret_plot_title"($data_var)")

! Initialize next dataset. 
! Assume the interface has returned correct, matching regions in X

! Apply any expression only to the first variable not to this one.
CANCEL SYMBOL ferret_expression 

GO LAS_initialize_region 1
GO LAS_initialize_data 1

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Check whether the region is too small; reduces to a single grid point.
! (Region is a point)
GO LAS_check_1d_region

! Check for errors 
IF ($error_status"0|ERROR>1") THEN
  MESSAGE/ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! Here is variable 1 and its title

DEFINE SYMBOL ferret_var_1 = ($ferret_plot_var)
DEFINE SYMBOL ferret_title_1 = ($ferret_plot_title"($data_var)")

! Compare the X axes of the two variables. They must be the same.

GO LAS_compare_axes "($data_0_var)[d=($data_0_num)]" "($data_1_var)[d=($data_1_num)]" ($fview)

! Check for errors 

IF ($error_status"0|*>1") THEN   
   DEFINE SYMBOL error_string = ($fview) grid of ($data_1_var) must match ($fview) grid of ($data_0_var)
   MESSAGE/ERROR **ERROR ($error_string)
   EXIT/PROGRAM
ENDIF

! Define the difference variable 

DEFINE SYMBOL ferret_diff_var = ($ferret_var_0)  -  ($ferret_var_1)

! Set the title, and the labels for the upper left listing the locations
! and time in directions normal to the plot.

GO LAS_set_diff_labels ($fview)

! If the user gave dependent axis scaling, 
! 1) remove any surrounding parentheses
! 2) check for third argument and add it if needed.

IF ($ferret_dep_axis_scale"0|*>1") THEN

  IF `STRINDEX("($ferret_dep_axis_scale)", "(") GT 0` THEN
     LET paren = `STRINDEX("($ferret_dep_axis_scale)", "(")`
     LET slen = `STRLEN("($ferret_dep_axis_scale)")`
     DEFINE SYMBOL ferret_dep_axis_scale = `SUBSTRING("($ferret_dep_axis_scale)", paren+1, slen-1)`
     
     LET paren = `STRINDEX("($ferret_dep_axis_scale)", ")")`
     LET slen = `STRLEN("($ferret_dep_axis_scale)")`
     DEFINE SYMBOL ferret_dep_axis_scale = `SUBSTRING("($ferret_dep_axis_scale)", 1, slen-1)`
  ENDIF

  LET slen = STRLEN("($ferret_dep_axis_scale)")
  LET nc = 0
  REPEAT/RANGE=2:`slen`/NAME=m (let c = `m`; \
     DEFINE SYMBOL next = `SUBSTRING("($ferret_dep_axis_scale)", c, 1)`; sh sym next; \
     IF `STRCMP("($next)", ",") EQ 0` THEN let nc = `nc+1`)

 IF `nc EQ 1` THEN   
     LET c = STRINDEX ("($ferret_dep_axis_scale)", ",")
     DEFINE SYMBOL lo_ax = `SUBSTRING("($ferret_dep_axis_scale)", 1, c-1)`
     DEFINE SYMBOL hi_ax = `SUBSTRING("($ferret_dep_axis_scale)", c+1, slen)`
     PPL %RANGE ($lo_ax), ($hi_ax), 10
     DEFINE SYMBOL ferret_dep_axis_scale = ($ferret_dep_axis_scale),($ppl$range_inc)
  ENDIF

ENDIF

! Open the window, apply size, scale options
GO LAS_open_window

! Define the plot variable

! TODO:  Should the creation of the ($qualifiers) symbol for standard
! TODO:  1D plots be separated out in a separate script called:
! TODO:    LAS_1D_plot_qualifiers.jnl
!
! Build up the plot qualifiers

IF ($ferret_use_graticules"1|0|*>1) THEN
  SET MODE GRATICULE:(DASH,COLOR=black)
ENDIF

! Line color

IF `($ferret_line_color%0|default>0|*>1%)` THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/COLOR=($ferret_line_color%0|default>black|red|blue|green|lightblue|purple|*>black%)
ENDIF

! symbols

IF `($ferret_line_or_sym%0|default>1|*>1%)` THEN
   DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_line_or_sym%/LINE|default>/LINE|line>/LINE|both>/LINE/SYM|sym>/SYM%)
ENDIF

! line thickness

IF `($ferret_line_thickness%0|default>1|*>1%)` THEN
   DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_line_thickness%/THICK=1|default>/THICK=1|1>/THICK=1|2>/THICK=2|3>/THICK=3|*>/THICK=1%)
ENDIF

! Set up for degrees-minutes-seconds labels on independent variable axis
! If this was not requested, the script just returns.

IF ($ferret_deg_min_sec"0|false>0|*>1") THEN GO LAS_set_deg_min_sec.jnl

! Do the plot. 

PLOT($qualifiers)/set/title = "($ferret_diff_title)" ($ferret_diff_var)
IF ($xform_dms"0|*>1") THEN PPL XFOR (($xform_dms))
IF `($labnum_y%0|*>1%)` THEN GO unlabel ($labnum_y)
IF `($labnum_z%0|*>1%)` THEN GO unlabel ($labnum_z)
IF `($labnum_t%0|*>1%)` THEN GO unlabel ($labnum_t)
IF `($title_size%0|*>1%)` THEN PPL LABSET ($title_size)
IF `($labnum_dset%0|*>1%)` THEN GO unlabel ($labnum_dset)
IF `($labnum_datitl%0|*>1%)` THEN GO unlabel ($labnum_datitl)
IF `($labnum_dods%0|*>1%)` THEN GO unlabel ($labnum_dods)
IF `($ferret_dep_axis_scale"0|*>1")` THEN PPL YAXIS ($ferret_dep_axis_scale)
PPL plot
LABEL/NOUSER `($ppl$xlen)/2`, `($ppl$ylen)+1.2`, 0, 0, 0.12, "LAS 7.+/Ferret ($FERRET_VERSION)     NOAA/PMEL"

! Put the labels defined for above the plot

GO labels_above_plot

! Save the results
GO LAS_results xline

! End of $RCSfile -----------------------------------------------------

