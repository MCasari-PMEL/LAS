!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! prop_prop_plot.jnl
! 
! $Author: ansley $
! $Date: 11-Jan-2008
!
! prop_prop_plot.jnl creates a property-property plot, using the 
! data variable in symbol data_0_var and the second variable from
! ferret symbol ferret_prop_var. Lets us do a property-property
! plot without being in 2-variable mode.

! This script assumes the usual symbols for a single-variable plot, plus 
!    ferret_prop_var   Second var for vertical axis of PLOT/VS plot
!    ferret_prop_url   Optional - URL for the 2nd var, if different from 
!                      data_0_url.
!
! The symbols for a line plot e.g. color, thickness might apply. Default
! values of these are assumed (black, thickness=2)

! TODO:
! * Check the variables for conformability. The script currently applies ($region_xyzt)
!      as computed for the first variable.
! * Input another symbol ferret_plot_symbol = either plot-point symbol codes
!      as understood by Ferret, or some other means of choosing the symbol
! * Allow for polygons instead of plot/vs


! Define symbols associated with the region and data and perform any
! initialization needed for this dataset.

GO LAS_initialize_region 0
GO LAS_initialize_data 0

! Check for errors (They often occur during dataset initialization.)

IF ($error_status"0|*>1") THEN
  MESSAGE/ERROR **ERROR ($error_string)
  EXIT/PROGRAM
ENDIF

! The second variable may be in a different dataset

IF ($ferret_prop_url"0|*>1") THEN 
   SET DATA "($ferret_prop_url)"
   DEFINE SYMBOL prop_num = `($ferret_prop_url),RETURN=dsetnum`
   IF ($error_status"0|*>1") THEN
     MESSAGE/ERROR "**ERROR dataset for second variable not opened"
     EXIT/PROGRAM
   ENDIF

ELSE
! The second variable is assumed to be in dataset 0 initialized above. Check this
! and define the second variable with the same region as the first.
   DEFINE SYMBOL prop_num = `($ferret_prop_var),RETURN=dsetnum`
   DEFINE SYMBOL prop_var = ($ferret_prop_var)[d=($prop_num),($region_xyzt)]
ENDIF

! Here is variable 0 title (not used)
!DEFINE SYMBOL ferret_title_0 = ($ferret_plot_title"($data_var)")

! Open the window, apply size, scale options
GO LAS_open_window

! TODO:  Should the creation of the ($qualifiers) symbol for standard
! TODO:  1D plots be separated out in a separate script called:
! TODO:    LAS_1D_plot_qualifiers.jnl
!
! Build up the plot qualifiers

IF ($ferret_use_graticules"0|0|*>1) THEN
  SET MODE GRATICULE:(DASH,COLOR=black)
ENDIF

! This is going to be a PLOT/VS plot.
DEFINE SYMBOL qualifiers = ($qualifiers)/VS

! Line color

IF `($ferret_line_color%0|default>0|*>1%)` THEN
  DEFINE SYMBOL qualifiers = ($qualifiers)/COLOR=($ferret_line_color%0|default>black|red|blue|green|lightblue|purple|*>black%)
ENDIF

! symbol type, default to 20, a triangle. This could be an option.

DEFINE SYMBOL qualifiers = ($qualifiers)/SYM=20

! line thickness, default to thick=2

IF `($ferret_line_thickness%0|default>1|*>1%)` THEN
   DEFINE SYMBOL qualifiers = ($qualifiers)($ferret_line_thickness%/THICK=1|default>/THICK=2|1>/THICK=1|2>/THICK=2|3>/THICK=3|*>/THICK=2%)
ELSE 
   DEFINE SYMBOL qualifiers = ($qualifiers)/THICK=2
ENDIF

! Set the plot argument. 

DEFINE SYMBOL plot_arg = ($ferret_plot_var), ($prop_var)

! Expand the horizontal and vertical scale a bit data is not on top of
! the plot axes.

STAT ($ferret_plot_var)
PPL %RANGE `($stat_min)`, `($stat_max)`, 10
DEFINE SYMBOL ferret_indep_axis_scale ($ppl$range_low),($ppl$range_high),($ppl$range_inc)

STAT ($prop_var)
PPL %RANGE `($stat_min)`, `($stat_max)`, 10
DEFINE SYMBOL ferret_dep_axis_scale ($ppl$range_low),($ppl$range_high),($ppl$range_inc)

! Draw the plot

PLOT($qualifiers)/NOKEY/SET/TITLE="($ferret_title_0)" ($plot_arg)
   IF `($ferret_indep_axis_scale"0|*>1")` THEN PPL XAXIS ($ferret_indep_axis_scale)
   IF `($ferret_dep_axis_scale"0|*>1")` THEN PPL YAXIS ($ferret_dep_axis_scale)
   IF `($labnum_dset%0|*>1%)` THEN GO unlabel ($labnum_dset)
   IF `($labnum_datitl%0|*>1%)` THEN GO unlabel ($labnum_datitl)
   IF `($labnum_dods%0|*>1%)` THEN GO unlabel ($labnum_dods)
PPL PLOT

GO LAS_url_label 0
IF `($n_right_labels"0") GT 0` THEN GO labels_above_plot

LABEL/NOUSER `($ppl$xlen)/2`, `($ppl$ylen)+1.2`, 0, 0, 0.12, "LAS 7.+/Ferret ($FERRET_VERSION)     NOAA/PMEL"

! Save the results

GO LAS_results box

! End of $RCSfile -----------------------------------------------------

