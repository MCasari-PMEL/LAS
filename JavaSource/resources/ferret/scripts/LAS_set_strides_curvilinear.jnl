
! From the region and the grid we can define striding. When there's going to be
! a transformation or something, then this might be better someplace else, or
! might be undone by the transform script.

show symbol operation_id
!IF `STRINDEX( "($operation_ID)", "Plot" ) EQ 0` THEN EXIT/SCRIPT
IF `STRINDEX( "($operation_service_action)", "Plot" ) EQ 0` THEN EXIT/SCRIPT

LET maxpix = 600  ! default
IF ($ferret_size"0|0.06667>1|*>0") THEN LET maxpix = 300  ! small
IF ($ferret_size"0|0.25>1|*>0")    THEN LET maxpix = 500  ! medium
IF ($ferret_size"0|0.5>1|*>0")     THEN LET maxpix = 600  ! default
IF ($ferret_size"0|0.8333>1|*>0")  THEN LET maxpix = 700  ! large

IF `($ferret_google_plot"0|*>1")` THEN LET  maxpix = `2*maxpix`

! For 2D plots
IF `STRLEN("($ferret_view)") EQ 2` THEN

! curvilinear grid? Get percentage of total that the region represents, see
! if the number of points looks like it will be large.

! Curvilinear and XY plot
   IF `($ferret_curvi_coord_lat"0|*>1") + ($ferret_view"|xy>1|*>0") EQ 2 ` THEN 
   
      IF ($region_x_lo"0|*>1") THEN 
         LET xx = x[gx=($data_var)[d=($data_num)]]
         LET nx = xx[i=@ngd]
         LET x_pct = ($region_x_range)/360
         LET pixsize = `nx*x_pct`
!         LET pixsize = `MAX(pixsize, nx)`  ! comment this out to set strides by region

         IF `pixsize GT maxpix` THEN SET AXIS/STRIDE=`INT((pixsize/maxpix)+1)` `($data_var)[d=($data_num)],return=xaxis`
         IF `pixsize GT maxpix` THEN SAY SET AXIS/STRIDE=`INT((pixsize/maxpix)+1)` `($data_var)[d=($data_num)],return=xaxis`
      ENDIF

      IF ($region_y_lo"0|*>1") THEN 
         LET yy = y[gy=($data_var)[d=($data_num)]]
         LET ny = yy[j=@ngd]
         LET y_pct = ($region_y_range)/180
         LET pixsize = `ny*y_pct`

!         LET pixsize = `MAX(pixsize, ny)`  ! comment this out to set strides by region

         IF `pixsize GT maxpix` THEN SET AXIS/STRIDE=`2*INT((pixsize/maxpix)+1)` `($data_var)[d=($data_num)],return=yaxis`
         IF `pixsize GT maxpix` THEN SAY SET AXIS/STRIDE=`2*INT((pixsize/maxpix)+1)` `($data_var)[d=($data_num)],return=yaxis`
      ENDIF

! Curvilinear and some other view than xy

   ELIF `($ferret_curvi_coord_lat"0|*>1") AND (STRINDEX("($ferret_view)", "x") + STRINDEX("($ferret_view)", "y") GT 0) ` THEN

        IF `(STRINDEX("($ferret_view)", "x") GT 0)` THEN 
           LET xx = x[gx=($data_var)[d=($data_num)]]
           LET nx = xx[i=@ngd]
           LET x_pct = ($region_x_range)/360
           LET pixsize = `nx*x_pct`
!           LET pixsize = `MAX(pixsize, nx)`  ! comment this out to set strides by region
  
           IF `pixsize GT maxpix` THEN SET AXIS/STRIDE=`INT((pixsize/maxpix)+1)` `($data_var)[d=($data_num)],return=xaxis`
        ENDIF
  
        IF `(STRINDEX("($ferret_view)", "y") GT 0)` THEN 
           LET yy = y[gy=($data_var)[d=($data_num)]]
           LET ny = yy[j=@ngd]
           LET y_pct = ($region_y_range)/180
           LET pixsize = `ny*y_pct`
!           LET pixsize = `MAX(pixsize, ny)`  ! comment this out to set strides by region
  
           IF `pixsize GT maxpix` THEN SET AXIS/STRIDE=`INT((pixsize/maxpix)+1)` `($data_var)[d=($data_num)],return=yaxis`
        ENDIF
  
        IF ($region_z_lo"0|*>1") THEN 
           LET zz = z[gz=($data_var)[d=($data_num)],($region_z)]
           LET nz = zz[k=@ngd]
           LET pixsize = `MAX(pixsize, nz)`
           IF `nz GT maxpix` THEN SET AXIS/STRIDE=`2*INT((nz/maxpix)+1)` `($data_var)[d=($data_num)],return=zaxis`
        ENDIF
        
        IF ($region_t_lo"0|*>1") THEN     
           LET nt = tt[l=@ngd]
           LET tt = t[gt=($data_var)[d=($data_num)],($region_t)]
           LET pixsize = `MAX(pixsize, nt)`
           IF `nt GT maxpix` THEN SET AXIS/STRIDE=`2*INT((nt/maxpix)+1)` `($data_var)[d=($data_num)],return=taxis`
        ENDIF
  
   ENDIF 

ENDIF  ! 2D plots

! For 1D plots
IF `STRLEN("($ferret_view)") EQ 1` THEN
   LET pixsize = 1
   IF ($region_x_lo"0|*>1") THEN 
      LET xx = x[gx=($data_var)[d=($data_num)],($region_x)]
      LET nx = xx[i=@ngd]
      LET pixsize = `MAX(pixsize, nx)`
   ENDIF
   IF ($region_y_lo"0|*>1") THEN 
      LET yy = y[gy=($data_var)[d=($data_num)],($region_y)]
      LET ny = yy[j=@ngd]
      LET pixsize = `MAX(pixsize, ny)`
   ENDIF
   IF ($region_z_lo"0|*>1") THEN 
      LET zz = z[gz=($data_var)[d=($data_num)],($region_z)]
      LET nz = zz[k=@ngd]
      LET pixsize = `MAX(pixsize, nz)`
   ENDIF
   IF ($region_t_lo"0|*>1") THEN 
      LET nt = tt[l=@ngd]
      LET tt = t[gt=($data_var)[d=($data_num)],($region_t)]
      LET pixsize = `MAX(pixsize, nt)`
   ENDIF
   
   IF `pixsize GT maxpix` THEN

      IF ($region_x_lo"0|*>1") THEN \
         IF `nx GT maxpix` THEN SET AXIS/STRIDE=`2*INT((nx/maxpix)+1)` `($data_var)[d=($data_num)],return=xaxis`

      IF ($region_y_lo"0|*>1") THEN \
         IF `ny GT maxpix` THEN SET AXIS/STRIDE=`2*INT((ny/maxpix)+1)` `($data_var)[d=($data_num)],return=yaxis`

      IF ($region_z_lo"0|*>1") THEN  \
         IF `nz GT maxpix` THEN SET AXIS/STRIDE=`2*INT((nz/maxpix)+1)` `($data_var)[d=($data_num)],return=zaxis`

      IF ($region_t_lo"0|*>1") THEN  \
         IF `nt GT maxpix` THEN SET AXIS/STRIDE=`2*INT((nt/maxpix)+1)` `($data_var)[d=($data_num)],return=taxis`
   ENDIF

ENDIF ! 1D plots

! End of $RCSfile ------------LAS_set_strides.jnl--------------------------


